*comment ////////////////////////////////////////////////////////////
*comment // Layer 1
*comment ////////////////////////////////////////////////////////////

*label layer_1

*set diary ("I just embarked on a trip inside the tunnel. Let's see how this goes.[n/][n/]" & diary)

*comment New variable name: layer_1_kiwi_sword_seen
*if ((months_passed >= 215) and (not (layer_1_kiwi_sword_seen)))
    *set diary ("Um... I just suddenly started running somewhere. I don't even know why I originally came here. Should... should I be scared?[n/][n/]" & diary)
    
    You start your trip with some places to go in mind, but it all washes away. Why were you here again? Eh, you forget. Suddenly, you feel a wave of energy.
    
    *set energy max_energy
    [Energy Refreshed]
    
    Then you start running. To where? Who knows. You'll find out shortly. You smile. You don't know why, but you smile. Wherever you're going, you'll be happy. Wherever you're going, there will be no more pain.
    
    *goto layer_1_kiwi

*temp affirmation 0
*rand affirmation 1 16

*if (layer_1_base_built_affirmation_station)
    *if (affirmation = 1)
        You are beautiful, and will be beautiful no matter how you look on the outside.
    *if (affirmation = 2)
        You don't need to complete anything to have self-worth. Self worth is who you are, not what you do.
    *if (affirmation = 3)
        You are loved.
    *if (affirmation = 4)
        People like to see you.
    *if (affirmation = 5)
        You have choices in what you do. You can choose things that make you happy.
    *if (affirmation = 6)
        You are more than your emotions.
    *if (affirmation = 7)
        You are never alone… you always have yourself.
    *if (affirmation = 8)
        Your differences are quirks, not weirds.
    *if (affirmation = 9)
        Fuck the goobledeegockers.
    *if (affirmation = 10)
        When dogs wag their tail at you, that's genuine.
    *if (affirmation = 11)
        Red hair best hair.
    *if (affirmation = 12)
        I could never write enough affirmations for you.
    *if (affirmation = 13)
        You deserve what you have.
    *if (affirmation = 14)
        You've come so far.
    *if (affirmation = 15)
        Keep on going where you're going, cuz you're gonna go far.
    *if (affirmation = 16)
        It doesn't matter what people say, it matters what you DO.

*if (not (unlocked_support_self))
    *temp equipments_needed
    *temp fall_asleep 2

*if (skip_mode = "normal")
    What would you like to do in the first layer?

*gosub_scene logic update_total_lemens

*temp net_hint_garden_months months_passed
*set net_hint_garden_months - layer_1_hint_garden_last_visited

*temp equipments_needed layer_2_equipments_needed
*if (artifact_has_equipipen)
    *set equipments_needed - 1

*comment TODO: Skips
*choice
    #General exploration [+1 month, +4 experience]
        *goto layer_1_general_exploration
    #Fetch lemens [+1 month, +2 Experience, +${lemen_fetch_skill} Lemens, +1 Lemen fetch skill]
        *set diary ("Today's goal is simple: Fetch lemens.[n/][n/]" & diary)
        
        *set achievement_lemen_lover_var_is_gathering_lemens true
        *gosub_scene logic month_passed
        *set achievement_lemen_lover_var_is_gathering_lemens false
        
        [+2 Experience]
        *set experience + 2
        
        *goto layer_1_gather_lemens
    *if (mapped_layer_1_gather_rosalia) #Gather rosalia [+1 month, +2 Experience, +${rosalia_fetch_skill} Rosalia, +1 Rosalia fetch skill]
        *set diary ("It's not every day you get to smell the sweet smell of Rosalia, so I'm gonna go fetch some.[n/][n/]" & diary)
        
        *gosub_scene logic month_passed
        
        [+2 Experience]
        *set experience + 2
        
        *goto layer_1_gather_rosalia
    *if (((knowledge_rosalia) and (not (mapped_layer_1_gather_rosalia))) and (unlocked_search_for_rosalia))
        #Search for a Rosalia patch [+2 months]
            *set diary ("Found a rosalia patch all by myself. Feelin' kinda baller, not gonna lie.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            *gosub_scene logic month_passed
            
            [Mapped Location]
            *set mapped_layer_1_gather_rosalia true
            
            You successfully find a rosalia patch, but it takes a good bit of searching.
            
            *goto_scene {home_base_scene} {home_base_label}
    *if (mapped_layer_1_blue_coin)
        #Return to the place you found the blue coins [+1 Month]
            *set diary ("Returning to the place with the Blue Coin stash...[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            *goto layer_1_blue_coin
    *if (mapped_layer_1_kiwi)
        #Return to the place you named ${layer_1_kiwi_map_name} [+1 Month]
            *set diary ("Going on a mission back to this one place. Who even remembers where.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            *goto layer_1_kiwi
    *if (mapped_layer_1_materials)
        #Return to the place where you found the building materials [+1 Month]
            *set diary ("Headed straight to Lowes! Or, errr, that patch of building materials I found.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            *goto layer_1_materials
    *if (mapped_layer_1_ghost)
        #Return to "Running Stick River" [+1 Month]
            *set diary ("Headed straight to the River Sticks! No wait, that's somewhere else.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            
            *goto layer_1_ghost
    *if (layer_1_base_built_marker)
        #Return to the base you've been building [+1 Month]
            *set diary ("I'm so excited to work on my treehouse... or, err, I mean very official layer one base.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            
            *goto layer_1_base
    *if (mapped_layer_1_keys)
        #Return to the key shop [+1 Month]
            *set diary ("Let's see what the key shop has in store for us.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            
            *goto layer_1_keys
    *if (researched_tracking_1)
        *selectable_if (energy >= 12) #Tracking! [+3 Months, -4 Energy, Requires at least 12 Energy]
            *set diary ("It's time for a hunt![n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            *gosub_scene logic month_passed
            *gosub_scene logic month_passed
            
            [-4 Energy]
            *set energy - 4
            
            *goto layer_1_tracking
    *if (unlocked_hint_garden)
        *selectable_if (net_hint_garden_months >= 8) #Visit the Hint Garden for a hint of what you should do next [+1 Month, You can only direct travel there if it's been at least 8 months since your last visit, but you may still come across it in general exploration]
            *set diary ("I went straight to the hint garden.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            
            *goto layer_1_hint_garden
    *if (layer_1_num_general_explorations >= 7)
        *selectable_if (total_lemens >= 8) #Pay a cartographer for directions to somewhere specific [+1 Month, -8 Lemens]
            *set diary ("I felt determined today, so I went straight toward this one place rather than dilly dallying wandering around.[n/][n/]" & diary)
            
            *gosub_scene logic month_passed
            
            [-8 Lemens]
            *set logic_lemens_to_remove 8
            *gosub_scene logic remove_lemens
            
            *goto layer_1_cartographer
    *if (unlocked_level_up)
        *selectable_if ((((pass_unlocked_blue) and (inventory_equipments >= equipments_needed)) and (total_lemens >= 2)) and (inventory_rosalia >= 1)) #Go to layer two [+1 Month, -${equipments_needed} Equipments, -2 Lemens, -1 Rosalia, Requires Blue Pass]
            *set diary ("Onto layer 2![n/][n/]" & diary)
            
            *goto layer_1_layer_2_pass_eval

*label layer_1_layer_2_pass_eval

*if (pass_lost_blue_from_cave)
    *goto layer_1_layer_2_pass_lost

*gosub_scene logic month_passed

[-${equipments_needed} Equipments]
*line_break
[-2 Lemens]
*line_break
[-1 Rosalia]
*set inventory_equipments - equipments_needed
*set logic_lemens_to_remove 2
*gosub_scene logic remove_lemens
*set inventory_rosalia - 1

*goto layer_2

*label layer_1_layer_2_pass_lost

*set diary ("Oh, um, you see, this is awkward. I lost my Blue Pass and the guards didn't actually let me enter Layer 2. I had to head all the way back without a refund...[n/][n/]" & diary)

Unfortunately, the guards won't let you in because you've still lost your blue pass. You need to see George (or somewhere else if you're no longer at the orphanage) for a new copy. You return home defeated with nothing to show for it.

*if (george_gone)
    *set diary ("Just thought of George again. Am I ever going to forget him?[n/][n/]" & diary)
    
    Wait, did you just think of seeing George for a new copy? Get it together Lauren, he's gone.

*goto {home_base_scene} {home_base_label}

*label layer_1_cartographer

Could you describe the place you'd like to go?

*choice
    #"Uh, this one cave, it looks like it's rising out of the ground or something?"
        *goto layer_1_cave
    #"There was, like, this one egg-shaped pokemon there, uh, do you know pokemon even? You look kinda old."
        *goto layer_1_pokemon
    #"I saw this weird tree machine thing. Do you know where that is?"
        *goto layer_1_henry
    *if (knowledge_blue_coins)
        #"I've heard of this stash of blue coins. Not sure if you even know what a blue coin is, but yeah."
            *goto layer_1_blue_coin
    #"You know anywhere that would be a good place to build a base?"
        *goto layer_1_base
    #"Is there anywhere around here that sells keys?"
        *goto layer_1_keys

*label layer_1_general_exploration

*set diary ("Adventure time![n/][n/]" & diary)

*gosub_scene logic month_passed

[+4 Experience]
*set experience + 4

*set layer_1_num_general_explorations + 1

*if (skip_mode = "normal")
    You go exploring in layer 1.
    *if (ghost_mode = "ghost")
        (But as a ghost… spooky).
    
*if ((ghost_mode = "ghost") and (layer_2_portal_enter_visits >= 3))
    Since you're a ghost, you can choose which encounter to go to with your ghost navigation (unlocked after three ghostly journeys). The ghost navigation only speaks in a single word though. Where would you like to head?
    
    *choice
        #Somewhere random
            *goto layer_1_redirect
        #Cave
            *goto layer_1_cave
        #Lemen
            *goto layer_1_lemen_encounter
        #Pokemon
            *goto layer_1_pokemon
        #Rosalia
            *goto layer_1_rosalia_encounter
        #Tso
            *goto layer_1_tso
        #Henry
            *goto layer_1_henry
        #Note
            *goto layer_1_gift
        #Dagger
            *goto layer_1_dagger
        #Backpack
            *goto layer_1_backpack
        #Bluecoin
            *goto layer_1_blue_coin
        #Gravestone
            *goto layer_1_gravestone
        #Base
            *goto layer_1_base
        #Keys
            *goto layer_1_keys
        #Scrolls
            *goto layer_1_scrolls
        #Kiwi
            *goto layer_1_kiwi
        #Tim
            *goto layer_1_ghost
        #Materials
            *goto layer_1_materials
        #Button
            *goto layer_1_button
        #Starblossom
            *goto layer_1_starblossom
        #Hint
            *goto layer_1_hint_garden
        *if (researched_exploration_1)
            #Dragon
                *goto layer_1_dragon
        *if (researched_exploration_1)
            #Overhang
                *goto layer_1_overhang
        *if (researched_exploration_1)
            #Colosseum
                *goto layer_1_colosseum
        *if (researched_exploration_1)
            #Shrine
                *goto layer_1_shrine_of_good
        *if (researched_exploration_1)
            #Training
                *goto layer_1_training

*temp encounter 0
*temp num_encounters 20
*if (researched_exploration_1)
    *set num_encounters + 5
*if (researched_exploration_2)
    *set num_encounters + 5
*rand encounter 1 num_encounters

*page_break

*label layer_1_redirect

*if (encounter = 1)
    *goto layer_1_cave
*if (encounter = 2)
    *goto layer_1_lemen_encounter
*if (encounter = 3)
    *goto layer_1_pokemon
*if (encounter = 4)
    *goto layer_1_rosalia_encounter
*if (encounter = 5)
    *goto layer_1_tso
*if (encounter = 6)
    *goto layer_1_henry
*if (encounter = 7)
    *goto layer_1_gift
*if (encounter = 8)
    *goto layer_1_dagger
*if (encounter = 9)
    *goto layer_1_backpack
*if (encounter = 10)
    *goto layer_1_blue_coin
*if (encounter = 11)
    *goto layer_1_gravestone
*if (encounter = 12)
    *goto layer_1_base
*if (encounter = 13)
    *goto layer_1_keys
*if (encounter = 14)
    *goto layer_1_scrolls
*if (encounter = 15)
    *goto layer_1_kiwi
*if (encounter = 16)
    *goto layer_1_ghost
*if (encounter = 17)
    *goto layer_1_materials
*if (encounter = 18)
    *goto layer_1_button
*if (encounter = 19)
    *goto layer_1_starblossom
*if (encounter = 20)
    *goto layer_1_hint_garden
*if (encounter = 21)
    *goto layer_1_dragon
*if (encounter = 22)
    *goto layer_1_overhang
*if (encounter = 23)
    *goto layer_1_colosseum
*if (encounter = 24)
    *goto layer_1_shrine_of_good
*if (encounter = 25)
    *goto layer_1_training

*label layer_1_cave

*set layer_1_cave_visits + 1

*comment Unused label, just for organization
*label layer_1_cave_lake

*if (layer_1_cave_visits = 3)
    *set diary ("I looked at my reflection today and thought of my parents. I wonder what they were like.[n/][n/]" & diary)
    
    You're headed in the same direction as the cave again, but this time you take a detour through some woods and come across a serene lake.
    *if (not (ghost_mode = "ghost"))
        You can stare in and see your reflection. The orphanage doesn't have mirrors, so this is one of the few chances you get to see what you look like.
    *if (ghost_mode = "ghost")
        When you stare in, you see nothing. Oh right, you're a ghost. Still, though, you imagine that you see your face in the water for plot purposes.
    
    Seeing your face brings back vague memories of parents. You don't really know anything about them, but somewhere in the back of your head you must remember what they look like, and they looked like you.
    
    You get up and continue on your way.

*comment Unused label, just for organization
*label layer_1_cave_cave

*set layer_1_cave_cave_visits + 1

*if (layer_1_cave_cave_visits = 1)
    *set diary ("Ooh, an interesting cave.[n/][n/]" & diary)
    
    *if (skip_mode = "normal")
        You explore a few days and come across some rocks rising up from the ground. They form the entrance to a cave that seems to suck you into the ground. It's really dark inside.
*if (layer_1_cave_cave_visits >= 2)
    You find that one cave again, the one that looks like it's going to suck you up from the ground.
    
    *comment Check level 1
    *if (researched_observation_1)
        {Check: energy >= 6}

        *if (energy < 6)
            {Fail}
        *if (energy >= 6)
            {Pass}
    
    *if (energy >= 6)
        This time, though, since you're somewhat rested and visited this place before, you are familiar enough with it to spot some doubloons on the ground.
        
        [+8 Doubloons]
        *set doubloons + 8

*comment Required to see kiwi sword first
*if ((ghost_mode = "normal") and ((layer_1_kiwi_sword_seen) and (not (layer_1_cave_sword_seen))))
    You see a that sword in the stone, except now it's sticking out from the cave's entrance rather than just a stone on the ground. Did… someone move it?…
    *set layer_1_cave_sword_seen true

*if (skip_mode = "skip")
    *if (decision_point = "4")
        *set decision_point "5"
        *goto layer_1_cave_top
*choice
    *if (ghost_mode = "normal")
        *selectable_if (inventory_torch > 0) #Use a torch to light up the cave and head inside [-1 Torch]
            *set inventory_torch - 1
            *goto layer_1_cave_inside
    *if (ghost_mode = "ghost")
        #Straight up head inside the cave because you're a ghost and ghosts have night vision anyways.
            *goto layer_1_cave_inside
    #Explore around the cave
        *goto layer_1_cave_around
    *selectable_if (energy >= 11) #Explore on top of the cave [Requires 11 Energy]
        *goto layer_1_cave_top

*label layer_1_cave_top

*if (met_amy)
    You climb up top again, but Amy isn't here anymore. However, there are some building materials for you to collect.
    
    [+2 Building Materials]
    *set inventory_building_materials + 2
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*set met_amy true
*if (ghost_mode = "ghost")
    *set met_amy_as_ghost true

*set diary ("I met a vampire today and lived to tell the tale.[n/][n/]" & diary)

*if (skip_mode = "normal")
    From here, you can make out the tall buildings in the city far outside of town. Hopefully, you'll go to the city one day, so that you can earn more money for the orphanage. But first, you need to be able to support yourself and such.

    "Hiya," says a voice from behind you. "I'm Amy." You stare at her, a young woman with sleek black hair and emerald green eyes. Who is this person? "I'm having a party in the third layer if you want to join."

    "I don't think I can make it," you say, confused.

    *if (ghost_mode = "normal")
        "Oh, don't worry, human, us vampires party for long times."

        Ohmygosh a vampire, is she going to eat you?
    *if (ghost_mode = "ghost")
        "Oh, don't worry, ghost, us vampires party for long times. Besides, I hear ghosts stick around a while too, eh?"

        You hesitate a bit.

        "Ohhhh, you're a portal ghost. Well, humans are invited too, in case you ever manage to change back. Though I like your ghost form, kinda cute."

    "Make sure to invite your friends. But mostly the ones you want to dispose of.
    *if (ghost_mode = "ghost")
        Or anyone you want to join you as a ghost (wink).
    And be sure to have a bottle of blood wine as a gift if you do go! Anyways, I must go, but first I will give you this starblossom as a gift."

[+1 Starblossom]
*set inventory_starblossom + 1

*if (not (knowledge_starblossom))
    She even explains what a starblossom is to you after seeing your confused look.
    
    [Encyclopedia Entry: Starblossoms]
    *set knowledge_starblossom true

*if (skip_mode = "normal")
    You return home thinking about the weirdness that just occurred.

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_around

*if (layer_1_cave_around_petunal_picked)
    *goto layer_1_cave_around_picked

*if (knowledge_petunal_blossoms)
    Oh some flowers! Upon closer examination, they're Petunal Blossoms. You're able to pick a few up and head back.
    
    [+3 Petunal Blossoms]
    *set layer_1_cave_around_petunal_picked true
    *set inventory_petunal_blossoms + 3
*if (not (knowledge_petunal_blossoms))
    *set layer_1_cave_around_petunal_times_missed + 1
    
    You find nothing but a few useless purple flowers with yellow specks, so you return home. If only you knew some botany you might be able to identify them better.
    
    *if ((layer_1_cave_around_petunal_times_missed >= 2) and (((home_base_scene = "orphanage") and (home_base_label = "orphanage_home_base")) and  (ghost_mode = "normal")))
        You finally decide to ask the orphanage botanist about these sorts of flowers, so you tell her about them when you go back. Apparently, they're petunal blossoms, which can actually be gathered for money. Should have picked them up!
        
        *if (researched_observation_1)
            {Check: Energy >= 6}

            *if (energy < 6)
                {Fail}
            *if (energy >= 6)
                {Pass}
        
        *if (energy >= 6)
            Then you reach into your pockets and realize that since you're pretty rested, you had the mental acuity to actually pick up the flowers anyways!
            
            [+3 Petunal Blossoms]
            *set layer_1_cave_around_petunal_picked true
            *set inventory_petunal_blossoms + 3
        
        *set knowledge_petunal_blossoms true
    
*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_around_picked

*if (artifact_has_ghost_orb_off_white)
    The tracks and petunal blossoms are both gone now.
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

The petunal blossoms are picked, but where they were before, you notice some tracks.
*if (researched_tracking_1)
    *goto layer_1_cave_tracks
If only your tracking skills were good enough you could hunt whatever animal left this.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_tracks

Do you hunt the beast?

*choice
    *selectable_if (energy >= 16) #Yes [+4 Months, -7 Energy, Requires 16 Energy]
        *if (ghost_mode = "ghost")
            You immediately tell something's off with this hunt. No, you're not supposed to be a ghost during it. You leave. (Don't worry, you don't lose energy or have any months pass because of this).

            *return
        
        *gosub_scene logic month_passed
        *gosub_scene logic month_passed
        *gosub_scene logic month_passed
        *gosub_scene logic month_passed
        
        [-7 Energy]
        *set energy - 7
        
        *page_break
        *goto layer_1_cave_tracks_hunt
    #No
        *if (ghost_mode = "ghost")
            *return
        *goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_tracks_hunt

*temp layer_1_tracking_question_1 0
*temp layer_1_tracking_question_2 0
*rand layer_1_tracking_question_1 1 30
*rand layer_1_tracking_question_2 1 30

You start the hunt!
*set tracking_answer_wrong false
*set hide_stats true

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

You feel a mild throbbing between your eyes. Nothing much.

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Weird to get the same question twice in a row.

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Uhh… Is this normal?

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

How many times do you have to answer this question?

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Do you have to answer it wrong?

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

No, you most certainly have to answer it correctly. At least the dialog is changing each time.

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

No, you most certainly have to answer it correctly. At least the dialog is changing each time.

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Did I get you?

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Okay, seriously, when will this end…

*page_break

*set tracking_question layer_1_tracking_question_1
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Okay, this next question better be different.

*page_break

How many times did I just ask you that question?

*temp guess
*input_number guess 1 100

*if (not (guess = 10))
    *goto layer_1_cave_tracks_hunt_failed
        
Good job. Your headache intensifies. Did you think this was over?

*page_break

*set tracking_question layer_1_tracking_question_2
*gosub layer_1_tracking_question
*if (tracking_answer_wrong)
    *goto layer_1_cave_tracks_hunt_failed

Your head throbs and your vision blacks out. The last thing you can make out is the chanting of ghosts before they suddenly hush and disappear.

*set artifact_has_ghost_orb_off_white true
[Artifact: Off-White Ghostly Orb]
*line_break
[Encyclopedia Entry: The Ghostly Orbs]
*set artifact_has_ghost_orb_off_white true
*set knowledge_ghostly_orbs true

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_tracks_hunt_failed

You failed. Oof

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_inside

*if (researched_observation_1)
    {Check: Energy < 1}

    *if (energy >= 1)
        {Fail}
    *if (energy < 1)
        {Pass}

*if ((energy = 0) and (not (ghost_mode = "ghost")))
    You're so tired you end up dropping the first torch. Darn it.
    
    *choice
        *selectable_if (inventory_torch > 0) #Get out another torch [-1 Torch]
            *set inventory_torch - 1
            *goto layer_1_cave_inside_energy_eval
        #Go back home
            *goto_scene {home_base_scene} {home_base_label}

*label layer_1_cave_inside_energy_eval

*if (pass_unlocked_blue)
    You search around inside the cave and find some special-looking dirt.
    
    [+2 Special Dirt]
    *set inventory_special_dirt + 2
    
    Then, you see something gleaming in the back of the cave.
    *goto layer_1_cave_inside_gleam

*if (researched_observation_1)
    {Check: Energy >= 6}

    *if (energy < 6)
        {Fail}
    *if (energy >= 6)
        {Pass}

*if (energy >= 6)
    *comment TODO: Option to wait to get it the proper way.
    
    *set diary ("[i]I achieved the Blue Pass.[/i] Wow, okay, just got a Blue Pass for free.[n/][n/]" & diary)
    
    You find a ruffled up blue pass in the corner. Someone must have dropped it. Still, it'll allow you to explore the second layer!

    *set pass_unlocked_blue true
    
    You also see something gleaming in the back of the cave.
*if (energy <= 5)
    You see something blue in the corner but don't care enough to check what it is since you're kinda tired. Instead, something gleaming in the back of the cave catches your eye.

*label layer_1_cave_inside_gleam

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (not (ghost_mode = "ghost"))
    It's a ruby. If only you had some way to extract it. Or if only you were a ghost! (Rubies often signify ghostly entrances). Oh well, so you head back and forget about it.
    *goto_scene {home_base_scene} {home_base_label}

*comment Unused; just for organization
*label layer_1_cave_ruby

It's a ruby! This must be a wall you can fly through. (Contrary to popular belief, ghosts can only fly through specially designed walls using ruby powers). So you fly right through to find a ghost banquet. Every seems to be enjoying a dead wooly mammoth ghost that was hunted down.

"Hey, it's a new one," Shouts a ghost from the far corner.

"Oh, I'm just passing by," you say.

"That's fine, just stay for a couple centuries, you don't have to be here for the whole millenia-long party!" Shouts back the same ghost.

"No, really, I'm just in temporary ghost form."

"Ohhhh, filthy human!"

"Hey, a ghost is a ghost, even if they got a human form back home they're taking care of." Says another ghost.

*if ((troy_orphaned_event_occurred) and (not (layer_1_cave_ruby_troy_parent_encounter)))
    *set layer_1_cave_ruby_troy_parent_encounter true
    
    *if (researched_observation_1)
        {Check: Saved Troy}

        *if (not (saved_troy_orphanage))
            {Fail}
        *if (saved_troy_orphanage)
            {Pass}
    
    *if (saved_troy_orphanage)
        A third and a fourth ghost gasp, on female and one male. "Are you, no, it can't be!" They rush over.

        "You're that kid who saved our child at the orphanage!" Says the male ghost.

        Ohmygosh, it's Troy's parents.

        "We saw you bringing out Dicky to take him in." Says the Troy's mom. "We didn't see what went on inside since we were trying to comfort him, but we saw that you were the one that brought Dicky out and saved our son."


        "How can we thank you?" Adds on Troy's dad.

        "Wait, I know," Says the Troy's mom, "we visit Troy in his dreams every know and then. Special ghost ability. We'll let him know something for the next time you see him! Just come back here afterward and let us know if you need anything else!"

        *set layer_1_cave_ruby_troy_parent_approval true
    *if (not (saved_troy_orphanage))
        A ghost couple in the corner gives you the side-eye. They look oddly like Troy.

You stay a bit to be polite, but it starts to feel kind of awkward. These are bona-fide ghosts, and you're just an impostor. So you head back.

*return

*label layer_1_lemen_encounter

*if (layer_1_lemen_encounter_duke_met)
    You don't see Duke this time around, but you're in the same area. You could head to his patch, which gives +6 more fetch skill rather than +3, but it costs 3 energy to get there, and you only pick extra lemens if you have at least 11 energy afterwards (so you have to have 14 energy now).
    
    Which patch do you go to?
    
    *choice
        #Your patch [+3 Lemen Fetch Skill]
            *goto layer_1_lemen_encounter_default
        *selectable_if (energy >= 3) #Duke's patch [-3 Energy, +6 Lemen Fetch Skill]
            [-3 Energy]
            *set energy - 3
            
            *goto layer_1_lemen_encounter_new

*comment Unused; just for organization
*label layer_1_lemen_encounter_duke

You pass a gray squirrel with the bushiest tail you've ever seen.
*if (ghost_mode = "ghost")
    It sees you and runs away. "Ahhh ghost!" It squeaks.
    *goto layer_1_lemen_encounter_default
    
*set layer_1_lemen_encounter_duke_visits + 1

*if (unlocked_squirrel_war)
    *set diary ("I met Duke Squirrel today.[n/][n/]" & diary)
    
    "Are you one of those head tribe squirrels?" You ask it.
    
    "Get away! Just when I thought humans were safe to talk to, at least!"
    
    "Sorry, sorry," you apologize. "So you're not in the squirrel war?"
    *if (squirrel_faction_unlocked_henry)
        You consider how a human, Henry, is involved in the war even, but this little squirrel want nothing to do with it.
    
    "No! Of course not! Silly political game for dukes and counts. Well, counts. My name is Duke Squirrel." And the squirrel holds out its paw. You shake it. "I just wanted to tell you about that lemen patch over there." And Duke gestures over yonder in an unspecified direction. "Much better! And it has my nut there too. If you happen over there just don't give it to any other squirrels, alright? It'd be awesome if you could deliver it to me, too, but no rush. It's a family heirloom nut, so it's pretty important to me!"
    
    *set layer_1_lemen_encounter_duke_met true
    
    You nod and continue on to the better lemen patch.
    
    *goto layer_1_lemen_encounter_new
*if ((layer_1_lemen_encounter_duke_visits = 1) and (not (unlocked_squirrel_war)))
    It chatters a lot, as squirrels do.
*if ((layer_1_lemen_encounter_duke_visits = 2) and (not (unlocked_squirrel_war)))
    This time, it seems quite mad at you. Or worried? Concerned? Like it's trying to redirect you. You don't really know what to make of it. You just walk around it as best as you can.
*if ((layer_1_lemen_encounter_duke_visits = 3) and (not (unlocked_squirrel_war)))
    *set diary ("I met Duke Squirrel today.[n/][n/]" & diary)
    
    "Man, what is up with this squirrel!" You mutter aloud.
    
    "Hey! I'm just tryna help you out here," snaps the squirrel back. Wait, squirrels can talk? You stop dead in your tracks. "Yeah, I'm talking to you!" It continues. "There's a better lemen patch just over that way, out in the clearing. Can't get to it myself because it's way in the open where all those birds will get me, but if you can collect the nut I left there, too, that'd be great."
    
    "Oh, uh, thanks." You say to the squirrel, dumbfounded.
    
    "By the way," it goes on, "don't involve me, Duke Squirrel, in any of those squirrel politics. I just wanted to make sure you knew that, too. I don't want the nut for loyalty reasons, it's just nostalgic. It's [i]my[/i] nut. Well, it was my grandma's before she passed away. Got it? Don't go giving it away to other squirrels.
    
    "What politics?" You ask. And then Duke Squirrel sighs and hands you an encyclopedia entry.
    
    [Encyclopedia Entry: Squirrel Wars]
    *set unlocked_squirrel_war true
    
    *set layer_1_lemen_encounter_duke_met true
    
    You briefly skim the article and stick it in your bag. Then you nod at Duke and walk to the new lemen patch.
    
    *goto layer_1_lemen_encounter_new

*label layer_1_lemen_encounter_default

You find a new lemen patch! You don't have any time to pick anything today, but this one's fresher and should give more lemens.

[+3 Lemen Fetch Skill]
*set lemen_fetch_skill + 3

*if (researched_observation_1)
    {Check: Energy >= 11}

    *if (not (energy >= 11))
        {Fail}
    *if (energy >= 11)
        {Pass}

*if (energy >= 11)
    While you start to head back, a sudden wave of energy from your well-rested state courses through you, so you decide to quickly pick some lemens, too.
    
    [+${lemen_fetch_skill} Lemens]
    *set inventory_lemens_13 + lemen_fetch_skill

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_lemen_encounter_new

You head over to Duke's fresher patch.

[+6 Lemen Fetch Skill]
*set lemen_fetch_skill + 6

*if (ghost_mode = "ghost")
    *set layer_1_lemen_encounter_new_left_ectoplasm true
*if ((not (ghost_mode = "ghost")) and (layer_1_lemen_encounter_new_left_ectoplasm))
    *set layer_1_lemen_encounter_new_left_ectoplasm false
    You find some Ectoplasm left here from when you visited this place as a ghost.
    
    [+1 Ectoplasm]
    *set inventory_ectoplasm + 1

*if (researched_observation_1)
    {Check: Energy >= 11}

    *if (not (energy >= 11))
        {Fail}
    *if (energy >= 11)
        {Pass}

*if (energy >= 11)
    While you start to head back, a sudden wave of energy from your well-rested state courses through you, so you decide to quickly pick some lemens, too.
    
    [+${lemen_fetch_skill} Lemens]
    *set inventory_lemens_13 + lemen_fetch_skill

Since you're around, you search a little for Duke's nut.

*if (researched_observation_1)
    {Check: Researched Observation I}
    
    Wait, but I need Observation I to perform these checks in the first place, which means…

    *if (not (researched_observation_1))
        {Fail}
    *if (researched_observation_1)
        {Pass}
        
        Yay! Is this where I find Duke's Nut? :0
        
        *page_break

*if ((not (layer_1_lemen_encounter_found_duke_nut)) and (not (researched_observation_1)))
    You feel close to finding Duke's nut, but you need better searching skills somehow…
*if ((not (layer_1_lemen_encounter_found_duke_nut)) and (researched_observation_1))
    *set layer_1_lemen_encounter_found_duke_nut true
    
    With the skills you learned in your Observation I studies, you're even able to find Duke's nut. Ew, it's all covered in ectoplasm.
    
    [+2 Ectoplasm]
    *line_break
    [+1 Duke's Nut]
    *set inventory_ectoplasm + 2
    *set inventory_duke_nut + 1

*comment TODO: Duke's nut is special with different dialog for all the other squirrels if you try to hand it off

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_pokemon

*if (unlocked_exeggcute_quest)
    You pass by the exeggcute again. "Keep looking for the one that did this to me," it says, then it hands you some seeds.
    
    [+1 Basic Flower Seed]
    *line_break
    [+1 Kiwi Seed]
    *set inventory_flower_seeds_1 + 1
    *set inventory_kiwi_seeds + 1
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("I came across an exeggcute today, but I couldn't catch it. *sad*[n/][n/]" & diary)

You find an exeggcute, your favorite pokemon, but you don't have any pokeballs or other pokemon to use to catch it.

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    *set diary ("The exeggcute is... a ghost??[n/][n/]" & diary)
    
    "Hello ghost," It says. With your ghostly skills, you can tell that the exeggcute is also a ghost. It goes on, "If you find out who killed me, I will reward you."
    
    Then, it vanishes, leaving behind a pokeball.
    
    [+1 Pokeball]
    *set inventory_pokeball + 1
    *set unlocked_exeggcute_quest true
    *return

*if ((months_passed < 200) and (inventory_dagger > 0))
    The exeggcute stares deep into your soul and speaks. "I know what you have. You shouldn't have it." Then it fades away.
    
    *set layer_1_pokemon_tattle true

However, on your disappointed path of shame way back you coward, you find some flower seeds. Well, one flower seed.

[+1 Basic Flower Seed]
*set inventory_flower_seeds_1 + 1

*if (researched_observation_1)
    {Check: Energy >= 11}

    *if (not (energy >= 11))
        {Fail}
    *if (energy >= 11)
        {Pass}

*if (energy >= 11)
    You even find a kiwi seed after looking more closely, since you're awake enough to see better or smth. You know the drill, awake = more rewards. Yeah.
    
    [+1 Kiwi Seed]
    
    *set inventory_kiwi_seeds + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_rosalia_encounter

*if (not (knowledge_rosalia))
    *if (ghost_mode = "ghost")
        Normally, you wouldn't recognize these red flowers, but since you're a ghost, you know what these are. They're for honoring the dead.
        
        [Encyclopedia Entry: Rosalia]
        *set knowledge_rosalia true
        
        *goto layer_1_rosalia_encounter_ghost
    
    You find a field of red flowers with large broad leaves on their stems. Pretty, but you're not sure what they're used for. You return empty-handed.
    
    *if (researched_observation_1)
        {Check: Energy >= 6}

        *if (not (energy >= 6))
            {Fail}
        *if (energy >= 6)
            {Pass}
    
    *if (energy >= 6)
        When you get back, you have enough energy to ask the botanist what they were. Apparently, they're rosalia, and Mabel uses them for her expensive teas. Ohhhh, you probably should have been listening to her rambles now that you think about it. Anyways, you should probably go to the encyclopedia entry to learn more about them.
        *set knowledge_rosalia true
    *goto_scene {home_base_scene} {home_base_label}

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    *goto layer_1_rosalia_encounter_ghost

*if (mapped_layer_1_gather_rosalia)
    You stumble upon the rosalia patch. Might as well gather some rosalia now that you're here.
    *goto layer_1_gather_rosalia

*set diary ("I found a new Rosalia patch. This should come in handy.[n/][n/]" & diary)

You find a beautiful rosalia patch. Might as well gather some rosalia!

*set mapped_layer_1_gather_rosalia true

*goto layer_1_gather_rosalia

*label layer_1_rosalia_encounter_ghost

*set diary ("I yearn for the ghost queen Rosaline to grace the Dome once more.[n/][n/]" & diary)

You wander in the rosalia patch you just found, mourning, even though you aren't actually dead yet. It's a ghost thing.

You can feel the energies of the Ghost Queen Rosaline strong in this Rosalia patch. You dream of returning to your human form and resurrecting her so that she can rule once again. Of course, that's a ghost thing too.

*return

*label layer_1_tso

*if ((layer_1_tso_found_note) or (ghost_mode = "ghost"))
    *if ((not (layer_1_tso_blue_coin_taken)) and (knowledge_blue_coins))
        *set layer_1_tso_blue_coin_taken true
        
        You find a blue coin.
        
        [+1 Blue Coin]
        *set inventory_blue_coins + 1
        
        *if (ghost_mode = "ghost")
            *return
        *goto_scene {home_base_scene} {home_base_label}
    
    You wander for hours but find nothing new except some special dirt.
    
    [+3 Special Dirt]
    *set inventory_special_dirt + 3
    
    *if (researched_observation_1)
        {Check: Energy >= 11}

        *if (not (energy >= 11))
            {Fail}
        *if (energy >= 11)
            {Pass}
    
    *if (energy >= 11)
        You're even hyper-awake enough to find some nearly-invisible ectoplasm nearby. You hear some spooky ghost people types use this as currency, so maybe it'll turn out to be useful.
        *if (ghost_mode = "ghost")
            Wait, you are a ghost though? Oh shucks, better hope this doesn't count as cannibalism.
        
        [+1 Ectoplasm]
        *set inventory_ectoplasm + 1
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*set layer_1_tso_found_note true

*comment If you're no longer at the orphanage, don't do the orphanage-based scene (though this is incredibly unlikely)
*comment TODO: Make it so that you're not completely locked out of the tso line this way?
*if (home_base_scene != "orphanage")
    You find a note on the ground with familiar handwriting, but can't quite place whose it is. Oh well.
    *goto_scene {home_base_scene} {home_base_label}

*if (researched_observation_1)
    {Check: Energy >= 1}

    *if (not (energy >= 1))
        {Fail}
    *if (energy >= 1)
        {Pass}

*if (energy = 0)
    You find a note on the ground but are too tired to recognize any of the handwriting. Oh well. You don't find anything else.
    
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("I can't believe George's father might still be alive. I have to find him![n/][n/]" & diary)

*if (skip_mode = "normal")
    You find a note on the ground and recognize the handwriting as similar to George's. At first, you think it must be a journal entry that got blown away. At first, you find it funny that of all the notes you could have found, you found George's, the only other pioneer at the orphanage. But they you start reading and see it's not George who wrote the note. It says…

    "I hope someone finds this. I need them to deliver this to my son George, let him know I'm okay. There was an… accident with me and someone important a few days ago, someone I owe many debts. I need to get away, and the only way to escape is through this goddamned tunnel. I have to get out. Let him know that I love him and am still alive."

    *temp time_passed_since_father_left months_passed
    *set time_passed_since_father_left - 70

    George's father is still alive? But that's impossible! He disappeared ${time_passed_since_father_left} months ago, leaving George homeless and without a family. Regardless, you have to get this to George, it's a major finding.

    *page_break

    When you show George, he sighs. "I hope he's okay. I shouldn't go looking for him, as much as I yearn to see him again. He told me that if he ever disappeared never to go after him, because he didn't want us both to die. Still, if you find any more hint of him, I would be incredibly helpful for you to share it with me. For your help so far, I'll let you know of a rosalia patch I found a while back"

    *if (not (knowledge_rosalia))
        "Oh you don't know about rosalia? I can teach you about it more tomorrow, but essentially it's just another useful plant from the tunnel. Used in a lot of teas. I know this town tends to rely on local crops more, which don't make for good teas, but in the summer when the city merchants make their trips down here you can sell them in the market for a fair price."

        [Encyclopedia Entry: Rosalia]

    He discusses with you where the rosalia patch is and then heads off to bed with his father's note, his eyes glossy.

[+2 Rosalia Fetch Skill]
*set knowledge_rosalia true
*set mapped_layer_1_gather_rosalia true
*set rosalia_fetch_skill + 2

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_henry

*comment Unused; for organization purposes
*label layer_1_henry_machine

*set layer_1_henry_machine_visits + 1

*if (skip_mode = "normal")
    You're walking along a well-traveled dirt trail when you pass a tree with a coin symbol on it. "You may invest any number of doubloons now, and the next time we meet I will give you something depending on what you invested."

*if (layer_1_henry_machine_visits >= 2)
    Last time, you invested ${layer_1_henry_machine_doubloons_invested} Doubloons.
    
    *if (layer_1_henry_machine_doubloons_invested = 0)
        My gift to you is -3 energy. You motherfucker didn't give me anything.
        
        *if (energy < 3)
            *set energy 0
        *if (energy >= 3)
            *set energy - 3
    *if (layer_1_henry_machine_doubloons_invested = 1)
        Here is 1 Doubloon. How does it feel being given a single coin? Yeah, that's what I thought. Maybe you should give me more next time.
        
        [+1 Doubloon]
        *set doubloons + 1
    *if (layer_1_henry_machine_doubloons_invested = 2)
        Here's a useless fish. (The tree doesn't output anything). Sorry, do you not sea it? Get it? Haha, give me a living wage next time dumbass.
    *if (layer_1_henry_machine_doubloons_invested = 3)
        Three for the price of one. That is, you paid three doubloons, and I give back one. Try giving a substantial amount next time…
        
        [+1 Doubloon]
        *set doubloons + 1
    *if (layer_1_henry_machine_doubloons_invested = 4)
        You gotta give me more than FOUR fucking doubloons for me to do anything.
    *if (layer_1_henry_machine_doubloons_invested = 5)
        "Give me a high five." Says the tree. When your hand is about to hit the tree for the high five it dodges out of the way with a strategically placed *swoosh* of the wind.
        
        "Yeah, how does it feel, huh? Give me more than 5 doubloons next time."
    *if (layer_1_henry_machine_doubloons_invested = 6)
        The tree grumbles about how you're a 6 offender and therefore you should have invested more than 6 coins.
    *if (layer_1_henry_machine_doubloons_invested = 7)
        Seven is only a lucky number if you follow it with a few zeroes…
    *if (layer_1_henry_machine_doubloons_invested = 8)
        You're eaten by the tree because you didn't put in enough doubloons. Get it? Ate?
        
        Jk, the game isn't over yet, but the tree is certainly not happy.
    *if (layer_1_henry_machine_doubloons_invested = 9)
        No. That's what nein means in german and it also means "no nothing's happening with that number of doubloons."
    *if ((10 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 22))
        That's still not enough Doubloons to satisfy the tree.
    *if (layer_1_henry_machine_doubloons_invested = 23)
        Hey! 23, that's my lucky number. Here's 200 Doubloons.
        
        [+200 Doubloons]
        *set doubloons + 200
    *if ((24 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 49))
        The tree spits out a Petunal Blossom
        
        *if (knowledge_petunal_blossoms)
            [+1 Petunal Blossom]
            *set inventory_petunal_blossoms + 1
        *if (not (knowledge_petunal_blossoms))
            Unfortunately, your character doesn't know what a petunal blossom is, so she just leaves it on the ground.
    *if ((50 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 68))
        The tree teaches you some methods of lemen gathering.
        
        [+3 Lemen Fetch Skill]
        *set lemen_fetch_skill + 3
    *if (layer_1_henry_machine_doubloons_invested = 69)
        Nice.
        
        The tree gives nothing.
    *if ((70 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 99))
        The tree teaches you some methods of lemen gathering.
        
        [+3 Lemen Fetch Skill]
        *set lemen_fetch_skill + 3
    *if ((100 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 102))
        The tree does not like the following numbers: 100, 101, and 102. The tree does not like these numbers because they are bad. Therefore, it gives nothing.
    *if ((103 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 112))
        The tree gives back a single doubloon. Hey, it's something.
        
        [+1 Doubloon]
        *set doubloons + 1
    *if ((113 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 222))
        The tree gives back your gift but decides to round to 200 doubloons.
        
        [+200 Doubloons]
        *set doubloons + 200
    *if ((223 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 306))
        The tree gives you a blue coin.
        *if (not (knowledge_blue_coins))
            However, your perplexed look gives away that you don't know what that is, so it takes it back and gives nothing.
        *if (knowledge_blue_coins)
            
            [+1 Blue Coins]
            *set inventory_blue_coins + 1
    *if ((307 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 327))
        The tree gives you 2 Kiwis
        
        [+2 Kiwis]
        *set inventory_kiwi + 2
    *if (328 = layer_1_henry_machine_doubloons_invested)
        The tree gives you a starblossom. Yay!
        *if (not (knowledge_starblossom))
            It even teaches you about starblossoms since you didn't know about them before.
            
            [Encyclopedia Entry: Starblossom Flower]
            *set knowledge_starblossom true
            
        [+1 Starblossom]
        *set inventory_starblossom + 1
    *if ((329 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 372))
        The tree spits out a copy of Exploration II. You can now research it in the orphanage once you have the proper prerequisites!
        
        [Unlocked: Exploration II]
        *set research_unlocked_exploration_2 true
    *if ((373 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 403))
        The tree gives you 5 immortal lemens.
        
        [+5 Immortal Lemens]
        *set inventory_lemens_immortal + 5
    *if ((404 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 555))
        The tree gives 6 Building Materials.
        
        [+6 Building Materials]
        *set inventory_building_materials + 6
    *if ((556 <= layer_1_henry_machine_doubloons_invested) and (layer_1_henry_machine_doubloons_invested <= 748))
        The tree gives you 10 Special Dirt.
        
        [+10 Special Dirt]
        *set inventory_special_dirt + 10
    *if (749 <= layer_1_henry_machine_doubloons_invested)
        The tree spits out nothing. Apparently, it thinks you're too rich already because you gave it so many coins last time.

*if (skip_mode = "normal")
    So how many doubloons will you invest this time? You must input a number between 0 and the number of doubloons you actually have.

*if (skip_mode = "skip")
    *if (decision_point = "22")
        *set decision_point "23"
        *set layer_1_henry_machine_doubloons_invested 1
        *goto layer_1_henry_machine_input_choice

*input_number layer_1_henry_machine_doubloons_invested 0 doubloons

*label layer_1_henry_machine_input_choice

*set doubloons - layer_1_henry_machine_doubloons_invested

*if (skip_mode = "normal")
    After inserting ${layer_1_henry_machine_doubloons_invested} doubloons into the tree machine, you wander along your way.

*set diary ((("I inserted " & layer_1_henry_machine_doubloons_invested) & " Doubloons into a weird tree machine.[n/][n/]") & diary)

*label layer_1_henry_henry

*if (met_henry)
    *goto layer_1_henry_henry_already_met

*if (skip_mode = "normal")
    As you wander, you find a middle-aged traveler returning from a trip. "Howdy," you exclaim!

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    However, you've completely forgotten that you're a ghost. He doesn't respond, so you both continue on your way.
    
    *goto layer_1_henry_henry_ghost

*if (researched_observation_1)
    {Check: Months Passed < 220}

    *if (not (months_passed < 220))
        {Fail}
    *if (months_passed < 220)
        {Pass}

*comment He responds differently at 220 months
*if (months_passed < 220)
    *set diary ("I met such a friendly traveler today! His name is Henry.[n/][n/]" & diary)
    
    *if (skip_mode = "normal")
        He responds in a friendly manner back to you, "I'm Henry. How are you doing little lass?"

        "I'm doing well." You look at his massive backpack of trinkets.

        "Yes, quite well, little lass. Here, you can have one if you'd like."

        He hands you an orb and trots off.
    
    *set diary ("[i]Acquired Language Orb[/i][n/][n/]" & diary)
    
    [Artifact: Language Orb]
    *line_break
    [Encyclopedia Entry: Language Orb]
    *set artifact_has_language_orb true
    
    *if (skip_mode = "normal")
        Nothing much else happens while you're off.
    
    *set fond_henry true
    *set met_henry true
    
    *goto_scene {home_base_scene} {home_base_label}
*else
    *set diary ("Whoever that Henry traveler guy is, I want no part of him![n/][n/]" & diary)
    
    "Whatever you want of Henry, you ain't gettin' it." Huffs the man, careful to keep his distance from you.
    
    "I was just saying hi."
    
    "Scammers and murderers say hi as well." He shoots back. "I shouldn't have even given you my name!" Then, he passes out of sight into the cover of some woods.
    
    Nothing much else adventurous happens on your trip.
    
    *set fond_henry false
    *set met_henry true
    
    *goto_scene {home_base_scene} {home_base_label}
    
*label layer_1_henry_henry_already_met

You happen upon Henry again.

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    You try to greet him, but you've completely forgotten that you're a ghost. He doesn't respond, so you both continue on your way.
    
    *goto layer_1_henry_henry_ghost

*if (not (fond_henry))
    He just walks around and avoids you.
    
    *goto_scene {home_base_scene} {home_base_label}

"Howdy," he says to you. "I don't have much to offer you in physical possessions right now unfortunately, but I'd give a good lad like you something great if I had it!"

He walks on past.

*if (not (unlocked_squirrel_war))
    *goto_scene {home_base_scene} {home_base_label}

*if (squirrel_faction_unlocked_henry)
    While walking away, Henry adds, "Keep recruiting squirrels for me!" Then whoops in the air.
    
    *goto_scene {home_base_scene} {home_base_label}
    
*set diary ("Today I learned that even Henry, a human, has a stake to the Squirrel Throne. Weird, but okay bruh.[n/][n/]" & diary)
    
Then, he abruptly turns around. "Oh wait, there is something I have to show you." He says. "It's a long held secret of my family. It relates to Charles the usurped squirrel king. I heard you were gathering forces for him?"

"Why?" You ask.

"Well, and I trust you enough to believe me on this, but I'm the true Squirrel Lord." You stare blankly at him. "I know I'm not a squirrel but… well, there was this favor and I got a cool staff but then the squirrel constitution that was [i]rightly[/i] voted on actually left out the mention of needing to be a squirrel even if the two [i]wrongly[/i] voted on constitutions (and the decree of the last king, but no one uses that as judicial precedent now that…" And then Henry's voice trails off.

"Anyways, you'll recruit the squirrels to my cause, right?" Says Henry, before adding, "Great!" And then actually walking on past. For reals this time.

*set squirrel_faction_unlocked_henry true

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_henry_henry_ghost

*if (layer_1_henry_saw_orange_orb)
    You spot that orange orb again with your ghost vision. Your ghostliness yearns for it.
    
    *return

*set layer_1_henry_saw_orange_orb true

*set diary ("Henry has the orange ghost orb![n/][n/]" & diary)

As you wander away from Henry, though, you look back and can see with your ghost vision that he has the orange ghostly orb in his backpack, the weakest of the colored ghostly orbs. Even though it's the weakest, it's still quite a find. It's said that someone with all the ghostly orbs would be able to bring a ghost back to life, no catches or gotchas. But only once, and then all the ghost orbs would be destroyed forever. Every ghost's fantasy has been to resurrect Rosaline, the Ghost Queen, so naturally your ghostly urges are to haunt Henry and steal the ghost orb, but you resist. No need to get yourself into these ghostly politics.

*return

*label layer_1_gift

*if (layer_1_gift_gift_found)
    *goto layer_1_gift_charles

*comment Unused; just for organization
*label layer_1_gift_gift

*set diary ("Wow, whoever left this massive pile of flowers here is so nice![n/][n/]" & diary)

*if (researched_observation_1)
    {Check: Energy > 0}

    *if (not (energy > 0))
        {Fail}
    *if (energy > 0)
        {Pass}

*if (skip_mode = "normal")
    You find some harvested flowers on the ground along with a note.
*if (energy = 0)
    *if (skip_mode = "normal")
        However, you're too tired to even read the note.
    *goto_scene {home_base_scene} {home_base_label}
*if (skip_mode = "normal")
    The note says:
    
    "I just wanted to be nice to the next person who came upon this area. Here's some rosalia and petunal blossoms. The first is a rose-like flower used in tea, and the second is very nutritious, especially for a vegetarian like myself! Use them however you see fit. :)"

*set layer_1_gift_gift_found true

[+8 Petunal Blossoms]
*line_break
[+6 Rosalia]
*set inventory_petunal_blossoms + 8
*set inventory_rosalia +6
*comment Set these to true now in case they weren't before. Would be weird for them to be in possession of something they don't know about.
*set knowledge_petunal_blossoms true
*set knowledge_rosalia true

*if (skip_mode = "normal")
    Very nice of whoever left this. You wander a little more before returning home.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_gift_charles

*if (met_charles)
    *goto layer_1_gift_dirt

*if (ghost_mode = "ghost")
    A little squirrel scurries away, spooked by your squirrel presence.
    
    *goto layer_1_gift_dirt

*if (unlocked_squirrel_war)
    *set diary ("I met Charles the Squirrel today.[n/][n/]" & diary)
    
    *set met_charles true
    
    You meet a golden-colored squirrel with a crown on its head. Its majestic nature leads you to immediately conclude that it's Charles.
    
    "Ah, yes, a human," he says. "Make sure to be recruiting squirrels to my cause!"
    
    Then, he leaves.
    
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("I met Charles the Squirrel today.[n/][n/]" & diary)

*set met_charles true

You find the place where that helpful person left those flowers and that note. In its place is a squirrel. I mean, it's just a squirrel, not really noticeable.

"I'm Charles," Says the squirrel. Wait wut. "I was a king of the squirrels of the fifth layer before I was usurped by the tyrannical Goodmington. Can you help me?"

You're still unsure what to think of the squirrel, but it has such a fluffy tail that you nod your head. Communicating with a squirrel… you must be going nuts. (Get it?)

"Well, in each layer you will find some layer companions, and you can use a nut to recruit them to a cause, mine or Goodmington, and then, once one of us has an army of squirrels to defeat the other, we will do so. Nuts will be found in various places. I vow that if you help me, though, I will allow you to use my army for your own purposes. Squirrels are quite ferocious, you know?"

*set unlocked_squirrel_war true

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_gift_dirt

You find the place where that helpful person left those flowers and a note again. In its place is some shiny soil.

[+1 Special Dirt]
*set inventory_special_dirt + 1

*if (inventory_shovel)
    With your shovel's help, you double the amount collected.
    
    *if (ghost_mode = "ghost")
        (Yes, it works even as a ghost).
    
    [+1 Special Dirt]
    *set inventory_special_dirt + 1

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_dagger

You come across a squirrel.
*if ((not (unlocked_squirrel_war)) or (ghost_mode = "ghost"))
     It scurries away and you continue on.
     *goto layer_1_dagger_flying_key

*comment Just for organizational purposes
*label layer_1_dagger_alice

*if (met_alice)
    You come across Alice, of the bushytail clan, once more.
    
    *goto layer_1_dagger_alice_assign
*if (not (met_alice))
    *set diary ("I met Alice today (she's a squirrel).[n/][n/]" & diary)

    Hey, it's a squirrel! You've heard about these creatures. You smile at this one. "What's your name?" You ask.
    
    "I'm Alice, of the bushytail clan. We are 200 strong. If you give me a nut, I will forever join whatever cause you wish of me, human."
    
    *set met_alice true
    
*if (not (squirrel_alive_alice))
    You come across a desolate waste of burnt trees. On the ground, you see wailing father squirrels and child squirrels that look like they haven't eaten for weeks.
    
    "Daddy, is mommy coming back?" Says one of the child squirrels.
    
    "No, Fred killed her."
    
    "What about leader Alice?" The child inquires.
    
    "No, child, like I said before, Fred killed her."
    
    And then, like, the child dies of hunger. Poor guy.
    
    You decide it's best to continue on.
    
    *goto layer_1_dagger_flying_key

*label layer_1_dagger_alice_assign

*set temp_affiliate "squirrel_affiliation_alice"
*set temp_affiliate_strength_var "squirrel_affiliation_alice_strength_var"
*set logic_nuts_required 1
*set logic_squirrel_strength_offered 200

*gosub_scene logic squirrel_wrapper

Then, you continue on.

*label layer_1_dagger_flying_key

*if ((layer_1_dagger_flying_key_has) or (layer_1_dagger_flying_key_used))
    *goto layer_1_dagger_dagger

*comment Don't do the encounter if you don't have requisite knowledge (which is really unlikely, but still gotta check)
*if (((not (knowledge_rosalia)) or (not (knowledge_starblossom))) or (not (mapped_layer_1_gather_rosalia)))
    *goto layer_1_dagger_dagger

You spot a shiny object from afar through some tallgrass. Suddenly, it lifts of the ground and darts away.
*if (layer_1_dagger_flying_key_caught_once)
    That must be the key that was here before!

*choice
    *selectable_if ((nimblicity_skill >= 20) and (energy >= 4)) #Chase it down [-4 Energy, Requires 20 Nimblicity]
        *goto layer_1_dagger_flying_key_caught
    #Move on
        *goto layer_1_dagger_dagger

*label layer_1_dagger_flying_key_caught

You chase down the little zippy object that's gleaming in the Sun. It takes a lot of energy, but you do it! This practice even helps you improve your nimblicity a little bit, too!

[-4 Energy]
*line_break
[+2 Nimblicity Skill]
*set energy - 4
*set nimblicity_skill + 2

You finally make a big deceiving leap one way while swinging your arms out the other way and you're able to cup the object between your hands.

*if (not (layer_1_dagger_flying_key_caught_once))
    It's a key with wings on it. Woah, these are supposed to be incredibly difficult to catch, and usually open up a lockbox of high importance. You really shouldn't have been able to catch it, but it seems like someone already damaged one of its wings, which is why you must have been able to catch it.
    
    Good job!
    
    Now you just have to figure out what it unlocks.
*if (layer_1_dagger_flying_key_caught_once)
    Yep, it's the key that was here before. Back in your possession.
    
[Acquired Winged Key]

*set layer_1_dagger_flying_key_caught_month months_passed
*set layer_1_dagger_flying_key_caught_once true
*set layer_1_dagger_flying_key_has true

You examine the key and find a little engraving with the words "Kettle Flower". What does that mean?

*choice
    #I know what it means (risk losing the key if you answer wrong, but get the choice to travel directly there now if you answer right)
        *goto layer_1_dagger_flying_key_quiz
    #Hope to stumble across it (might lose key each month)
        *goto layer_1_dagger_dagger

*label layer_1_dagger_flying_key_quiz

I've got it!

*choice
    #I have to deliver the key to the orphan gardens for a "kettle flower", which is a rare item!
        *goto layer_1_dagger_flying_key_quiz_failed
    #There are people named Kettle and Flower in town who own this key!
        *goto layer_1_dagger_flying_key_quiz_failed
    #They'll be serving kettle corn tomorrow at the orphanage, which tastes as sweet as a flower smells. I have to show this to one of the orphanage cooks!
        *goto layer_1_dagger_flying_key_quiz_failed
    #Rosalia is used for teas, which are made in a kettle. Onward to the "kettle" patch!
        *goto layer_1_gather_rosalia
    #Kettle sounds very similarly to petal, which suggests that I need to find a flower whose petals are as large as kettles, like the starblossom!
        *goto layer_1_dagger_flying_key_quiz_failed
    #This all boils down to that one joke Troy told me about kettles when I found a dandelion on the ground.
        *if ((not (saved_troy_orphanage)) and (troy_orphaned_event_occurred))
            Troy is dead because of your choices during this game. He wouldn't want to see you even if this answer were correct.
        
        *goto layer_1_dagger_flying_key_quiz_failed
    #Idk bruh this is too many options. I was sort of expecting more.
        *goto layer_1_dagger_flying_key_quiz_failed
    
*label layer_1_dagger_flying_key_quiz_failed

The key flaps up to eye level, wiggles back in forth to signal a "no", and flaps away.

[Lost Winged Key]
*set layer_1_dagger_flying_key_has false

*goto layer_1_dagger_dagger

*label layer_1_dagger_dagger

*set layer_1_dagger_dagger_visits + 1

*if (layer_1_dagger_dagger_taken)
    You find the spot where that dagger was, and there's some shiny soil for you to stick into your backpack in its place.
    
    [+1 Special Dirt]
    *set inventory_special_dirt + 1
    
    *if (inventory_shovel)
        [+1 Special Dirt] (Double Shovel)
        *set inventory_special_dirt + 1
        
    *if (not (layer_1_dagger_ghost_dagger_picked))
        *if (researched_observation_1)
            {Check: Ghost}

            *if (not (ghost_mode = "ghost"))
                {Fail}
            *if (ghost_mode = "ghost")
                {Pass}

        *if (ghost_mode = "ghost")
            You find a ghost dagger on the ground in the same spot the normal dagger was. Each round of combat, a ghost dagger may be used to consume a regular dagger without any hand limitations (that is, you can still hold another dagger or two handed sword).
            
            [+1 Ghost Dagger]
            *set layer_1_dagger_ghost_dagger_picked true
            *set inventory_ghost_dagger + 1
    
    *if (ghost_mode = "ghost")
        *return
    *goto layer_1_dagger_child

The next biggest thing that happens on your trip is that you find a rusty dagger on the ground.

*if (months_passed < 200)
    It's illegal for someone less than 200 months old to possess a weapon. Should you leave it, or risk being caught?
    
    *choice
        #Leave it
            You leave the dagger.
            
            *if (layer_1_dagger_dagger_visits >= 2)
                You're walking off and behind you, you can see another traveler picking up the dagger. Guess you missed your chance.
                *set layer_1_dagger_dagger_taken true
                
            *goto layer_1_dagger_child
        #Risk being caught and pick it up
            You take the dagger with you.
            
            [+1 Dagger]
            *set layer_1_dagger_dagger_taken true
            *set inventory_dagger + 1
            
            *if (layer_1_shrine_of_good_bad_deed = "")
                *set layer_1_shrine_of_good_bad_deed "Picked up a dagger even though she wasn't old enough."
                *set layer_1_shrine_of_good_bad_deed_month months_passed
            *goto layer_1_dagger_child

You pick up the dagger. Nice find. Now back home.

[+1 Dagger]
*set layer_1_dagger_dagger_taken true
*set inventory_dagger + 1

*goto layer_1_dagger_child

*label layer_1_dagger_child

*if ((not (layer_1_kiwi_sword_seen)) or (layer_1_dagger_sword_seen))
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*comment If they've seen the sword, something creepy happens

On your way, you hear the wails of a child from below the soil.

"Save me, save me."

Where are they?

*if (ghost_mode = "ghost")
    *set layer_1_dagger_sword_seen true
    
    After some looking, you spot them. Them? Wasn't there just one? Well, whatever it is there's a group of kids burying something just over there. You hover over to see that they aren't bona fide kids, but rather, just ghosts like you. And they're burying another kid.
    
    They turn your way and each one unsheathes a dagger that resembles a mini replica of the sword you saw in the stone. But then a blinding light appears and everything fades away. Next thing you know, you're in the same spot and there are no creepy ghost kids with daggers. Your ghost head must be getting to you. You just wander on.
    
    *return

*if (not (inventory_shovel))
    Maybe if you had a shovel, you could dig them out.
    
    *goto_scene {home_base_scene} {home_base_label}

Do you dig the child out?

*choice
    *selectable_if (energy >= 8) #Yes [-8 Energy]
        *set energy - 8
        *goto layer_1_dagger_child_dig
    #No
        Hopefully, they're still there when you come back. Or, well, hopefully not. You don't want to think about it.
        *goto_scene {home_base_scene} {home_base_label}

*label layer_1_dagger_child_dig

*set layer_1_dagger_sword_seen true

You dig and dig, but you can't find anything. Then, the wailing stops, and you feel a piercing pain through your back. You collapse, only to find 12 children shoveling dirt onto you, burying you alive. You can't move.

*page_break

Then, you see a bright light and the next thing you know, you're lying on the ground on your back, staring up at the bright Sun. You roll over to see a rock jutting out of the ground, and recognize it as the one that the sword was on. But when you look up, the sword isn't there.

You get up and find the piercing pain in your back gone. You finish heading back to the orphanage.

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_backpack

*comment TODO: Write out some different paths for this encounter each time.

*if ((knowledge_blue_coins) and (layer_1_button_extra_blue_coins > 0))
    You find some blue coins along the path, thanks to that button you pressed earlier. You're able to pick them all up, since they come with a neat little bag, too.
    
    [+${layer_1_button_extra_blue_coins} Blue Coins]
    *set inventory_blue_coins + layer_1_button_extra_blue_coins
    *set layer_1_button_extra_blue_coins 0

You come across a backpack lying on the ground next to a fresh campfire. Do you take it?

*choice
    #Yes
        *if (layer_1_shrine_of_good_bad_deed = "")
            *set layer_1_shrine_of_good_bad_deed "Stole a backpack."
            *set layer_1_shrine_of_good_bad_deed_month months_passed
        *goto layer_1_backpack_steal
    #No
        *goto layer_1_backpack_left

*label layer_1_backpack_steal

You pick up the backpack and carry on a ways before you hear a yell from behind you. "Hey, that's mine!" says the voice.

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    Then the man who bears the voice sees that no one is there. His backpack is simply hovering in mid-air. He screams like a child and runs. "Ghost!" He yells.
    
    *if (not (knowledge_petunal_blossoms))
        You find some weird flowers in the backpack and not much else.
        
        *return
    
    In the backpack, you find 5 Petunal Blossoms.
    
    [+5 Petunal Blossoms]
    *set inventory_petunal_blossoms + 5
    
    *return

*choice
    #Return the backpack
        *goto layer_1_backpack_steal_return
    #Run away with the backpack
        *goto layer_1_backpack_steal_run

*label layer_1_backpack_steal_return

"Thanks for returning the backpack," says the owner, "But you really should never have taken it." He balls up his fist and approaches you.

*gosub_scene logic has_weapon

*comment TODO: Update

*if (temp_logic_boolean)
    *choice
        #Defend yourself with the knife
            *goto layer_1_backpack_steal_defend
        #Let yourself take a beating
            *goto layer_1_backpack_steal_beat_up

*goto layer_1_backpack_steal_beat_up

*label layer_1_backpack_steal_run

*if (researched_observation_1)
    {Check: Nimblicity Skill >= 10}

    *if (not (nimblicity_skill >= 10))
        {Fail}
    *if (nimblicity_skill >= 10)
        {Pass}

*if (nimblicity_skill >= 10)
    The work you've put into your nimblicity skill has really paid off, and you're able to outrun him.
    
    *if (not (knowledge_petunal_blossoms))
        Once he's far behind you, you find some weird flowers in the backpack and not much else.
        
        *goto layer_1_backpack_ppp
    
    Once he's far behind you, you find 5 Petunal Blossoms in the bag.
    
    *set inventory_petunal_blossoms + 5
    
    *goto layer_1_backpack_ppp

You run as fast as you can, but with the backpack weighing you down, it's useless.

*if (temp_logic_boolean)
    *choice
        #Defend yourself
            *goto layer_1_backpack_steal_defend
        #Let yourself take a beating
            *goto layer_1_backpack_steal_beat_up

*goto layer_1_backpack_steal_beat_up

*label layer_1_backpack_steal_defend

You whip out your weapon.

*if (months_passed < 200)
    He says, "You know, you don't look too old to be wielding a weapon like that... would be a shame if someone found out." Then he winks and runs away.
    
    *set layer_1_backpack_knife_tattle true
*else
    The man runs away like a coward.
    
*if (not (knowledge_petunal_blossoms))
    You find some weird flowers in the backpack and not much else.

    *goto layer_1_backpack_ppp

Inside the backpack, you find 5 Petunal Blossoms

*set inventory_petunal_blossoms + 5

*goto layer_1_backpack_ppp

*label layer_1_backpack_steal_beat_up

*set diary ("I got beaten up today. All I wanted was to steal someone else's stuff.[n/][n/]" & diary)

He beats you up. It takes you an extra month to recover and you lose energy.

*gosub_scene logic month_passed

*if (energy = 0)
    Oh, whoops, you had no energy to lose. Nevermind about that part.
*if (energy < 3) and (energy > 0)
    [-${energy} Energy]
    *set energy 0
*if (energy >= 3)
    [-3 Energy]
    *set energy - 3

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_backpack_left

As a reward for your good ethics, here's a petunal blossom.

[+1 Petunal Blossom]
*set inventory_petunal_blossoms + 1

*if not (knowledge_petunal_blossoms)
    *set knowledge_petunal_blossoms true
    
    Petunal blossoms are purple flowers with yellow specks. They can be found in the first layer, and are edible.

*goto layer_1_backpack_ppp

*label layer_1_backpack_ppp

TODO

——————————————————————————————

*comment "Progress points"? Slots for whether you've done something between visits

*comment Use whether you have the perpetual progress point to see whether you've been here before
*if (layer_1_perpetual_progress_point_free)
    You see the perpetual progress point area just across from you. Do you stop by?
    
    Now that you're close enough to the perpetual progress point area, you can also sense the progress points you've gained in the meantime…
    
    *temp total_points 0
    *if (layer_1_perpetual_progress_point_free)
        You have the perpetual progress point (PPP) for visiting the PPP area.
    *if (layer_1_perpetual_progress_point_sold_rosalia_general_store)
        You have the PPP for selling rosalia to the general store.
    *if (layer_1_perpetual_progress_point_harvest_rosalia_from_garden)
        You have the PPP for harvesting rosalia from a garden.
    *comment TODO
    
    *choice
        #Stop by (you will have to spend your earned progress points, and unspent ones go to waste)

*comment TODO: Perpetual progress point power is perpetual progress points to the fourth power

You come across the perpetual progress point area. In the future, you will be able to trade in these special points for rewards. Get them by doing things between perpetual progress point area visits! For now, these goals won't be tracked and nothing will happen the next time you come here, but check back again later for new updates!

[PPP Goal: Freebie!]
*line_break
[PPP Achieved]
*line_break
[PPP Goal: Sell Rosalia to General Store]
*line_break
[PPP Goal: Buy From Market Square Booths]
*line_break
[PPP Goal: Finish Non-Orphanage Research]
*line_break
[PPP Goal: Sell Rosalia to General Store]
*line_break
[PPP Goal: Work Out at Outskirts Gym]

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_blue_coin

*if (researched_observation_1)
    {Check: Energy >= 11}

    *if (not (energy >= 11))
        {Fail}
    *if (energy >= 11)
        {Pass}

*if (energy <= 10)
    You pass by a bald guy yelling at you. You don't have the energy for him.
    
    *goto layer_1_blue_coin_eval
*if (energy >= 11)
    You pass by a bald guy yelling at you and start yelling back. HIYA!

THIS IS THE MINI ARTIFACTS STORE. THOSE THINGS THAT ARE ONE-OF-A-KIND BUT DON'T QUITE MEET THE "ARTIFACT" CRITERIA? THOSE GO HERE!!

*if (not (mapped_layer_1_keys))
    I SEE YOU HAVEN'T FOUND MY COMPETITOR DIRT SHOP, HERE I'LL LET YOU KNOW WHERE IT IS SINCE I'M SUCH A NICE GUY.
    
    [Mapped Location]
    *set mapped_layer_1_keys true

*comment TODO: More stuff unlocks as you research more artifact researches

*choice
    #CONTINUE ON WITH THE ENCOUNTER
        *goto layer_1_blue_coin_eval
    #BUY THE SCROLL "ART OF DIRT SALES" [FREE]
        [Acquired Mini Artifact: Art of Dirt Sales]
        *set mini_artifact_has_art_of_dirt_sales true
        
        *goto layer_1_blue_coin_eval

*label layer_1_blue_coin_eval

*if ((not (unlocked_squirrel_war)) and (not (layer_1_blue_coin_collected_nut)))
    You pass by a nut but those are probably not used for anything, so whatever.
*if ((unlocked_squirrel_war) and (not (layer_1_blue_coin_collected_nut)))
    The first thing is that you happen upon a nut.
    *set inventory_nuts + 1
    *set layer_1_blue_coin_collected_nut true

*if (knowledge_blue_coins)
    *goto layer_1_blue_coin_stash_found
*else
    *goto layer_1_blue_coin_stash_missed

*label layer_1_blue_coin_stash_found

*if ((knowledge_petunal_blossoms) and (not (layer_1_blue_coin_petunal_blossoms_picked)))
    *set layer_1_blue_coin_petunal_blossoms_picked true
    
    You find some Petunal Blossoms.
    
    [+2 Petunal Blossoms]
    *set inventory_petunal_blossoms + 2

*comment Make it so you can fit more if you have bag upgrades or physical health upgrades

*if ((not (mapped_layer_1_blue_coin)) and (inventory_map_kit))
    You make sure to mark this new place on your map.
    *set mapped_layer_1_blue_coin true

*if (layer_1_blue_coin_stash_amount = 0)
    *goto layer_1_blue_coin_stash_empty
*if (layer_1_blue_coin_stash_amount = 1)
    *goto layer_1_blue_coin_stash_single
*else
    *goto layer_1_blue_coin_stash_two

*label layer_1_blue_coin_stash_empty

You find the blue coin stash place, but there aren't any blue coins left. However, where they once were, you find some special dirt.

[+3 Special Dirt]
*set inventory_special_dirt + 3

*if (inventory_shovel)
    Luckily, your shovel comes in to save the day. You get double the Special Dirt.
    
    [+3 Special Dirt]
    *set inventory_special_dirt + 3

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_blue_coin_stash_single

You see only a single blue coin. You look for others, but can't really find more in the stash. Oh well, might as well take what you can.

[+1 Blue Coin]
*set layer_1_blue_coin_stash_amount - 1
*set inventory_blue_coins + 1

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_blue_coin_stash_two

It's a blue coin stash! Unfortunately, the blue coins are really heavy and spiky, so you can only really fit two into your bag.

[+2 Blue Coins]
*set layer_1_blue_coin_stash_amount - 2
*set inventory_blue_coins + 2

Then you return.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_blue_coin_stash_missed

*if (ghost_mode = "ghost")
    You have no idea what these blue pointy things you see on the ground are, and you just want to become human again anyways, so you continue on.
    
    *return

*set layer_1_blue_coin_stash_times_missed + 1

*if (layer_1_blue_coin_stash_times_missed = 1)
    You find 6 pointy blue spheres. It's a weird shape, but that's the best way you have to describe them. You don't take any since they don't seem useful, though, and are really heavy.
    
    The rest of your trip is uneventful.
*if (layer_1_blue_coin_stash_times_missed >= 2)
    You see the blue sphere area again and leave them there like last time. Not only are they really heavy, but they're also really clunky and would take up a lot of space in your bag.
    
    However, when you get back, you decide to ask George about them. "Oh, those are blue coins!" He says, before giving you an entry to put in your encyclopedia. "You should have picked some up, they're pretty valuable!"
    
    [Encyclopedia Entry: Blue Coins]
    *set knowledge_blue_coins true
    
    Darn, maybe next time.

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_gravestone

*if (unlocked_trainings)
    *if (exploration_trainings < 2)
        You come across a beetle who only speaks to people with at least 2 trainings.
    *if (exploration_trainings >= 2)
        *comment TODO

*set diary ("I found some extra equipments on my trip today.[n/][n/]" & diary)

You find some equipments lying on the ground.

[+3 Equipments]

*set inventory_equipments + 3
*if (not (unlocked_support_self))
    You don't know what they are, but you still are happy to have found something.

Also, there's a hill with gravestone on it. It's grassy green with a gentle slope and no large trees around. The gravestone looks like the centerpiece of this area. Something serene and deep against the gray skies.
*if ((not (layer_1_gravestone_grave_respects_paid)) and (knowledge_rosalia))
    You consider what it would look like with an offering, like a rosalia on it.

*if (ghost_mode = "ghost")
    *set layer_1_gravestone_grave_ghost_visits + 1
    
    The gravestone isn't your own, of course, but as a ghost you still feel a calling for it. You wander up the hill and through your ghost powers can sense the death that caused this grave to be. A little boy pushed into a river. How tragic.
    
    In the background, you can see his father, who looks just like the ghost seller who's out in market square in September. Hm… You wonder if there's a sidequest to be had. You better visit market square in September just to find out.
    
    *return

*comment Also option to defile the shrine later

*choice
    #Continue on
        *goto layer_1_gravestone_next
    *if (layer_1_gravestone_grave_respects_paid)
        #Pick up the rosalia you left there earlier [+1 Rosalia]
            *goto layer_1_gravestone_grave_remove_respects
    *if ((knowledge_rosalia) and (not (layer_1_gravestone_grave_respects_paid)))
        *selectable_if (inventory_rosalia > 0) #Go up there and pay your respects [-1 Rosalia]
            *set diary ("I paid respects to a grave today. It was a very serene moment.[n/][n/]" & diary)
            
            *goto layer_1_gravestone_grave_add_respects

*label layer_1_gravestone_grave_remove_respects

*set layer_1_gravestone_grave_respects_paid false
[+1 Rosalia]
*set inventory_rosalia + 1

You remove the rosalia from earlier. There's some goo-ey ectoplasm on it now for you to collect.

[+1 Ectoplasm]
*set inventory_ectoplasm + 1

*goto layer_1_gravestone_next

*label layer_1_gravestone_grave_add_respects

*set layer_1_gravestone_grave_respects_paid true
[-1 Rosalia]
*set inventory_rosalia - 1

You pay your respects.

*goto layer_1_gravestone_next

*label layer_1_gravestone_next

Nothing else much happens.

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_base

*set layer_1_base_visits + 1

*if (layer_1_base_visits = 1)
    *set diary ("I found the absolute BEST clearing for a base today.[n/][n/]" & diary)
    
    *if (skip_mode = "normal")
        You come across a clearing, a perfect spot for a base! You could even start now. Each time you visit this place, you can build one step of your structure. Each time, it will take at least 10 building materials, though, and some parts of the structure require others to be built first.
        
    *goto layer_1_base_enter

*if (not (can_fight))
    *goto layer_1_base_enter

Your legs start carrying you to that clearing you started a base in, but then a blue dingrit has a different idea. It's an attack!

*temp health_top 7
*temp health_front 7
*temp health_middle 7
*temp health_back 7
*temp health_bottom 7

*temp layer_top 0
*temp layer_front 0
*temp layer_middle 0
*temp layer_back 0
*temp layer_bottom 0

*label layer_1_base_dingrit

TODO (sorry uwu)

*label layer_1_base_enter

*if (layer_1_base_built_lemen_garden)
    While stopping by, you harvest some Lemens from the garden.
    
    [+1 Lemens]
    *set inventory_lemens_13 + 1
    
    *if (town_lemen_collective_doubloon_cost >= 100)
        You also get 3 Bonus Lemens from the collective's bonuses
        [+3 Lemens]
        *set inventory_lemens_13 + 3

*if (layer_1_base_built_ghost_circle)
    *comment TODO: Some game effect as well
    You say hi to Crystal, the ghost who's settled into your ghost circle. She makes the place more homey.

*if (skip_mode = "normal")
    So, what would you like to build?

*if (ghost_mode = "ghost")
    Even as a ghost, you're excited to build something.

*comment TODO: Add descriptions of these things

*set layer_1_base_num_stages_built_this_trip 0

*label layer_1_base_build

*comment TODO: Storage area (with kiwi bin)
*comment TODO: Personal study (can study books faster and can store some books for later)
*comment TODO: Shop
*comment TODO: Something to allow fast travel to base without going through first layer
*comment TODO: Later able to live inside here as home base
*comment TODO: Something you can only build in ghost form
*comment TODO: A stage? Like, music stage

*if (((layer_1_base_built_foundation) and (ghost_mode = "ghost")) and (not (layer_1_base_built_ghost_circle)))
    *if (researched_observation_1)
        {Check: Ghost}

        *if (not (ghost_mode = "ghost"))
            {Fail}
        *if (ghost_mode = "ghost")
            {Pass}

*choice
    *if (((layer_1_base_built_foundation) and (ghost_mode = "ghost")) and (not (layer_1_base_built_ghost_circle)))
        *selectable_if ((inventory_rosalia >= 16) and (inventory_building_materials >= 10)) #Build a ghost circle [-16 Rosalia, -10 Building Materials, Must be in ghost form to build]
            *set diary ("A ghost circle for a ghostly ghost.[n/][n/]" & diary)
            
            [-10 Building Materials]
            *line_break
            [-16 Rosalia]
            *line_break
            [Built: Layer 1 Ghost Circle]
            *set inventory_building_materials - 10
            *set inventory_rosalia - 16
            *set layer_1_base_built_ghost_circle true
            
            *goto layer_1_base_build_stage_built
    *if ((layer_1_base_built_covering) and (not (layer_1_base_built_garden_1)))
        *selectable_if (inventory_building_materials >= 10) #Build a proper garden plot [-10 Building Materials]
            *set diary ("I'm building gardens all over![n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_garden_1 true
            
            *goto layer_1_base_build_stage_built
    *if ((((layer_1_base_built_interaction_station) and (layer_1_base_built_foundation)) and (researched_construction_1)) and (not (layer_1_base_built_covering)))
        *selectable_if (inventory_building_materials >= 10) #Build a covering, which will allow construction of more advanced additions and also increase the max number of times you can construct in one trip. [-10 Building Materials, Requires Construction I research]
            *set diary ("I don't need any help with this base, I've got it... covered *wink*[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_covering true
            
            *set layer_1_base_num_stages_max_built_per_trip + 1
            
            *goto layer_1_base_build_stage_built
    *if ((layer_1_base_built_interaction_station) and ((layer_1_base_built_foundation) and (not (layer_1_base_built_squirrel_planning_1))))
        *selectable_if (inventory_building_materials >= 10) #Build a Squirrel War planning table to be able to attack small foes with tactical squads of at most 50 Squirrels. [-10 Building Materials]
            *set diary ("Hm.. I wonder who I'll attack first. Muahahhaha.[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_squirrel_planning_1 true
            *set layer_1_base_max_squirrels_commanded 50
            
            *goto layer_1_base_build_stage_built
    *if ((layer_1_base_built_interaction_station) and ((layer_1_base_built_foundation) and (not (layer_1_base_built_firepit))))
        *selectable_if (inventory_building_materials >= 10) #Build a Firepit to be able to cook. [-10 Building Materials]
            *set diary ("Just built a cooking station. I can cook now![n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_firepit true
            
            *goto layer_1_base_build_stage_built
    *if ((layer_1_base_built_interaction_station) and (not (layer_1_base_built_training_station_1)))
        *selectable_if (inventory_building_materials >= 10) #Build a Basic Training Station that you can use to convert Energy into Experience. [-10 Building Materials]
            *set diary ("Well, that didn't turn out so well. Will probably need a better foundation for a better training station.[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_training_station_1 true
            
            *goto layer_1_base_build_stage_built
    *if (not (layer_1_base_built_interaction_station))
        *selectable_if (inventory_building_materials >= 10) #Build an interaction station to be able to build further structures that you can even interact with. [-10 Building Materials]
            *set diary ("I wonder what this interaction station will do for me.[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_interaction_station true
            
            *goto layer_1_base_build_stage_built
    *if ((layer_1_base_built_foundation) and (not (layer_1_base_built_affirmation_station)))
        *selectable_if (inventory_building_materials >= 10) #Build a Positive Affirmation Station (you'll get a random compliment each time you embark on layer 1) [-10 Building Materials]
            *set diary ("Time to start raking in some positive affirmations. Stonks.[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_affirmation_station true
            
            *goto layer_1_base_build_stage_built
    *if (not (layer_1_base_built_lemen_garden))
        *selectable_if ((inventory_building_materials >= 10) and (lemen_fetch_skill >= 10)) #Build a Lemen Garden to gain a lemen each time you visit. [-10 Building Materials, Requires at least Lemen Fetch Skill 10]
            *set diary ("When life gives you the ability to make a Lemen garden, build it.[n/][n/]" & diary)
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_lemen_garden true
            
            *goto layer_1_base_build_stage_built
    *if (not (layer_1_base_built_foundation))
        *selectable_if (inventory_building_materials >= 10) #Build a Foundation here to be able to build more things in the future. [-10 Building Materials]
            *set diary ("I'm sure this foundation for my base will allow me to build more things in the future.[n/][n/]" & diary)
            
            You find a Blue Coin in the ground while digging out the foundation.
            *if (not (knowledge_blue_coins))
                Oops! Looks like your character doesn't know what a Blue Coin is. Well, I'm not gonna tell you now, but you get to keep the Blue Coin I'm giving you for when you do find out. (It won't display in your inventory until you figure out what it is).
                
            [+1 Blue Coin]
            *set inventory_blue_coins + 1
            
            *set inventory_building_materials - 10
            *set layer_1_base_built_foundation true
            
            *goto layer_1_base_build_stage_built
    *if (not (layer_1_base_built_marker))
        *selectable_if (inventory_building_materials >= 2) #Build a marker to later be able to return here whenever I want. [-2 Building Materials]
            *goto layer_1_base_build_marker
    *if (layer_1_base_built_interaction_station)
        #Use the interaction station to interact with one of your structures.
            *goto layer_1_base_interact
    #Scavenge a measly bit of building supplies and return home. [+2 Building Materials]
        *set inventory_building_materials + 2
        
        *if (ghost_mode = "ghost")
            *return
        *goto_scene {home_base_scene} {home_base_label}

*label layer_1_base_build_marker

*set diary ("Now that I've set done this massive eyesore pile-of-wood, I'll be able to spot this place more easily later.[n/][n/]" & diary)

[-2 Building Materials]
*line_break
[Built: Layer 1 Base Marker]
*set inventory_building_materials - 2
*set layer_1_base_built_marker true

*goto layer_1_base_build_stage_built

*label layer_1_base_build_stage_built

*set layer_1_base_num_stages_built_this_trip + 1

*if (layer_1_base_num_stages_max_built_per_trip <= layer_1_base_num_stages_built_this_trip)
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*goto layer_1_base_build

*label layer_1_base_interact

As you build structures that you can interact with, more choices will show up here.

*choice
    *if (layer_1_base_built_squirrel_planning_1)
        #Send a squirrel army after someone you don't like.
            *goto layer_1_base_squirrel_command
    *if (layer_1_base_built_training_station_1) and (months_passed >= (layer_1_base_last_used_training_station_1 + 3))
        *selectable_if (energy >= 5) #Use the basic training station you built (can only be used once every 3 months) [Lose 5 Energy, +8 Experience]
            *set diary ("That didn't feel like great training, but my arms hurt so hopefully I got some experience in?[n/][n/]" & diary)
            
            *set layer_1_base_last_used_training_station_1 months_passed
            
            [-5 Energy]
            *line_break
            [+8 Experience]
            *set energy - 5
            *set experience + 8
            *goto layer_1_base_interact
    *if (layer_1_base_built_garden_1)
        #Tend to your base's garden plot(s)
            *goto layer_1_base_interact_garden
    #Return to building menu
        *goto layer_1_base_build

*label layer_1_base_interact_garden

*comment TODO: Make it so that you can plant lemens in here

*choice
    *if (gardens_layer_1_base_plot_1 = "nothing")
        #Plot something in plot 1
            *set logic_garden_plot "gardens_layer_1_base_plot_1"
            *set logic_garden_plot_timer "gardens_layer_1_base_plot_1_ready_to_harvest"
            
            *gosub_scene logic garden_plant_seed
            
            *goto layer_1_base_interact_garden
    *if ((not (gardens_layer_1_base_plot_1 = "nothing")) and (gardens_layer_1_base_plot_1_ready_to_harvest <= months_passed))
        #Harvest plants in plot 1
            *set logic_garden_plot "gardens_layer_1_base_plot_1"
            *set logic_garden_plot_timer "gardens_layer_1_base_plot_1_ready_to_harvest"
            
            *gosub_scene logic harvest_plants
            
            *goto layer_1_base_interact_garden
    *if (not (gardens_layer_1_base_plot_1 = "nothing"))
        #Check on what you planted in plot 1
            *set logic_garden_plot "gardens_layer_1_base_plot_1"
            *set logic_garden_plot_timer "gardens_layer_1_base_plot_1_ready_to_harvest"
            
            *gosub_scene logic check_garden_plot
            
            *goto layer_1_base_interact_garden
    *if (((not (gardens_layer_1_base_plot_1 = "nothing")) and (inventory_fertilizer > 0)) and (months_passed < gardens_orphanage_plot_1_ready_to_harvest))
        #Use fertilizer on my plot 1 plants
            *set logic_garden_plot "gardens_layer_1_base_plot_1"
            *set logic_garden_plot_timer "gardens_layer_1_base_plot_1_ready_to_harvest"
            
            *gosub_scene logic apply_fertilizer
            
            *goto layer_1_base_interact_garden

*label layer_1_base_squirrel_command

*if (ghost_mode = "ghost")
    Unfortunately, squirrels don't interact with ghosts. Probably not a good idea to call a meeting with them while you're in this form.
    
    *page_break
    *goto layer_1_base_interact

*temp num_enemies 0

List of enemies:
*line_break
*if (town_outskirts_randy_cheated)
    Randy: Requires at least 50 squirrels to neutralize
    *set num_enemies + 1

*if (num_enemies = 0)
    You don't have any enemies to send the squirrels after. Go find some people to hate first!
    
    *page_break
    
    *goto layer_1_base_interact

*comment TODO: This needs an overhaul once we get more enemies/affiliations with favors
*choice
    *if ((squirrel_favors_fred > 0) and (town_outskirts_randy_cheated))
        #Neutralize Randy
            *goto layer_1_base_squirrel_command_randy
    #Nevermind, leave them alive for now
        *goto layer_1_base_interact

*label layer_1_base_squirrel_command_randy

*set randy_burned true

*set diary ("I burned down Randy's gas station with the help of some squirrels.[n/][n/]" & diary)

Your footsteps whisk the smoldering ashes into the air as the full moon shines down on the remains of Randy's gas station. Apparently, the squirrels were able to unlock some flammable fluids beneath the machines and, once the place was sufficiently soaked with this "oil" as they called it, ignite the fluid. You know this because you saw yourself.

You watched from the tree in the center of the outskirts. Ironically, even though this place was run-down, no crime really happened here, so people won't really suspect a crime of this level. People have been good at keeping the "real crime" to The Streets. Even Randy only cheated people from within the comfort of his own store, and even then never in amounts more than a thousand or so Doubloons at most.

But he wouldn't cheat people any longer.

You hear a squirrel next to you radio another closer to the gas station. "Demo Squirrel A, we are ready for ignition." The other squirrels you've enlisted are circled around the station in various little discreet corners, armed with riot gear in the case of plan B. They don't need to be completely hidden, just enough that people would chalk up any weird sights to their wild imagination.

"Copy that, Demo Squirrel B," answers Demo Squirrel A.

"Will this burn down the rest of the outskirts?" You ask Demo Squirrel B.

"Don't worry about it, son," answers the Squirrel Tech. "We're professionals. We calculated every flame down to the acorn."

And surely enough, when the sparks from Squirrel A's chattering teeth fly onto the beginnings of the oil pool in front of the gas station, only the station catches fire. There's a brief moment when the flames move too far to the West and some ground squirrels are dispatched to blow water from their fat cheeks in order to calm it down, but other than that the fire is completely contained to the gas station. That's not to say that it's a small fire, though.

As the oil pool grows hotter and hotter, it starts to ignite the pools of oil beneath. You hear Randy scream. "My home! My home!" Then he rushes out. You hope that the expression of intense anger he gives when he exits is his reaction to seeing you sitting at that tree hundreds of yards away, and knowing who gave him what was coming for him, but there's only a brief moment before the first machine explodes and sends shrapnel straight at his screeching head. It's hard to see from this distance, but you think his head is cut clean off by a metal plate. Sad. You were hoping he'd die from the flames.

You can never really confirm how he died, though, as even his bones are hard to find in the smoldering remains of his home. The Sun starts to creep out from the horizon, lighting up more than just the gas station. You know it's dangerous to stay for long, but you need to just soak in the taste of victory, in the form of soot and wholesale destruction.

"Ma'am," starts an authoritative voice behind you, "come with me."

*page_break

"So, what'd you find?" You ask the infiltration squirrels who retrieved the last of Randy's belongings before the mission. "I have to admit, you did spook me when you called out for me to come with you."

"Sorry, about that, we're used to arresting dissenters." Then the lead infiltration squirrel winks. Here are the remains of his belongings.

*comment TODO: Base this also off what you gave him.
[+231 Doubloons]
*set doubloons + 231
*if (not (town_outskirts_randy_blue_coin_bought))
    *line_break
    [+1 Blue Coin]
    *set inventory_blue_coins + 1

"Also, we found some logs he was keeping of his cheats. See your Helpful Information tab in the Stats Screen if you'd like to see that sort of useless drivel."

"So that's it?" You say.

"Yeah, he was a poor bum, what do you expect?"

"I honestly didn't expect anything," you say, laughing. Then you head home after a job well done.

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_keys

*if (ghost_mode = "ghost")
    You catch sight of squirrel quickly scuttering off.
    
    *goto layer_1_keys_shop

*if (met_rory)
    On your way to the key shop, you stumble upon Rory again.
*if (not (met_rory))
    *set met_rory true
    
    *if (skip_mode = "normal")
        *if (not (unlocked_squirrel_war))
            You pass by a squirrel. It straight-up starts speaking English to you.
        *if (unlocked_squirrel_war)
            You see a squirrel. Squirrel!
        
    *if (skip_mode = "normal")
        "I am Rory, of, uh, well, I don't have a clan. But I'm pretty cool. I'm, like 3 squirrels worth of strength!" You size Rory up. Although he's on his own, he is quite large for a squirrel. You judge him more at 2 squirrels of strength. And I'll tell you all about the squirrel wars, measly human! Look how useful I am."
    
    *if (unlocked_squirrel_war)
        "Oh, you already know those? Uh, well…"
    *if (not (unlocked_squirrel_war))
        
        [Encyclopedia Entry: Squirrel Wars]
        *set unlocked_squirrel_war true
        
    *if (skip_mode = "normal")
        "You can still assign me just like any squirrel, for a Nut. I may not provide much in numbers or power or fear struck into the enemy or smarts or… uh, pls just give me a nut uwu."
    
*if (skip_mode = "normal")
    "So, who would you like me to support?" Says Rory proudly.

*set temp_affiliate "squirrel_affiliation_rory"
*set temp_affiliate_strength_var "squirrel_affiliation_rory_strength_var"
*set logic_nuts_required 1
*set logic_squirrel_strength_offered 3

*gosub_scene logic squirrel_wrapper

*goto layer_1_keys_shop

*label layer_1_keys_shop

*if (not (mapped_layer_1_keys))
    *set diary ("I found someone running a key shop. This is bound to have some good deals![n/][n/]" & diary)
    
    *if (skip_mode = "normal")
        You come across a little shop with keys. Sitting on a rocking chair outside is a frail old man with green hair. "Hello!" He says, greeting you. "I sell keys to various parts of town. Most people recognize someone as experienced by whether they have some of my keys. My keys can also open things in other places. For the most part, completing an encounter in one area will give you special dirt that you can use at my shop!"

        Just keep in mind that you need 100 experience before the orphanage will allow you to venture out to the town outskirts where these buildings are located.
*if (mapped_layer_1_keys)
    You walk to the key shop again and greet the old man with green hair.

*set mapped_layer_1_keys true

*if (ghost_mode = "ghost")
    Then it occurs to you that this old man sees you even though you're a ghost. Very strange, but you'll take it.

*label layer_1_keys_shop_choice

*if (researched_observation_1)
    {Check: Energy >= 6}

    *if (not (energy >= 6))
        {Fail}
    *if (energy >= 6)
        {Pass}

*temp get_extra_experience false
*if (energy >= 6)
    Note: Since you're energetic, each time you buy a key, you get 5 experience for free!
    *set get_extra_experience true

*choice
    *if (not (key_owned_town_gym))
        *selectable_if (inventory_special_dirt >= 3) #Key to the gym [-3 Special Dirt]
            *set diary ("I'm really excited to try to try out the gym I just got access to.[n/][n/]" & diary)
            
            [-3 Special Dirt]
            *line_break
            [Key: Town Outskirts Gym]
            *set inventory_special_dirt - 3
            *set key_owned_town_gym true
            
            *goto layer_1_keys_shop_key_bought
    *if (not (key_owned_gas_station))
        *selectable_if (inventory_special_dirt >= 5) #Key to the Gas Station [-5 Special Dirt]
            *set diary ("I wonder what a 'gas station' is?[n/][n/]" & diary)
            
            [-5 Special Dirt]
            *line_break
            [Key: Town Outskirts Gas Station]
            *set inventory_special_dirt - 5
            *set key_owned_gas_station true
            
            *goto layer_1_keys_shop_key_bought
    *if (not (key_owned_town_outskirts_cold_topic))
        *selectable_if (inventory_special_dirt >= 18) #Key to the dumpy clothing store in the town outskirts [-18 Special Dirt]
            *set diary ("Ah, I think I've heard of that store. Cold Topic. I wonder why they call it that.[n/][n/]" & diary)
            
            [-18 Special Dirt]
            *line_break
            [Key: Cold Topic Clothing Store]
            *set inventory_special_dirt - 18
            *set key_owned_town_outskirts_cold_topic true
            
            *goto layer_1_keys_shop_key_bought
    *if (not (key_owned_town_outskirts_community_garden))
        *selectable_if (inventory_special_dirt >= 7) #Key to the Community Gardens in the town outskirts [-7 Special Dirt]
            *set diary ("Community gardens are the best types of gardens.[n/][n/]" & diary)
            
            [-7 Special Dirt]
            *line_break
            [Key: Town Outskirts Community Garden]
            *set inventory_special_dirt - 7
            *set key_owned_town_outskirts_community_garden true
            
            *goto layer_1_keys_shop_key_bought
    *if (not (key_owned_town_outskirts_tea_shop))
        *selectable_if (inventory_special_dirt >= 6) #Key to the tea shop in the town outskirts [-6 Special Dirt]
            *set diary ("Now I can visit the tea shop to brew tea with my rosalia.[n/][n/]" & diary)
            
            [-6 Special Dirt]
            *line_break
            [Key: Leif's Tea Shop]
            *set inventory_special_dirt - 6
            *set key_owned_town_outskirts_tea_shop true
            
            *goto layer_1_keys_shop_key_bought
    *if (not (key_owned_town_outskirts_temple))
        *selectable_if (inventory_special_dirt >= 8) #Key to the temple on the town outskirts [-8 Special Dirt]
            *set diary ("I wonder why the key shop owner said the outskirts temple folk were strange?[n/][n/]" & diary)
            
            [-8 Special Dirt]
            *line_break
            [Key: Town Outskirts Temple]
            *set inventory_special_dirt - 8
            *set key_owned_town_outskirts_temple true
            
            "So that's what you'll be buying today, eh? I gotta warn you those outskirts temple folk are a little… abnormal. You might want to watch your mouth around them."

            *goto layer_1_keys_shop_key_bought
    #Scavenge for dirt so that you can buy something the next time you are here, then continue [+2 Special Dirt]
        *goto layer_1_keys_scavenge

*label layer_1_keys_shop_key_bought

*if (get_extra_experience)
    [+5 Experience]
    *set experience + 5

Alright, I only sell one thing at a time, so see you later!

*if (mini_artifact_has_art_of_dirt_sales)
    However, with your "art of dirt sales" artifact, you're able to overcome that restriction!
    
    *page_break
    *goto layer_1_keys_shop_choice

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_keys_scavenge

*if (skip_mode = "normal")
    "Aw, well, hope you come back later." Says the old man.
        
[+2 Special Dirt]
*set inventory_special_dirt + 2

*if (inventory_shovel)
    You get double the Special Dirt due to your trusty shovel.

    [+2 Special Dirt]
    *set inventory_special_dirt + 2
    
*if ((unlocked_squirrel_war) and (not (layer_1_keys_acorn_picked)))
    *set layer_1_keys_acorn_picked true
    
    *if (skip_mode = "normal")
        You found an acorn! What's it for, you ask? Oh, I have plans, don't worry. These don't count as nuts by the way. Now on your way you go!
    
    [+1 Acorn]
    *set inventory_acorns + 1

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_scrolls

*if (layer_1_button_extra_doubloons > 0)
    You pick up some bonus Doubloons from the button machine.
    
    [+${layer_1_button_extra_doubloons} Doubloons]
    *set doubloons + layer_1_button_extra_doubloons
    *set layer_1_button_extra_doubloons 0

Ahead of you are some woods, dark and gloomy like my soul. No, not your soul, mine. The narrator's. Anyways…

An ancient scroll crunches under your feet as you approach the woods.

*set layer_1_scrolls_visits + 1

*if (layer_1_scrolls_visits = 1)
    It teaches the Reading I research.
    
    *if (researched_reading_1)
        …Which isn't much use to you since you already know it.
    *if (not (researched_reading_1))
        *set diary ("Wow, I was so lucky to learn one of the most important researches for free today.[n/][n/]" & diary)
        
        [Researched: Reading I]
        *set researched_reading_1 true
        *set logic_research_passed "researched_reading_1"
        *gosub_scene logic do_research_effects
    
    *if (researched_observation_1)
        {Check: Ghost}

        *if (not (ghost_mode = "ghost"))
            {Fail}
        *if (ghost_mode = "ghost")
            {Pass}
    
    *if (ghost_mode = "ghost")
        Because you're a ghost, you can even read the back of the scroll (normal humans can only read the front of a scroll). That section is on Ghost Reading I.
        
        *if (not (researched_ghost_reading_1))
            [Researched: Ghost Reading I]
            *set researched_ghost_reading_1 true
        *if (researched_ghost_reading_1)
            You already researched that, though, so you don't get any benefit.

*if ((not (researched_ghost_reading_1)) and (not (layer_1_scrolls_visits = 1)))
    *if (researched_observation_1)
        {Check: Ghost}

        *if (not (ghost_mode = "ghost"))
            {Fail}
        *if (ghost_mode = "ghost")
            {Pass}
    
    *if (ghost_mode = "ghost")
        Although you've read it before, you notice something different about the scroll this time. There's new lettering on the back! Of course, you'd completely forgotten about the [i]back[/i] of the scroll, which you can now read as a ghost (normal humans can only read the front of a scroll). The back is about Ghost Reading I.

        [Researched: Ghost Reading I]
        *set researched_ghost_reading_1 true

*if (layer_1_scrolls_woods_visits >= 2)
    As you continue, you hear a hissing from within the forest, like, well, pieces of paper hissing. Like they came from a Harry Potter novel and they're all magical. Not that the pieces of paper in the published works of JK Rowling are magical, I mean like if they were in the world Harry Potter. Like, ya know?

*choice
    #Enter the woods
        *goto layer_1_scrolls_woods
    #Walk around the woods
        *goto layer_1_scrolls_lake

*label layer_1_scrolls_woods

*set layer_1_scrolls_woods_visits + 1

*if (layer_1_scrolls_defeated)
    *goto layer_1_scrolls_shack

*if (layer_1_scrolls_woods_visits = 1)
    Nothing happens this time, it's just a normal forest, but you get an eerie feeling that you shouldn't return here again unless you're ready to fight.
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*if (layer_1_scrolls_woods_visits = 2)
    *set diary ("The scrolls are evil! I repeat, the scrolls are evil![n/][n/]" & diary)

    This time, when you walk through, there are scrolls everywhere. But they are in an undecipherable language. Alerted to your presence, these scrolls fly into the air and wrap you up like a mummy until you can't escape or even breath. You pass out.

    When you wake up, you notice that you have forgotten some things. You can feel the words "rosalia" and "petunal blossoms" escaping your brain like they were never there in the first place.

    [Lost Knowledge: Rosalia]
    *line_break
    [Lost Knowledge: Petunal Blossoms]
    *set knowledge_rosalia false
    *set knowledge_petunal_blossoms false

*if (not (can_fight))
    Unfortunately, you haven't learned the ways of fighting just yet. Return another time when you have.
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

Do you fight the scrolls?

*if (can_fight)
    *choice
        #It's on!
            *goto layer_1_scrolls_woods_fight
        #Back home
            *if (ghost_mode = "ghost")
                *return
            *goto_scene {home_base_scene} {home_base_label}

*label layer_1_scrolls_woods_fight

*set logic_fight_encounter "scrolls"
*gosub_scene logic fight

*if (logic_fight_result = "player_lost")
    
    You lost :(
    
    *if (knowledge_rosalia)
        The scrolls take away knowledge of rosalia from you. You're used to it at this point.

        [Lost Knowledge: Rosalia]
        *set knowledge_rosalia false
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*set layer_1_scrolls_defeated true

[+15 Research Speed]
*line_break
[+30 Experience]
*set research_speed + 15
*set experience + 30

You rip the scrolls to shreds. It seems they were guarding an ancient shack back here. But the shack seems to need a key. Something for another time

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_scrolls_shack

You find the place you defeated those scrolls in such a badass way. Then, you continue on since you still don't have the damn key for the shack you came across.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_scrolls_lake

*temp is_wintertime false
*if (((months_passed modulo 13) <= 2) or ((months_passed modulo 13) >= 11))
    *set is_wintertime true

*if (researched_observation_1)
    {Check: Is Wintertime}

    *if (not (is_wintertime))
        {Fail}
    *if (is_wintertime)
        {Pass}

*if (is_wintertime)
    You walk around and come across a frozen-over lake with an island in the middle of it. Even from here, you can see a big X on the island. You decide to wander over to it.
    
    *goto layer_1_scrolls_lake_island
*if (not (is_wintertime))
    You walk around and come across a pond, that you also have to walk around. It's really muddy here, actually, and the mosquitos whiz around like crazy. Out in the middle of the pond, you can see an island with a big "X" on it. If only the pond was frozen over (hint hint), so you could go investigate. (If I later add swimming but not a swim option here, then let's just both agree that the pond is full of sharks or something so that the narrative is still somewhat cohesive, okay?)
    
    So, yeah, you go around the pond and not much else happens.
    
    *if (researched_observation_1)
        {Check: Ghost}

        *if (not (ghost_mode = "ghost"))
            {Fail}
        *if (ghost_mode = "ghost")
            {Pass}
    
    *if (ghost_mode = "ghost")
        Oh wait, what's that? You're a ghost? You don't need to swim because you can just hover over the water? How could I forget, Jesus Christ! Here, you pass.
        
        *goto layer_1_scrolls_lake_island
    
    *goto_scene {home_base_scene} {home_base_label}

*label layer_1_scrolls_lake_island

You arrive at the {scene = "tunnel", label = "layer_1_scrolls_lake_island"} portion of my story. Congratulations!

*if (not (inventory_shovel))
    Just dig right here for your reward. Oh, what's that, you don't have a shovel? Well, come back later!
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

With your trusty shovel (thank goodness you brought it), you dig down into the soft soil of the island and find a congratulatory reward.

*if (layer_1_scrolls_lake_island_reward_taken)
    [+4 Special Dirt]
    *set inventory_special_dirt + 4
    
    *if (inventory_shovel)
        [+4 Special Dirt] (Double Shovel)
    
    (You already collected the doubloons, this is all I have, trust me!)
*if (not (layer_1_scrolls_lake_island_reward_taken))
    *set layer_1_scrolls_lake_island_reward_taken true
    [+3000 Doubloons]
    *set doubloons + 3000

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_kiwi

*if ((inventory_map_kit) and (not (mapped_layer_1_kiwi)))
    *set diary ("I mapped a new place today! I even decided to give it a name.[n/][n/]" & diary)
    
    *set mapped_layer_1_kiwi true
    
    You find a place you haven't put on your map. It may be somewhere you've been before, or somewhere you haven't been. Who knows. All you know is that you're mapping it. I'll tell you more about it later, but for now, it needs a name…
    
    *choice
        #Wait, can I know where I am first?
            I know it can be hard making decisions without all the information. I should have been more specific about this place. I should also follow your wishes, but I'm not going to. Instead, I'll name it for you, taking out that stressful decision from you life. The name is poopylandia.
            
            *set mapped_layer_1_kiwi "poopylandia"
            
            *goto layer_1_kiwi_map_eval
        #Hm... Okay, I got the perfect name.
            *input_text mapped_layer_1_kiwi
            
            *goto layer_1_kiwi_map_eval

*label layer_1_kiwi_map_eval

You pass by a heap of building scraps.
*if ((layer_1_base_visits >= 1) and (not (researched_construction_1)))
    Perfect for a base, but their shapes are all weird and you don't have good enough construction knowledge to work around that, so you leave them there.
*if ((layer_1_base_visits >= 1) and (researched_construction_1))
    Perfect for a base, and despite their weird shapes, you know just how to fit them into your base.
    
    [+3 Building Materials]
    *set inventory_building_materials + 3

*if (layer_1_kiwi_sword_seen)
    *set diary ("I wonder where the sword went.[n/][n/]" & diary)
    
    You pass by where the sword in the stone was, but it's not there anymore.

*if (not (layer_1_kiwi_sword_seen))
    *if (researched_observation_1)
        {Check: Energy >= 6}

        *if (not (energy >= 6))
            {Fail}
        *if (energy >= 6)
            {Pass}
    
    *if (energy >= 6)
        *set diary ("I saw a cool sword on my journey today.[n/][n/]" & diary)

        *if (skip_mode = "normal")
            First, you find a sword in a stone. It's super rad, but you'd need a lot of strength to pull it out, and you don't have that.

            It's kinda mirage-y.
            *if (ghost_mode = "ghost")
                Sorta like your ghostly self.
            You don't think you would have seen it if you didn't have enough energy, so you're glad you do.

        *set layer_1_kiwi_sword_seen true
    *if (energy <= 5)
        *set diary ("I was told by the narrator today that I did not see a cool sword.[n/][n/]" & diary)

        You see a cool-ass sword in a stone. Actually, scratch that, your energy needs to be at least 6 for that to work. Sorry.

        Wait, but you just told me I totally saw it?

        Nope, forget that detail, reader, whomever you may be.

        Uh, okay.

        You continue on. If only you had at least 6 energy.
    
*label layer_1_kiwi_circles

*if ((met_andellise) and (not (ghost_mode = "ghost")))
    *if ((inventory_andellise) and (not (andellise_loved)))
        "We're going in circles!" Complains Andellise. "We've been here before, ugh. This is where you picked me up!"
        
        "Shut up, Amelos," you shoot back, butchering her stupid name on purpose just to annoy her further.
        
        *choice
            #But secretly, I'm in love with her
                *set diary ("The feelings... they're undeniable. How do I tell her?[n/][n/]" & diary)
                
                You'll never get over her bright, bushy tail, and those inquiring beady eyes.
                
                *set andellise_loved true
                
                *goto layer_1_kiwi_george
            #But secretly, I brought her here to finish the job and kill her
                *set diary ("Squeak is what squirrels say before they die, right Andellise? Oh right, you can't answer because you're dead. *maniacal laughter*[n/][n/]" & diary)
                
                A little squeak is the last thing you hear from her as you snap her neck.
                
                *set andellise_dead true
                *set inventory_andellise false
                
                *if (not (layer_1_kiwi_andelisse_blue_coin_taken))
                    You find in her squirrel cheeks the Blue Coin she offered you when you guys first met. You stick your fingers in there and pull out the coin from her moist lips.
                    
                    I'm so sorry for making you read that.
                    
                    [+1 Blue Coin]
                    *set inventory_blue_coins + 1
                    *set layer_1_kiwi_andelisse_blue_coin_taken true
                
                *goto layer_1_kiwi_george
            #I realize that there's no middle ground on this choice
                Hey sometimes, you gotta make tough decisions in life.
                
                *page_break
                
                *goto layer_1_kiwi_circles
    
    *if (andellise_loved)
        You pass by the spot where you realized your love for Andellise. You sick furry.
    
    *goto layer_1_kiwi_george

*if (skip_mode = "normal")
    You pass by a petite squirrel.
    *if (ghost_mode = "ghost")
        It quickly runs off, squeaking in terror.
        *goto layer_1_kiwi_george
    
*if ((knowledge_blue_coins) and (unlocked_squirrel_war))
    *set met_andellise true
    
    She's offering you a blue coin. "This is in the honor of Edmington, the true king. He wishes to bestow upon you, stranger, his royal thanks for being part of his soon-to-be-conquered kingdom."
    
    "Isn't he all the way in layer 5?" You ask.
    
    "Why, yes, just one layer away!" Says the petite squirrel, which you've decided to call Andellise. "Also, my name is Andellise." Oh, what a coincidence. Honestly, you should take a picture of this moment, that's kinda creepy how you were able to guess her name moments before you said it. You sure you haven't seen this squirrel before?
    
    "Uh," you say, still getting over how weird it was that you knew Andellise's name. "This is the [i]first[/i] layer."
    
    "Wait… when I bonked my head on that river and got lost, I ended up all the way here. In [b]Charles'[/b] country??" The squirrel shivers in fear. "Well, in that case, get me out of here!"
    
    *choice
        #Take the squirrel with you on your journeys
            *set diary ("This squirrel's got some attitude problems. Why am I carrying her around again?[n/][n/]" & diary)
            
            Andellise points out how, since you're not in the fourth layer, you actually aren't considered a (soon-to-be) subject of Edmington and therefore do not deserve the Blue Coin
            
            *choice
                #Okay, that's it, death it is
                    Andellise quickly hands you the blue coin and jumps back into your backpack.
                    
                    [+1 Blue Coin]
                    *set inventory_blue_coins + 1
                    *set inventory_andellise true
                    *set layer_1_kiwi_andelisse_blue_coin_taken true
                    
                    Guess you'll bear her for a little bit longer.
                    
                    *goto layer_1_kiwi_george
                #Fine, whatever
                    Andellise holds her head up high, "a single squirrel against a human, and the squirrel wins!" She thinks. But your character doesn't hear that, so you missed your chance to kill her. (Don't worry, you'll get more).
                    *set inventory_andellise true
                    
                    *goto layer_1_kiwi_george
        #Leave Andellise to die
            You walk away. Not your problem, tbh.
            
            *goto layer_1_kiwi_george
        #Kill the squirrel myself.
            *set diary ("Squeak is what squirrels say before they die, right Andellise? Oh right, you can't answer because you're dead. *maniacal laughter*[n/][n/]" & diary)
            
            *set andellise_dead true
            
            "Squeak" Is the last thing she says before she's dead. Squeak, squeak, squeak, snap goes the squirrel's neck. The eyes are dead and stare off in the distance!
            
            Okay, that was a morbid, sure, but what did you expect from this option?
            
            Oh, and you get the blue coin she had, too.
            
            [+1 Blue Coin]
            *set inventory_blue_coins + 1
            *set layer_1_kiwi_andelisse_blue_coin_taken true
            *goto layer_1_kiwi_george

*label layer_1_kiwi_george
    
*if (george_gone)
    You bump into George on your way exploring. At least, that's how it feels as you wander into some fields with a very distinct "George" vibe. He always liked big open fields. You remember the time when he was training you and he took you out this way to gather kiwis. You decide to sneak by layer two to get some in his honor.
    
    [+3 Kiwi]
    *set inventory_kiwi + 3
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if (ghost_mode = "ghost")
    You bump into George on your way exploring, or, well, more literally just pass through him. He doesn't notice you, but shivers a little.
    
    *return

*if (skip_mode = "normal")
    You bump into George on your way exploring. "Oh, hey!" He says. "I'm just heading to the second layer, but if you've got enough experience here you can even join me!"

*if ((experience < 20) and (not (pass_unlocked_blue)))
    *set diary ("George is gatekeeping the second layer from me *sad*.[n/][n/]" & diary)
    
    "Oh, what's that?" George says. You only have ${experience} experience? Well, I'd like at least 20 to show you the second layer, since it can be pretty dangerous.
    
    However, here's some free experience!
    
    [+6 Experience]
    *set experience + 6
    
    *goto_scene {home_base_scene} {home_base_label}
*if (skip_mode = "normal")
    *if (pass_unlocked_blue) and (not (pass_unlocked_blue_properly))
        "Oh, it looks like you got a blue pass from somewhere! Couldn't have been me, I don't remember! Well, I trust that's a good, functioning blue pass so right this way!"
    *if (pass_unlocked_blue) and (pass_unlocked_blue_properly)
        "Just show them the blue pass I gave you and you should be A OK to get through!"
    *if (not (pass_unlocked_blue))
        "Well, looks like you have enough!" Says George after analyzing your "show stats" screen. "So, do you care to join me?"

*if (skip_mode = "skip")
    *if (decision_point = "39")
        *set decision_point "40"
        *goto layer_1_kiwi_kiwi
*choice
    #Yes!
        *goto layer_1_kiwi_kiwi
    #I'm Lame
        Well, you don't really find much else on your trip, unfortunately.
        *goto_scene {home_base_scene} {home_base_label}

*label layer_1_kiwi_kiwi

*set diary ("George let me keep the juiciest of kiwis from our gather trip together.[n/][n/]" & diary)

*if (skip_mode = "normal")
    "I'm going on a harvest mission to harvest Kiwis. They're often given as a sign of forgiveness, and are most common here in the second layer. If you help out, I'll let you have a couple."

    After a while of helping George gather up a bunch of Kiwis, he lets you keep two. But at least they seem to be the plumpest.

[+2 Kiwis]
*set inventory_kiwi + 2

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_ghost

*set layer_1_ghost_visits + 1

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    *goto layer_1_ghost_is_ghost

*if (layer_1_ghost_visits >= 2)
    *if ((inventory_map_kit) and (not (mapped_layer_1_ghost)))
        *set mapped_layer_1_ghost true
        *set diary ("I just came up with the best name for this one river I just mapped![n/][n/]" & diary)

        You get to map the place with that ghost out finally now that you have your map kit. You see a stick floating in the river, so you decide to call it "Running Stick River".
        
        [Mapped Location]
        
    *if (not (layer_1_gravestone_grave_respects_paid))
        You come across the ghost again.
        
        *goto layer_1_ghost_no_respects
    *if (layer_1_ghost_tim_quest_offered_red)
        You come across Tim again and he asks if you have the black hair yet. When you shake your head, he goes "Tsk, tsk, little orphan girls are so useless!" Then he disappears. Why does everyone know magically know you're an orphan? And why are they making fun of you for it? Ugh! Anyways, here's your Ectoplasm.
        
        [+4 Ectoplasm]
        *set inventory_ectoplasm + 4
        
        *goto_scene {home_base_scene} {home_base_label}
    *if (layer_1_ghost_tim_quest_offered)
        You come across Tim again.
        
        *goto layer_1_ghost_black_hair
    *if (layer_1_gravestone_grave_respects_paid)
        You come across the ghost again.
        
        *goto layer_1_ghost_paid_respects

*set diary ("Ah! I saw a ghost.[n/][n/]" & diary)

You are walking alongside a fast-flowing river located deep inside a forest when behind you, you hear someone shout, "boo!" It spooks you, but not too much as you know it's just a nefarious traveler looking to scare some others. You turn around, though, and can't see anyone. Weird.

*if ((inventory_map_kit) and (not (mapped_layer_1_ghost)))
    *set mapped_layer_1_ghost true
    *set diary ("I just came up with the best name for this one river I just mapped![n/][n/]" & diary)
    
    You think of how pretty this place is, and how it probably deserves to be put on the map. You see a stick on the river and decide to call this place "Running Stick River".
    
    [Mapped Location]

You continue on and then hear the same "Boo!" again, this time closer. This is getting a little freaky.You continue on a little more and come across a ring of flowers. Spooky. Then a mysterious translucent, white figure appears in this circle and shots "Boo!" again. This time, you jump and almost run.

"Don't run! I'm just playing!" Says the figure, a little boy no older than 7. Well, it's the [i]ghost[/i] of a little boy, that is.

*if (ghost_mode = "ghost")
    "Oh, a fellow ghost! I see you retain some humanness about you. Must have accidentally passed through a ghost portal. Good luck getting your human form back!" He cheers.
    
    *return

"Uh, hello," you say, having never spoken to a ghost before.

*if (layer_1_gravestone_grave_respects_paid)
    *goto layer_1_ghost_paid_respects

*label layer_1_ghost_no_respects

"Oh," says the ghost, "I don't really talk to people who haven't paid my grave respects. Then he disappears, leaving some ectoplasm on the ground for you to harvest.

[+4 Ectoplasm]
*set inventory_ectoplasm + 4

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_ghost_paid_respects

*comment TODO: Add ghost side-quests

*set met_tim_ghost true

"Thank you for paying respects to my grave by the way." Says the ghost. "My name is Tim Bayer. I'll let my brethren know that you're ghost-friendly. Also, here's some of my ectoplasm as a gift!" You scoop the ectoplasm into your bag.

[+4 Ectoplasm]
*set inventory_ectoplasm + 4

*if (knowledge_blue_coins)
    "Oh, and here's a blue coin I found in the meantime as well!"
    
    [+1 Blue Coin]
    *set inventory_blue_coins + 1

*label layer_1_ghost_questions

*choice
    #Ask the ghost if it needs avenging
        *goto layer_1_ghost_questions_avenge
    #Continue on
        *goto_scene {home_base_scene} {home_base_label}
    
*label layer_1_ghost_questions_avenge

"Common misconception," states Tim, "Not all ghosts are ghosts due to a murder or other violent death, sometimes they just are."

"Ah, so you just died of an illness or som…" You start to say.

"I WILL ALWAYS REMEMBER THE DAY SHE MURDERED ME. HER NAME WAS ASHLEE STRONG…"

*page_break

Tim goes on to tell you about a small red-headed girl who pushed him into the river. How he thought this girl was his friend (and future wife) and how he swore revenge against her. Of course, the original red-headed girl had long since passed away peacefully in her sleep at the age of 84 (or so little Tim had heard from another traveler). So anyone with red hair would do at this point.

He just wants a lock of red hair, and he'll be happy.

*choice
    #Ask Tim if he realizes I have red hair?
        *goto layer_1_ghost_questions_avenge_2

*label layer_1_ghost_questions_avenge_2

Tim informs you that it's nothing personal, and to watch out in these parts since he's told a lot of travelers a lot of things about ghostly rewards if a lock of red hair is brought to him.

*choice
    #Offer Tim a lock of my red hair as a symbol.
        *goto layer_1_ghost_questions_avenge_3

*label layer_1_ghost_questions_avenge_3

Tim says that since you're clearly still alive, it doesn't count.

*choice
    #Swear to Tim that I'll avenge him.
        *goto layer_1_ghost_questions_avenge_4
    
*label layer_1_ghost_questions_avenge_4

Tim smiles and claps, then vanishes. You cut off a lock of your hair and stick it in your pocket for the next time you come across that stupid red-head-hating ghost.

*set layer_1_ghost_tim_quest_offered true
    
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_ghost_black_hair

*set layer_1_ghost_tim_quest_offered_red true

When you show Tim the lock of red hair, he frowns and shakes his head. "No, no, I said it was [b]black[/b] hair that she had." You give him a look. Seriously? "Just bring me back the correct hair color this time." He says, then disappears, leaving behind 4 Ectoplasm and… no blue coin.

[+4 Ectoplasm]
*set inventory_ectoplasm + 4

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_ghost_is_ghost

You hover about along a fast flowing river with a stick in it until you hear another ghost yelling "boo" at you.

*if ((inventory_map_kit) and (not (mapped_layer_1_ghost)))
    *set mapped_layer_1_ghost true
    *set diary ("I just came up with the best name for this one river I just mapped![n/][n/]" & diary)

    Oh, and it looks like you haven't mapped this place yet, so let's give it a name for your map. How about… "Running Stick River"?
    
    [Mapped Location]

*if (met_tim_ghost)
    It's that Tim ghost again. You steer clear of him, since something's off.
    
    *return
*if (layer_1_ghost_visits >= 2)
    Ayy, it's that other ghost. The boy one. You wave.
    
    "Another ghost! I thought most of them disowned me after… uh, well, hi! My name's Tim."
    
    You hover around with Tim for a bit then continue on. If only you could offer to avenge the poor boy, but unfortunately only humans can make such offers.
    
    *return

You wave.

"Oh," says the other ghost, who apparently died as a young boy. "Another ghost! I thought most of them disowned me after… uh, well, hi! My name's Tim."

You might come back here to offer avenging later if you were a human, but ghosts can't offer that to other ghosts, so best to just continue on your way now.

*return

*label layer_1_materials

*if ((inventory_map_kit) and (not (mapped_layer_1_materials)))
    *set diary ("I was able to map out that place with the building materials.[n/][n/]" & diary)
    
    *set mapped_layer_1_materials true
    
    You make sure to put this place on your map since you haven't yet before.

You see some ectoplasm, but it's moving! You need high nimblicity skill to catch it.

*if (researched_observation_1)
    {Check: Nimblicity Skill >= 20}

    *if (not (nimblicity_skill >= 20))
        {Fail}
    *if (nimblicity_skill >= 20)
        {Pass}

*if (nimblicity_skill >= 20)
    You caught it!
    
    [+3 Ectoplasm]
    *set inventory_ectoplasm + 3
*if (nimblicity_skill < 20)
    Oop, there it goes.

*if (not (ghost_mode = "ghost"))
    *set layer_1_materials_visits + 1

*set diary ("I found plenty of building materials for that base.[n/][n/]" & diary)

*if (skip_mode = "normal")
    You find some building materials on the ground next to an oak tree.

*if (layer_1_base_visits > 0)
    These would be perfect for the clearing you found!
    
[+4 Building Materials]

*set inventory_building_materials + 4

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if (ghost_mode = "ghost")
    *goto layer_1_phantoshop

*comment TODO: Different response if you're a ghost
*if (layer_1_materials_visits = 2)
    While rummaging through the building materials, you find two dead squirrels.
    
    "Isn't it sad," squeaks a third squirrel behind you. "I mentored them both, and here they are dead from fighting. I'm Peasant Palpatine, by the way."
    
    *if (unlocked_squirrel_war)
        "Yeah…" You say, witnessing the cruelty of the current squirrely political turmoil.
    *if (not (unlocked_squirrel_war))
        *set diary ("I learned about the Squirrel Wars today.[n/][n/]" & diary)
        
        "Yeah, uh, I guess if times are tough food wis…" You begin, still offput by the fact that a squirrel is talking to you.
        
        "No no! It's not that." Says the elder squirrel. "They are fighting in the name of Charles and Marge, the two aristocrats who wish to rule the Squirrel Kingdom." Then, the elder explains all about the squirrel war to you.
        
        [+Encyclopedia Entry]
        *set unlocked_squirrel_war true
        
    "Well, I better get going." Says the Elder. "I hope you don't engage in this mess."

*if (layer_1_materials_visits = 3)
    You find a nut underneath the building materials!
    
    *set inventory_nuts + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_phantoshop

Just when you're about to leave, you hear someone shouting, "for sale, for sale," in a loud booming salesman's voice. It's a phantoshop!

*choice
    *if (not (layer_1_phantoshop_bought_nut))
        *selectable_if (inventory_ectoplasm >= 15) #Buy a nut [-15 Ectoplasm, +1 Nut]
            [-15 Ectoplasm]
            *line_break
            [+1 Nut]
            *set inventory_ectoplasm - 15
            *set inventory_nuts + 1
            
            ——————————————————————————————
            *goto layer_1_phantoshop
    *if (not (research_unlocked_exploration_2))
        *selectable_if (inventory_ectoplasm >= 8) #Buy the Exploration II book for the orphanage [-8 Ectoplasm]
            [-8 Ectoplasm]
            *line_break
            [Unlocked Research: Exploration II]
            *set inventory_ectoplasm - 8
            *set research_unlocked_exploration_2 true
            
            ——————————————————————————————
            *goto layer_1_phantoshop
    *selectable_if (inventory_rosalia >= 130) #Bulk sell rosalia [-130 Rosalia, +11800 Doubloons]
        [-130 Rosalia]
        *line_break
        [+11800 Doubloons]
        *set inventory_rosalia - 130
        *set doubloons + 11800
        
        ——————————————————————————————
        *goto layer_1_phantoshop
    #Continue on
        *return

*label layer_1_button

*set diary ("I wonder how this weird button wall even works.[n/][n/]" & diary)

You find a wall with some buttons on it. Pressing a given button will give you a bonus that can only be collected elsewhere. You can also leave an offering of lemens to increase the reward.

*temp multiplier 1

*label layer_1_button_choice

*temp rosalia_to_leave 5
*set rosalia_to_leave * multiplier

*temp doubloons_to_leave 100
*set doubloons_to_leave * multiplier

*temp building_materials_to_leave 3
*set building_materials_to_leave * multiplier

*gosub_scene logic update_total_lemens

*choice
    *if (knowledge_blue_coins)
        #Create ${multiplier} Blue Coins in a place with a backpack
            There's a certain place that when you explore it now will give you ${multiplier} blue coins.
            
            *set layer_1_button_extra_blue_coins + multiplier
            
            *goto_scene {home_base_scene} {home_base_label}
    #Create ${rosalia_to_leave} Rosalia in a Rosalia Patch... very creative
        
        Next time you visit the Rosalia patch, there will be ${rosalia_to_leave} extra rosalia there
        
        *set layer_1_button_extra_rosalia + rosalia_to_leave
        
        *goto_scene {home_base_scene} {home_base_label}
    #Create ${doubloons_to_leave} Doubloons right before a forest of scrolls
        Next time you visit the forest of scrolls (you don't have to go inside), there will be ${doubloons_to_leave} extra Doubloons there.
        
        *set layer_1_button_extra_doubloons + doubloons_to_leave
        
        *goto_scene {home_base_scene} {home_base_label}
    *if (layer_1_base_visits > 0)
        #Create ${building_materials_to_leave} building materials next to a starblossom
            Next time you visit the starblossom encounter, there will be ${building_materials_to_leave} extra building materials there to collect.
            
            *set layer_1_button_extra_building_materials + building_materials_to_leave
            
            *goto_scene {home_base_scene} {home_base_label}
    *if (multiplier = 1)
        *selectable_if (total_lemens >= 10) #Increase the amount of things left by the button by 2x [-10 Lemens]
            [-10 Lemens]
            *set logic_lemens_to_remove 10
            *gosub_scene logic remove_lemens
            
            *set multiplier 2
            
            *goto layer_1_button_choice
    *if (multiplier = 1)
        *selectable_if (total_lemens >= 100) #Increase the amount of things left by the button by 5x [-100 Lemens]
            [-100 Lemens]
            *set logic_lemens_to_remove 100
            *gosub_scene logic remove_lemens
            
            *set multiplier 5
            
            *goto layer_1_button_choice
    #Continue on
        *goto layer_1_button_button_eval

*label layer_1_button_button_eval

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_starblossom

First, you come across a dead squirrel corpse. Ew.
*if (unlocked_squirrel_war)
    Probably just another tragic casualty of the Squirrel Wars.

*if (researched_observation_1)
    {Check: Ghost}

    *if (not (ghost_mode = "ghost"))
        {Fail}
    *if (ghost_mode = "ghost")
        {Pass}

*if ((not (ghost_mode = "ghost")) or (not (unlocked_squirrel_war)))
    *goto layer_1_starblossom_petunal

Over top of it is a ghost squirrel that you would have probably never seen if not for being a ghost yourself.

"I am Benjamin the Dead-jamin. I command a skeletal squirrel army 1500 strong. However, I ask 2 nuts of you in order to be persuaded to any cause."

*set temp_affiliate "squirrel_affiliation_deadjamin"
*set temp_affiliate_strength_var "squirrel_affiliation_deadjamin_strength_var"
*set logic_nuts_required 2
*set logic_squirrel_strength_offered 1500

*gosub_scene logic squirrel_wrapper

*label layer_1_starblossom_petunal

You find a Petunal Blossom on the ground.

*if (not (knowledge_petunal_blossoms))
    What's a Petunal Blossom? Well, I, the narrator, will tell you.
    
    [Encyclopedia Entry: Petunal Blossoms]
    *set knowledge_petunal_blossoms true

[+1 Petunal Blossom]
*set inventory_petunal_blossoms + 1

*label layer_1_starblossom_starblossom

*if (not (knowledge_starblossom))
    Next, you wander past some big star-shaped-leafed plants and find nothing else.
*if (knowledge_starblossom)
    *if (not (inventory_shtg2))
        Next, you find a starblossom! Now, if only you had the special harvesting tool for it…
    *if (inventory_shtg2)
        Good thing you had the SHTG2 to harvest it!
        
        [+1 Starblossom]
        *set inventory_starblossom + 1
    
*if (layer_1_button_extra_building_materials > 0)
    Right next to the starblossom, there are extra building materials.
    
    [+${layer_1_button_extra_building_materials} Building Materials]
    *set inventory_building_materials + layer_1_button_extra_building_materials

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_hint_garden

*set diary ("It's the... HINT GARDEN.[n/][n/]" & diary)

*set layer_1_hint_garden_last_visited months_passed

You come across the hint garden. It's not really a garden. Just a place outside the fourth wall where I tell you the next step, and help you along your way there.

*temp net_months_passed months_passed
*set net_months_passed - starting_months_passed

*if (researched_observation_1)
    {Check: Energy > 0}

    *if (not (energy > 0))
        {Fail}
    *if (energy > 0)
        {Pass}

*if (energy = 0)
    You're too tired to take a hint. Maybe you should go home and rest.
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*comment Early hints

*if ((experience < 60) and (not (layer_1_hint_garden_hint_given_months_passed)))
    *set layer_1_hint_garden_hint_given_months_passed true
    
    Enjoy your youth! You're not too experienced, so there won't be as much you can do. Here is some experience and doubloons, though, to get you started!
    
    [+60 Doubloons]
    *line_break
    [+4 Experience]
    *set doubloons + 60
    *set experience + 4
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((experience < 60) and (layer_1_hint_garden_hint_given_months_passed))
    Visiting back so soon? What bad luck! Let me just send you back up to the encounters page so you can get something different. I'll also give you some lemens for your time.
    
    [+3 Lemens]
    *comment It's 11-lemens rather than the usual 13-lemens for the meme
    *set inventory_lemens_11 + 3
    
    *if (ghost_mode = "ghost")
        *return
    *goto layer_1_general_exploration

*comment Important hints

*if (((not (pass_unlocked_blue)) and (unlocked_level_up)) and (not (layer_1_hint_garden_hint_given_level_up)))
    *set layer_1_hint_garden_hint_given_level_up true
    
    It looks like you haven't unlocked the blue pass yet. Here's 2 Petunal Blossoms since I know they can be hard to find.
    
    [+2 Petunal Blossoms]
    *set inventory_petunal_blossoms + 2
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((((knowledge_blue_coins) and (not (layer_1_hint_garden_hint_given_blue_coins))) and (not (has_street_attire))) and (pass_unlocked_blue))
    *set layer_1_hint_garden_hint_given_blue_coins true
    
    You don't see to have street attire yet. Here's a blue coin. Go to the Blue Coin Clothing store to buy some.
    
    [+1 Blue Coin]
    *set inventory_blue_coins + 2
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((months_passed >= 200) and ((experience < 100) and (not (layer_1_hint_garden_hint_given_experience_1))))
    *set layer_1_hint_garden_hint_given_experience_1 true
    
    You have so little experience! So sad. Here's some, poor one.
    
    [+8 Experience]
    *set experience + 8
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if (((not (can_fight)) and (not (layer_1_hint_garden_hint_given_fight))) and (months_passed >= 210))
    *set layer_1_hint_garden_hint_given_fight true
    
    You don't know how to fight yet? Well let me teach you!
    
    [Encyclopedia Page: Fighting]
    *set can_fight true
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((months_passed >= 220) and ((objective_layer_2_visits = 0) and (not (layer_1_hint_garden_hint_given_layer_2_visits))))
    *set layer_1_hint_garden_hint_given_layer_2_visits true
    
    You haven't visited layer 2 a single time? Really? Here, let's go there now.
    
    *comment Impossible to be ghost in this case
    *goto layer_2

*comment Less important hints

*if ((experience >= 100) and ((not (mapped_layer_1_keys)) and (not (layer_1_hint_garden_hint_given_key_shop))))
    *set layer_1_hint_garden_hint_given_key_shop true
    
    It looks like you haven't unlocked the key shop yet. I'd recommend doing that ASAP! It unlocks a lot of new locations.
    
    Here, I'll actually do that for you and give you some Special Dirt to get you started with it.
    
    *set mapped_layer_1_keys true
    [+6 Special Dirt]
    *set inventory_special_dirt + 6
    
    *if (inventory_shovel)
        (Since this was given rather than gathered, there are no shovel multipliers on it).
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if (((town_market_square_booths_last_month_visited = 0) and (net_months_passed >= 31)) and (not (layer_1_hint_garden_hint_given_market_square)))
    *set layer_1_hint_garden_hint_given_market_square true
    
    It looks like it's been over two years and you still have bought anything from Market Square. You should do that, since they often have much better or unique deals than the general store. Here's 250 Doubloons to help get you going there.
    
    [+250 Doubloons]
    *set doubloons + 250
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((job_experience_farm_hand < 4) and (not (layer_1_hint_garden_hint_given_farm_experience)))
    *set layer_1_hint_garden_hint_given_farm_experience true
    
    It looks like you haven't worked a second time yet (or somehow lost farm hand experience). Let's fix that! The farm hand experience problem, I mean, I won't force you to work, don't worry.
    
    [+3 Farm Hand Experience]
    *set job_experience_farm_hand + 3
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if (((gardens_orphanage_num_times_harvested = 0) and (unlocked_orphanage_garden)) and (not (layer_1_hint_garden_hint_given_garden)))
    *set layer_1_hint_garden_hint_given_garden true
    
    You should start planting and harvesting from the orphanage gardens if you haven't yet already. They're an especially good source of petunal blossoms, which are hard to obtain in the beginning.
    
    Here's some basic flower seeds in case that's the thing that's been holding you back.
    
    [+2 Basic Flower seeds]
    *set inventory_flower_seeds_1 + 2
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((not (layer_1_hint_garden_hint_given_base_marker)) and (not (layer_1_base_built_marker)))
    *set layer_1_hint_garden_hint_given_base_marker true
    
    You should build a marker in that one clearing so that you can return to it easily and build a base there. In fact, I'll just do that for you right now!
    
    *set layer_1_base_built_marker true
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((not (unlocked_squirrel_war)) and (not (layer_1_hint_garden_hint_given_squirrel_war)))
    *set layer_1_hint_garden_hint_given_squirrel_war true
    
    You don't know about the squirrel wars yet? Catch up with the times! Here's an encyclopedia page to fill you in on the deets.
    
    [Encyclopedia Page: Squirrel Wars]
    *set unlocked_squirrel_war true
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((not (researched_reading_1)) and (not (layer_1_hint_garden_hint_given_reading_1)))
    *set layer_1_hint_garden_hint_given_reading_1 true
    
    You haven't researched Reading 1 yet. Here's some Rosalia to help you out with that!
    
    [+6 Rosalia]
    *set inventory_rosalia + 6
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*comment Filler hints

*if ((not (job_offered_advanced_farm_hand)) and (not (layer_1_hint_garden_hint_given_advanced_farm_hand)))
    *set layer_1_hint_garden_hint_given_advanced_farm_hand true
    
    Looks like you haven't unlocked the advanced farm hand yet. Here's some farm hand experience to get you on your way with that.
    
    [+3 Farm Hand Experience]
    *set job_experience_farm_hand + 3
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
*if ((lemen_fetch_skill <= 9) and (not (layer_1_hint_garden_hint_given_lemen_fetch)))
    *set layer_1_hint_garden_hint_given_lemen_fetch true
    
    Your lemen fetch skill is low. You get to meet someone special in the market if you have a certain number of lemens. Let's help to fix the fetch skill problem. You go gather the lemens.
    
    [+3 Lemen Fetch Skill]
    *set lemen_fetch_skill + 3
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

*comment TODO: Give ability to have special encounter (or something like that) if you get to this point

I don't have any more hints at the moment! More will unlock later, but for now, here, take this starblossom as a symbol of your good progress!

[+1 Starblossom]
*set inventory_starblossom + 1

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_dragon

*set diary ("Wtf it's a frickin dragon.[n/][n/]" & diary)

*comment Make this relevant later.

[b]Bonus Encounter from Exploration I Skill[/b]

With your newfound exploration skills, you're able to navigate this one hill a lot better. The hill overlooks a large, sweeping rocky desert valley, but there's a fence that indicates that the desert is layer 2 material. Tumbleweeds blow across the desert, and the fence is tantalizingly short here. In fact, the desert continues on, a flat desolate wasteland, all the way to layer 3, where there are lush mountains. And peaking behind the mountains, you see a large vicious bird.

*if (inventory_clackadoo_feather > 0)
    Your clackadoo feather, even from this distance, starts to shake.

*comment TODO: Way to get over the fence

No, that's not a bird, it's a dragon. It seems beyond the third layer, so in layer 4, but that's still quite early for such beasts. It's green and its scales glint in the natural light of the Tunnel. It's so far away it appears stationary, but you know it must be traveling at the speed of sound. Often, that's why people can't even hear the dragons coming, or at least that's what others have told you.

You watch it until it disappears behind a mountain, then head back.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_overhang

*set diary ("I saw this massive cliff today. It blotted out the Sun on the plains below it.[n/][n/]" & diary)

[b]Bonus Encounter from Exploration I Skill[/b]

You come across a dip in the ground that goes beneath an overhanging cliff. This is exactly the sort of place you'd expect to find a cave, but you don't see any. Heck, it's hard to see anything at all with the cliff blocking the Sun.

*if (ghost_mode = "ghost")
    Actually, scratch that, you have ghost vision!
    
    *choice
        #Go beneath the overhang
            *goto layer_1_overhang_scythe
        #Explore elsewhere around the area
            *goto layer_1_overhange_no_torch
            
*if (inventory_torch = 0)
    You better come back here with a torch if you want to make any progress searching this place.
    *goto layer_1_overhange_no_torch
*if (inventory_torch > 0)
    You better use a torch if you want to make any progress searching this place
    
    *choice
        *selectable_if (inventory_torch >= 1) #Use a torch to explore beneath the overhang [-1 Torch]
            *set inventory_torch - 1
            *goto layer_1_overhang_scythe
        #Nah
            *goto layer_1_overhange_no_torch

*label layer_1_overhang_scythe

*if (researched_observation_1)
    {Check: Energy >= 6}

    *if (not (energy >= 6))
        {Fail}
    *if (energy >= 6)
        {Pass}

*if (not (layer_1_overhang_scythe_taken))
    *if (energy <= 5)
        *set diary ("I found a magic scythe today. I was so tired that I was scared to pick it up. I regret not taking it, honestly. Would probably help with farm hand jobs a lot.[n/][n/]" & diary)
        
        There's this shiny scythe thing leaning up against the cliff base with some runes around it, but you're so tired you can't make out what the runes are saying to you. "Better not mess with anything with magic runes around it," your sleepy brain tells you.
        
        [Requires 6 Energy]
        
        *if (ghost_mode = "ghost")
            *return
        *goto_scene {home_base_scene} {home_base_label}
    
    *set diary ("I found a magic scythe today. This will help a lot in any farmhand jobs.[n/][n/]" & diary)
    
    *set artifact_has_magic_scythe true
    *set layer_1_overhang_scythe_taken true
    
    You find a shiny scythe leaning up against the cliff base. Around it are some runes. They rearrange into the words, "Take it, bruh, it's all yours." Wow, a magic scythe. You never thought you'd find an aritfact in the first layer.
    
    [Artifact: The Magic Scythe]
    *line_break
    [Encyclopedia Entry: The Magic Scythe]
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}
    
*comment TODO: More stuff here

You don't find anything more here.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_overhange_no_torch

You find nothing else on your journey

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_colosseum

[b]Bonus Encounter from Exploration I Skill[/b]

*if (months_passed < 220)
    Only those aged at least 220 months can be hear. Bugger off.
    
    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

Not much here yet…

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_shrine_of_good

[b]Bonus Encounter from Exploration I Skill[/b]

You enter a place surrounded by starblossoms. There are also twelve large stone pillars in a circle. The starblossoms are already harvested, so you could take them, but you get the feeling that that's a bad idea.

*comment TODO: In future, deliver this info somewhere else

You read on a sign that this is the Shrine of Good. Anyone may enter, as long as they haven't done a single morally wrong thing in their lives. Seems pretty straightforward. You walk in.

*if (not (layer_1_shrine_of_good_bad_deed = ""))
    Unfortunately, you're instantly repelled by a forcefield for the following wrong deed which you did on month ${layer_1_shrine_of_good_bad_deed_month}.
    
    ${layer_1_shrine_of_good_bad_deed}
    
    This was the first time you ever did something wrong, and it set you on the path of evil that you're on today.
    
    You can no longer visit the Shrine of Good in this lifetime, but maybe future members of your dynasty will be able to harvest from its riches. Or you could just cleanse yourself in a holy fountain. I'll probably implement one sometime.

    *if (ghost_mode = "ghost")
        *return
    *goto_scene {home_base_scene} {home_base_label}

Somehow, you're in the clear. You've done nothing wrong. Hot damn we got a Leslie Knope in our presence.

Next, you have to be at least 320 months old. Otherwise, you basically haven't done anything wrong by default.

What's that? You're still a youngin'? That explains it. Just don't do anything wrong and then come back here when you're older and you'll get to see what's inside!

*comment TODO: Write part for when they do become 320 months old

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*label layer_1_training

[b]Bonus Encounter from Exploration I Skill[/b]

*if ((not (training_done_layer_1_extra_encounter)) and (unlocked_trainings))
    You get a training. From whom? Don't ask me, it was you who did it.
    
    [Training]
    *set training_done_layer_1_extra_encounter true
    *set exploration_trainings + 1

*if (researched_observation_1)
    {Check: Energy >= 6}

    *if (not (energy >= 6))
        {Fail}
    *if (energy >= 6)
        {Pass}

*if ((not (research_unlocked_exploration_2)) and (energy >= 6))
    You unlocked a research… Exploration II. You still need to research it, but it's available to be researched in the orphanage at least.
    
    [Unlocked Research: Exploration II]
    *set research_unlocked_exploration_2 true
*if (energy < 6)
    You pass by a book but you're too tired to pick it up.

*if (ghost_mode = "ghost")
    *return
*goto_scene {home_base_scene} {home_base_label}

*comment ////////////////////////////////////////////////////////////
*comment // General exploration encounters are ABOVE this
*comment ////////////////////////////////////////////////////////////

*label layer_1_gather_lemens

You fetch some lemens.

[+${lemen_fetch_skill} Lemens]
*line_break
[+1 Lemen Fetch Skill]
*set inventory_lemens_13 + lemen_fetch_skill
*set lemen_fetch_skill + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_gather_rosalia

You fetch some rosalia.

[+${rosalia_fetch_skill} Rosalia]
*line_break
[+1 Rosalia Fetch Skill]
*set inventory_rosalia + rosalia_fetch_skill
*set rosalia_fetch_skill + 1

*if (layer_1_button_extra_rosalia > 0)
    You also gather ${layer_1_button_extra_rosalia} extra Rosalia from the button's grace.
    
    [+${layer_1_button_extra_rosalia} Rosalia]
    *set inventory_rosalia + layer_1_button_extra_rosalia
    *set layer_1_button_extra_rosalia 0

*comment Encounter triggered in "dagger" encounter
*if (layer_1_dagger_flying_key_has)
    Nothing happens. Nothing else.
    
    *page_break
    
    Why the page break without sending you back to the orphanage then? Ha, what do you think.
    
    *page_break
    
    It has to do with your little flying friend. You see, it started squeaking and… well, something exciting happened!
    
    *page_break
    
    You found the rosalia lockbox.
    
    *choice
        #Open it with the key
            *goto layer_1_gather_rosalia_lockbox
        #Let the key fly off and give up on this sidequest
            *set layer_1_dagger_flying_key_has false
            [Lost Winged Key]
            
            Bruh, there was literally no drawback to the other option and you can get cool stuff. Do you not like cool stuff?
            
            *goto_scene {home_base_scene} {home_base_label}

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_gather_rosalia_lockbox

*set layer_1_dagger_flying_key_has false
*set layer_1_dagger_flying_key_used true

You open the lockbox to find…

*page_break

Nothing. What a waste of time.

*comment Little did they know that something did happen...
*set layer_1_dagger_flying_key_used_month months_passed

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_tracking_question

*if (tracking_question = 1)
    What is the vampire's name?
    
    *choice
        #Selena
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Amy
            *goto layer_1_tracking_question_return
        #Amanda
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 2)
    What gem is inside the cave?
    
    *choice
        #Ruby
            *goto layer_1_tracking_question_return
        #Emerald
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Diamond
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 3)
    Which one is not the name of a layer 1 squirrel?
    
    *choice
        #Henry
            *goto layer_1_tracking_question_return
        #Charles
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Duke Squirrel
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Alice
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Peasant Palpatine
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 4)
    How is the sword in the stone described?
    
    *choice
        #Fantabulous
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Majestic
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Imaginary
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Had to pull out
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Mirage-y
            *goto layer_1_tracking_question_return
*if (tracking_question = 5)
    How many Petunal Blossoms did the guy with the note leave you?
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #8
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 6)
    How much does a 5x multiplier cost for the button?
    
    *choice
        #10 Lemens
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #100 Lemens
            *goto layer_1_tracking_question_return
        #There is no 5x multiplier
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 7)
    What pokemon do you come across?
    
    *choice
        #Exeggcute
            *goto layer_1_tracking_question_return
        #Eggxecute
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Pikachu
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 8)
    Where do you get hints?
    
    *choice
        #The Hint Garden
            *goto layer_1_tracking_question_return
        #The Hint Pile
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 9)
    What type of thing is Duke's heirloom?
    
    *choice
        #A lemen
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #A piece of furniture
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #A nut
            *goto layer_1_tracking_question_return
*if (tracking_question = 10)
    Where does the button leave the doubloons?
    
    *choice
        #Beneath the cave.
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #With Henry.
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #In the same place as where the dagger is.
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #In the place with the scrolls.
            *goto layer_1_tracking_question_return
*if (tracking_question = 11)
    How many building materials next to the oak tree do you get?
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #8
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 12)
    What type of harvesting tool is used for starblossoms?
    
    *choice
        #SGTH2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #SHTG2
            *goto layer_1_tracking_question_return
        #SGHT2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 13)
    What do you use to leave your respects?
    
    *choice
        #Lemen
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Rosalia
            *goto layer_1_tracking_question_return
        #Petunal Blossoms
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Starblossoms
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 14)
    Where does the lever leave the blue coins?
    
    *choice
        #The place with the dagger
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #The place with the backpack
            *goto layer_1_tracking_question_return
*if (tracking_question = 15)
    You describe the ghost boy as no older than…
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *goto layer_1_tracking_question_return
        #8
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 16)
    How many building materials does each step of your base normally take?
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #8
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *goto layer_1_tracking_question_return
*if (tracking_question = 17)
    What kiwis does George let you keep on your mission to Layer 2 together?
    
    *if (george_gone)
        Oh, uhhh… insensitive question my bad. Don't worry about answering this one.
        *page_break
        *goto layer_1_tracking_question_return
    
    *choice
        #The juiciest one
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #The worst one
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #The two juiciest ones
            *goto layer_1_tracking_question_return
        #The two worst ones
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 18)
    What do you need to build to return to your structure in the clearing?
    
    *choice
        #A marker
            *goto layer_1_tracking_question_return
        #Nothing
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #You just need a map
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 19)
    How much Special Dirt does the outskirts temple take?
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #8
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 20)
    What is the color of the old dirt man's hair?
    
    *choice
        #Black
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Gray
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Brown
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Green
            *goto layer_1_tracking_question_return
        #He has no hair
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 21)
    Is Henry fond of you?
    
    *choice
        #He hasn't met me
            *if (met_henry)
                *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #He's fond of me
            *if ((not (met_henry)) or (not (fond_henry)))
                *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #He's not fond of me
            *if ((not (met_henry)) or (fond_henry))
                *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 22)
    What are Petunal Blossoms used for?
    
    *choice
        #Teas
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Food
            *goto layer_1_tracking_question_return
        #Decoration
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 23)
    Is the number of lemens you have right now even, or odd?
    
    *gosub_scene logic update_total_lemens
    
    *choice
        #Even
            *if ((total_lemens modulo 2) = 1)
                *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Odd
            *if ((total_lemens modulo 2) = 0)
                *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 24)
    How many months do lemens last?
    
    *choice
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #11
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #12
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #13
            *goto layer_1_tracking_question_return
        #14
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 25)
    How much does a torch cost in the general store? Good luck LOL.
    
    *choice
        #182
            *goto layer_1_tracking_question_return
        #183
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #184
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #185
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 26)
    How many equipments does it take to travel into the first layer?
    
    *choice
        #0
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #1
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #2
            *goto layer_1_tracking_question_return
        #3
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #4
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #5
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #6
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #7
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #8
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #9
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #10
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 27)
    Is Aria (the author's cat) cute?
    
    *choice
        #Yes
            *goto layer_1_tracking_question_return
*if (tracking_question = 28)
    You fail at tracking. Yeah, there are "questions" like these.
    
    *choice
        #Yes
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 29)
    Which of these answers is correct?
    
    *choice
        #This one
            *goto layer_1_tracking_question_return
        #No This one
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #Actually This one  
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #No Really This one   
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
*if (tracking_question = 30)
    Which of these answers is correct?
    
    *choice
        #This one
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #No This one
            *goto layer_1_tracking_question_return
        #Actually This one  
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return
        #No Really This one   
            *set tracking_answer_wrong true
            *goto layer_1_tracking_question_return

*label layer_1_tracking_question_return

*return

*label layer_1_tracking

You go tracking.

*comment TODO: More different tracking rewards

*temp layer_1_tracking_reward 0
*rand layer_1_tracking_reward 1 1

*temp layer_1_tracking_question_1 0
*temp layer_1_tracking_question_2 0
*rand layer_1_tracking_question_1 1 30
*rand layer_1_tracking_question_2 1 30

*page_break

*set tracking_answer_wrong false
*set hide_stats true

You find some ectoplasm on the ground. Whatever it is you're after must have died a long time ago. Doesn't matter to you whether it's dead or alive, though; you're going to kill this beast either way.

[+2 Ectoplasm]
*set inventory_ectoplasm + 2

*set tracking_question layer_1_tracking_question_1

*gosub layer_1_tracking_question

*if (tracking_answer_wrong)
    *goto layer_1_tracking_failed

You find more ectoplasm. You're getting close.

[+3 Ectoplasm]
*set inventory_ectoplasm + 3

*set tracking_question layer_1_tracking_question_2

*gosub layer_1_tracking_question

*if (tracking_answer_wrong)
    *goto layer_1_tracking_failed

*set hide_stats false

*set diary ("I found an animal![n/][n/]" & diary)

You succeeded!

Here's some tracking meat. Idc if it makes sense for the thing you were tracking, use your fucking imagination.

[+3 Tracking Meat]
*set inventory_tracking_meat + 3

You come across a dead Wooly Mammoth's dead ghost. That is, the ghost is dead. Well, the Wooly Mammoth, too, obviously. You get the point.

You harvest all the ectoplasm you can.

[+15 Ectoplasm]
*set inventory_ectoplasm + 15

You also find some rosalia left by other ghosts to mark its passing. Don't mind you if you do!

[+3 Rosalia]
*set inventory_rosalia + 3

Finally, if you don't already have the ghost key, you get one! Wow, that's one loaded Mammoth. Mhmmm. Gosh, now I'm thinking of a "mammoth sized buffalo burger" and I'm hungry!

*if (not (key_owned_ghost_key))
    [Acquired Ghost Key]
    *line_break
    [Encyclopedia Entry: Ghost Key]
    *set key_owned_ghost_key true

*goto_scene {home_base_scene} {home_base_label}

*label layer_1_tracking_failed

*set hide_stats false

You failed.

*goto_scene {home_base_scene} {home_base_label}

*comment ////////////////////////////////////////////////////////////
*comment // Layer 2 TAG_LAYER_2
*comment ////////////////////////////////////////////////////////////

*label layer_2

*if (researched_artifacts_1)
    *set layer_2_persistence_visits + 1

*if (layer_2_persistence_visits = 2)
    *set diary ("Woah, just unlocked the Equipipen![n/][n/]" & diary)
    
    You unlocked a persistence artifact! It's the Equipipen. Each trip requires one fewer equipments (but not fewer than zero).
    
    [Encyclopedia Entry: Equipipen]
    *set artifact_has_equipipen true

*set objective_layer_2_visits + 1

*choice
    #General exploration [+2 Months, +18 Experience]
        *gosub_scene logic month_passed
        *gosub_scene logic month_passed
        
        [+18 Experience]
        *set experience + 18
        
        *goto layer_2_general_exploration
    *if (mapped_layer_2_mine)
        *selectable_if (doubloons >= 50) #Visit the Blue Coin Mines [-50 Doubloons, +1 Month]
            *gosub_scene logic month_passed
            
            [-50 Doubloons]
            *set doubloons - 50
            
            *goto layer_2_mine
    *if (mapped_layer_2_kiwi_trainer)
        #Visit the Kiwi Trainer [+1 Month, +8 Experience]
            *gosub_scene logic month_passed
            
            [+8 Experience]
            *set experience + 8
            
            *goto layer_2_kiwi_trainer
    *if (mapped_layer_2_portal)
        #Visit the ghost portal [+1 Month, +16 Experience]
            *gosub_scene logic month_passed
            
            [+16 Experience]
            *set experience + 16
            
            *goto layer_2_portal
    *if (not (layer_2_monument_mountain_name = ""))
        *selectable_if (energy >= 3) #Visit the Mountain I'm planning to put a Monument on [+1 Month, -3 Energy, +10 Experience]
            *gosub_scene logic month_passed
            
            [-3 Energy]
            *line_break
            [+10 Experience]
            *set energy - 3
            *set experience + 10
            
            *goto layer_2_monument
    *selectable_if (energy >= 1) #Scavenge for Special Dirt [-1 Energy, +11 Special Dirt]
        [-1 Energy]
        *set energy - 1
        
        *goto layer_2_special_dirt
    *if (false)
        *if ((months_passed >= 230) and (not (layer_2_the_boss_defeated)))
            #Fight The Boss
                *goto layer_2_the_boss
*comment TODO: Reimplement The Boss

*label layer_2_general_exploration

*temp encounter_2 0
*rand encounter_2 1 20

*set diary ("Let's get this bread! Or, err, sandwhich, cuz it's two layers, get it?[n/][n/]" & diary)

*set layer_2_num_general_explorations + 1

You go exploring.

*page_break

*if (encounter_2 = 1)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_1)))
        *set layer_2_blue_coin_encounter_1 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_patricia
*if (encounter_2 = 2)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_2)))
        *set layer_2_blue_coin_encounter_2 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_cave
*if (encounter_2 = 3)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_3)))
        *set layer_2_blue_coin_encounter_3 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_goblin
*if (encounter_2 = 4)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_4)))
        *set layer_2_blue_coin_encounter_4 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_mangrove
*if (encounter_2 = 5)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_5)))
        *set layer_2_blue_coin_encounter_5 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_gather_kiwis
*if (encounter_2 = 6)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_6)))
        *set layer_2_blue_coin_encounter_6 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_portal
*if (encounter_2 = 7)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_7)))
        *set layer_2_blue_coin_encounter_7 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_max
*if (encounter_2 = 8)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_8)))
        *set layer_2_blue_coin_encounter_8 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_mine
*if (encounter_2 = 9)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_9)))
        *set layer_2_blue_coin_encounter_9 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_potions
*if (encounter_2 = 10)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_10)))
        *set layer_2_blue_coin_encounter_10 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_kiwi_trainer
*if (encounter_2 = 11)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_11)))
        *set layer_2_blue_coin_encounter_11 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_monument
*if (encounter_2 = 12)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_12)))
        *set layer_2_blue_coin_encounter_12 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_starblossom
*if (encounter_2 = 13)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_13)))
        *set layer_2_blue_coin_encounter_13 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_spiders
*if (encounter_2 = 14)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_14)))
        *set layer_2_blue_coin_encounter_14 true
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_battleground
*if (encounter_2 = 15)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_15)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_15 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_tso
*if (encounter_2 = 16)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_16)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_16 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_artifact_dungeon
*if (encounter_2 = 17)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_17)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_17 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_temple_of_knowledge
*if (encounter_2 = 18)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_18)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_18 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_coin_pile
*if (encounter_2 = 19)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_19)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_19 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_mother_of_specters
*if (encounter_2 = 20)
    *if ((knowledge_blue_coins) and (not (layer_2_blue_coin_encounter_20)))
        *set diary ("Narrator guy just threw me a Blue Coin for finding a new layer 2 encounter![n/][n/]" & diary)
        
        You get a blue coin for finding a new layer 2 encounter!
        *set layer_2_blue_coin_encounter_20 true
        
        [+1 Blue Coins]
        *set inventory_blue_coins + 1
    *goto layer_2_scrolls

*label layer_2_patricia

*comment TODO: Add squirrel encounter here

You pass by a rock.

*if (researched_materials_1)
    [+1 Cool Pointy Blue Rock]
    *set inventory_cool_pointy_blue_rock + 1
    *set total_collected_cool_pointy_blue_rock + 1

*comment TODO: Make Patricia give a discount as you visit her more and more

*if (training_done_with_patricia)
    You come across Patricia again and she yells out "thanks again for letting me train you!"
    
    You also find some special dirt.
    
    [+5 Special Dirt]
    *set inventory_special_dirt + 5
    
    *if (inventory_shovel)
        Your shovel allows you to rake up even more Special Dirt
        
        [+5 Special Dirt]
        *set inventory_special_dirt + 5
    
    *goto layer_2_patricia_brock

*set layer_2_patricia_campsite_visits + 1

*comment TODO: Address the following... What if you met patricia elsewhere?
*if (layer_2_patricia_campsite_visits >= 2)
    You come across Patricia again.
    
    *goto layer_2_patricia_offer

*set met_patricia true

*set diary ("Just met this chick named Patricia at a campfire.[n/][n/]" & diary)

*temp patricia_age 300
*if (months_passed >= 230)
    *set patricia_age 400
*if (months_passed >= 330)
    *set patricia_age 500
*if (months_passed >= 430)
    *set patricia_age 600
*comment TODO: Make patricia die or smth after 530 months

You find a campsite fire. As you get close to it, you see the slender figure of a fit woman. She looks to be around ${patricia_age} months old (maybe a little older). She has long, brunette hair and you can see rosy cheeks. She looks up. "Hello!" She says cheerily. "I come out here often, here have a bite.

You sit down on the log next to her and she hands you some rabbit jerky. "I actually live around here, by the way, so you're free to visit if you ever need."

*comment TODO: Make it so you can travel back here with map

*if (unlocked_trainings)
    "I also train people."
*if (not (unlocked_trainings))
    "I can also train you. In what, you ask? Well, you'll need a certain number of 'trainings' before you can get a yellow pass, one that can get you to the third layer. You'll also need to do a test, but you can just cheat those honestly. Cheated mine. Honestly not too much of a test person myself."
    
    *set unlocked_trainings true

"For training though, I'll need some shiny things to make it worth my time. Doubloons! You know what I'm talking about."

*label layer_2_patricia_offer

*choice
    *if (not (training_done_with_patricia))
        *selectable_if (doubloons >= 1200) #Give Patricia some Doubloons for a training [-1200 Doubloons, +1 Trainings]
            *set diary ("Wow, this Patricia chick just trained me, and, like, she knows stuff.[n/][n/]" & diary)
            
            *set doubloons - 1200
            *set exploration_trainings + 1
            *set training_done_with_patricia true
            
            After that training, you're ready to head back home, so you do.
            
            *goto_scene {home_base_scene} {home_base_label}
    #Continue
        *goto layer_2_patricia_brock

*label layer_2_patricia_brock

Then, you come across Brock, another trainer, but of a different type. Unfortunately, you can't interact with him yet.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_cave

*set diary ("Big cave is big.[n/][n/]" & diary)

Like, a really big cave. Kyle had to write a lot to really flesh out the cave. But you'll need a lot of torches, trust me. First, I'm gonna ask for three. But you better have more on you. Who knows what it'll take to get out of this thing once you go inside.

*choice
    *selectable_if (inventory_torch >= 3) #Use some torches to light your way inside the dark cave [-3 Torches]
        [-3 Torches]
        *set inventory_torch - 3
        
        *goto layer_2_cave_enter
    #Don't go in the cave
        *goto layer_2_cave_around

*label layer_2_cave_enter

You head into the cave. It strikes fear into your heart, for some reason. It's big, black, and ominous. The torch struggles to light the whole thing. In fact, it doesn't do it at all. It burns out quite quickly, as if the darkness is consuming it. You take out another torch just for it to meet the same fate. As you start to light yet another torch, you notice it's defective. This isn't good.

*if (inventory_torch = 0)
    Oops, looks like that was your last torch. I did say I might ask more torches of you, didn't I?
    *goto layer_2_cave_failed

*choice
    *selectable_if (inventory_torch >= 1) #Head back [-1 Torch]
        [-1 Torch]
        *set inventory_torch - 1
        
        *goto_scene {home_base_scene} {home_base_label}
    *selectable_if (inventory_torch >= 4) #Trudge forward [-4 Torches]
        [-4 Torches]
        *set inventory_torch - 4
        
        *goto layer_2_cave_split

*label layer_2_cave_split

You come across a split in the caves as torch after torch gets burned. After a bit, you need to hold two in your hand at once to have enough light. It seems like the darkness is literally consuming the torches. You come across a fork in the cave. Which way do you go?

*comment TODO past this point

*if (inventory_torch < 3)
    Uh, wait. Hol' up. You have less than three torches? Nope, not enough. Out.
    
    *goto layer_2_cave_failed

*choice
    *selectable_if (inventory_torch >= 1) #Head back [-3 Torches]
        [-3 Torches]
        *set inventory_torch - 3
        
        *goto_scene {home_base_scene} {home_base_label}
    *selectable_if (inventory_torch >= 6) #Head to the path on the left [-6 Torches]
        [-6 Torches]
        *set inventory_torch - 6
        
        *goto layer_2_cave_left
    *selectable_if (inventory_torch >= 14) #Head to the path on the right [-14 Torches]
        [-14 Torches]
        *set inventory_torch - 14
        
        *goto layer_2_cave_right
    
*label layer_2_cave_right

*if (not (general_event_occurred_george_gave_glowshroom))
    Wha… what? You're not supposed to have somehow gotten this far this early in the game. I haven't even triggered certain events I assume to have happened in the following passage! So, er, what I mean is…
    
    You mysteriously get booted back home and find 500 extra Doubloons in your inventory.
    
    [+500 Doubloons]
    *set doubloons + 500
    
    Just wait a bit or go down the other way please, uwu?
    *goto {home_base_scene} {home_base_label}

[Encyclopedia Entry: Glowshroom Flower]
*set knowledge_glowshroom_flower true

Thankfully, you come across a bright area full of glowshroom flowers. At least, you're finding these things in a cave with mushrooms, and they're glowing, so you assume that's what they are. You've heard George talking about how beautiful these were, once, and how he'd give any girl he fell in love with one. He never really described what they looked like. He only described them as the most beautiful plant he'd ever laid his eyes on. They give off a soft glow, but the sheer number of them light up the whole cavern. There are thousands if not tens of thousands of plants here. All emitting that soft glow from their translucent, white leaves.

*if (george_gone)
    You miss George.

You decide to pick some and head back

[+3 Glowshroom Flower]
*set inventory_glowshroom_flower + 3

*goto {home_base_scene} {home_base_label}

*label layer_2_cave_left

Torch after torch extinguishes. You pull out 50 torches at once only to find them all defective. Wait, did you even have 50 more torches?

*if (inventory_torch < 50)
    You needed 50 extra torches. I mean, like, you could have just came prepared?
    *goto layer_2_cave_failed

You did. Thank goodness. Anyways.

[-50 Torches]
*set inventory_torch - 50

You throw the 50 extra torches on the ground in frustration and light them all on fire. A big explosion occurs and every single torch you've thrown out of the way previously in the cave magically lights up. Except one. You decide to name that torch Bob, after the lazy boy back at the orphanage who you don't like. On the side of the wall, you can see a door that now starts to glow. You couldn't have seen it before in the darkness.

*if (artifact_has_language_orb)
    You notice an inscription on the top of the door. You can't quite make out what it says, but luckily you have a language orb to help you out. Once you take it out and bring its energies to life, you see that the inscription says, "You can only really find this place if you're bright enough. Get it?"
    
    You groan.

*choice
    #Go in the spooky door.
        *goto layer_2_cave_door
    *selectable_if (inventory_torch > 2000) #Continue on the dark path. You'll probably want a bulk torch dealer for this one. [-2000 Torches]
        *comment TODO
        
        You were cheesed! This hasn't been written yet.
        
        *goto {home_base_scene} {home_base_label}
    #Go back along the lit up torches to see if there's anything you missed.
        There wasn't anything that was written in the story yet, at least.
        
        *goto {home_base_scene} {home_base_label}
        
*label layer_2_cave_door

Wow, this is becoming quite an expedition. But it starts to pay off, as now you are entering the Land of Lemens. Or so it appears, because they're everywhere. But this place gives spooky vibes. Probably shouldn't take any of those lemens. But then you see a really plump one right in front of your nose. Like, surely no on will mind if you take it, right?

*comment Implement willpower skill to decide if even looking away is a choice later.

*choice
    #Take the plump, shiny lemen.
        *goto layer_2_cave_door_took_one
    #Don't do it.
        *goto layer_2_cave_door_left
    #Heck, I'm taking all the lemens!
        *goto layer_2_cave_door_took_all

*label layer_2_cave_door_took_one

You take the single lemen and dart out of that place as fast as you can. When you get back and analyze it in the Sun, you notice it's just a higher quality lemen, but a normal lemen all the same. Huh, all those torches for a single lemen? Maybe you should have taken all the lemens instead of being a wuss.

[+1 Lemen]
*set inventory_lemens_13 + 1

*goto {home_base_scene} {home_base_label}

*label layer_2_cave_door_left

You leave, but you probably should have taken some of the lemens first. Like, seriously you wasted all those torches for nothing lol.

*goto {home_base_scene} {home_base_label}

*label layer_2_cave_door_took_all

You're midway through loading your bag with lemens when you hear a booming voice.

"Who is this?" The voice booms. Wait, didn't I already say it was booming? I forget. You forget too as you drop your bag and look around for the nearest exit. This isn't good. "Ah, I see you love lemens. Why'd you drop them? Were those ones not good enough? Here, what about these." Then, like 50 new lemens sprout from the sides of the walls, all of them even more magnificent than the ones you picked.

"Ohhh, I think I see," said the voice, "you can't carry everything! Well, I guess this is a good time to practice my throw." Then a lemen magically catapults through the air and into the wall. [i]Splat[/i]. "Oops, like I said I need to practice," Says the voice. Then it goes again and the Lemen whooshes through the air directly into your face.

*page_break

You wake up later outside the cave, but your blue pass is gone.

Haha, did I get you? It's actually over there underneath that massive pile of lemens. Yeah, it's a pretty massive pile. You could spend a couple extra months getting these before the next person, or just take what you can now. What do you say?

*choice
    #Take all the lemens [+2 Months, +6100 Lemens]
        *gosub_scene logic month_passed
        *gosub_scene logic month_passed
        
        [+6100 Lemens]
        *set inventory_lemens_13 + 6100
        
        *goto layer_2_cave_door_took_all_all
    #Take as many as you can carry now [+1200 Lemens]
        [+1200 Lemens]
        *set inventory_lemens_13 + 1200
        
        *goto layer_2_cave_door_took_all_some

*label layer_2_cave_door_took_all_all

You take everything. Every single lemen. You can barely hold everything in the orphanage. It's, uh, kind of ridiculous honestly. Like, 6100 is a big, arbitrary number. That's a lot.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_cave_door_took_all_some

You carry as many as you can at once, which, for plot purposes, happens to be 1200 lemens. That's a lot of frickin lemens. You silently wonder, though, if it was worth all those torches.

*goto_scene {home_base_scene} {home_base_label}
    
*label layer_2_cave_failed

*set pass_lost_blue_from_cave true

You mysteriously wake up outside the cave later, but your blue pass is gone.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_cave_around

You pass by a rock.

*if (researched_materials_1)
    [+1 Cool Pointy Blue Rock]
    *set inventory_cool_pointy_blue_rock + 1
    *set total_collected_cool_pointy_blue_rock + 1

You cross some yellow flowers with short petals. They have a sweet, buttery smell to them.

*choice
    #Eat them
        I'm just gonna stop your character right there. You'll thank me later.
        
        *goto_scene {home_base_scene} {home_base_label}
    #Continue On
        *if (not (knowledge_mother_of_specters))
            If only you knew what they were, you might be able to harvest them for something.
        *if (knowledge_mother_of_specters)
            Hey, they're safe to harvest as long as you don't eat them!
        
        *goto_scene {home_base_scene} {home_base_label}
    *if (knowledge_mother_of_specters)
        #Harvest the Mother of Specters 
            You carefully harvest the flowers.
            
            [+2 Mother of Specters]
            *line_break
            [+1 Mother of Specters Fetch Skill]
            *set inventory_mother_of_specters + 2
            *set skill_fetch_mother_of_specters + 1
            *goto_scene {home_base_scene} {home_base_label}

*label layer_2_goblin

*set diary ("Goblin crippling skills go brrrrr.[n/][n/]" & diary)

*comment TODO

You come across the path of a goblin. It looks hungry. "Give me 100 doubloons or you'll feel my wrath," it squeaks.

It's so small you're able to wrestle it to the ground and rip its arm off. It runs away screaming.

[+4 Goblin Meat]
*set inventory_goblin_meat + 4

*goto_scene {home_base_scene} {home_base_label}

*comment TODO TODO TODO

*if met_courtney
    *goto general_exploration_layer_2_courtney_hello

You come across the path of a goblin. It looks hungry. "Give me 100 doubloons or you'll feel my wrath," it squeaks.

*choice
    *selectable_if (doubloons >= 100) #Give the mighty goblin the doubloons it demands. [-100 Doubloons]
        *goto general_exploration_layer_2_goblin_gift
    #Wrestle the goblin to the ground
        *goto general_exploration_layer_2_goblin_fight
    #Tell the goblin you don't have the money
        *goto general_exploration_layer_2_goblin_empty

*label general_exploration_layer_2_goblin_gift

*set doubloons - 100

The goblin is pretty nasty and hoggles your coins all up. Then, she kicks you in the shin and laughs. "Filthy human," she says. Maybe next time you should just buy a weapon. Honestly, though, it wasn't as bad as it could have been, really.

*goto_scene {home_base_scene} {home_base_label}

*label general_exploration_layer_2_goblin_fight

You jump onto the goblin and it screams. It bites and scratches and puts up a good fight. You manage to rip off its arm before it wiggles free and runs off crying. You know that goblins regenerate limbs, though, so it's not gonna die or anything. What a wimp. At least you got some goblin meat during this journey.

*set inventory_goblin_meat + 4

*goto_scene {home_base_scene} {home_base_label}

*label general_exploration_layer_2_goblin_empty

*comment Later, add some logic to detect if you lied and do things later based on that.

You tell the goblin you don't have 100 coins. She looks at you curiously and then sighs. "It's hard times isn't it?" She says. You didn't realize goblins could speak the common tongue, also known as English.

"Yeah," you answer, just hoping not to be robbed.

"Well, if you ever come across me again, I'll show you to my home, we can talk and all. Really sorry, I didn't mean to take all your coins, just trying to get by." And then she walks away, adding over her shoulder, "My name is Courtney by the way!"

What a strange interaction.

*set met_courtney true

*goto_scene {home_base_scene} {home_base_label}

*label general_exploration_layer_2_courtney_hello

*comment TODO

You see Courtney and wave hi to her. That goblin's starting to wear on you! She smiles as you pass. Unfortunately, I haven't gotten around to fully writing this section yet, so you don't get anything else much on this expedition.

*goto_scene {home_base_scene} {home_base_label}

*comment END TODO

*label layer_2_mangrove

*set diary ("Something seems off about this mangrove forest. Like I've seen or heard of places like this before. If only I were bright enough I could put my lamp, or, err, finger on it.[n/][n/]" & diary)

*if (not ((layer_1_cave_sword_seen) or (layer_1_dagger_sword_seen)))
    You wander through an ominous mangrove forest, but find nothing but a few Doubloons.
    
    [+4 Doubloons]
    *set doubloons + 4

*comment TODO: Lantern bit

At least, that is, until you come across the QuestGiver. The QuestGiver will give you quests to complete by giving you a hint as a riddle of what you are supposed to do. Return here once you've done your quest.

*comment: Somewhere to remind the current QuestGiver quest

*if (layer_2_mangrove_quest_given = 0)
    *set layer_2_mangrove_quest_given + 1
    
    Your next quest is very simple,
    *line_break
    Visit market square in the month,
    *line_break
    That building materials are in stock

    Your main reward for this quest will be: A social vest.
*if ((not (layer_2_mangrove_quest_given = 0)) and (not (layer_2_mangrove_quest_completed)))
    Looks like you haven't completed the last QuestGiver quest. Come back here after you've done that!
*if (layer_2_mangrove_quest_completed)
    *if (layer_2_mangrove_quest_given = 1)
        Congratulations, your reward is 30 experience and a social vest.
        
        [+30 Experience]
        *line_break
        [Acquired Social Vest]
        *set experience + 30
        *set has_social_vest true

*goto_scene {home_base_scene} {home_base_label}

You actually triggered a rare event, but I haven't implemented it yet, so back home you go!

*goto_scene {home_base_scene} {home_base_label}

*comment I have no idea what this is for?
*if (layer_2_mangrove_found_lantern)
    *comment TODO: Add some more detail to this branch
    
    You wander around but find nothing.
    
    *goto_scene {home_base_scene} {home_base_label}

*set layer_2_mangrove_found_lantern true
*set artifact_lantern_month_end months_passed
*set artifact_lantern_month_end + 4

You trip and fall over something and when you turn over, the sky is dark! You must have hit your head. If this were layer 1, this would be no problem, but this is layer 2. You hope desperately that you're alone, but as you turn to the right, you see something bright, and your heart drops. It's a lantern of hope.

The lantern of hope is a layer 2 artifact that only appears to people who need some hope in their lives. In other words, you're about to get into some deep shit and you need some help, so this lantern popped by.

At least, most of the time the lantern saves the day, but not always. You must have triggered something very bad when you tripped and fell.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_gather_kiwis

*comment TODO: Time traveling sub-plot where you in the past is the person talking to George

*set diary ("Ooh, look. George is harvesting kiwis.[n/][n/]" & diary)

You spot George in the distance harvesting some Kiwis. However, he's with someone, so you decide not to bother him. Instead, you harvest some kiwis on your own.

*if (george_gone)
    *set diary ("Oh, right. George isn't here.[n/][n/]" & diary)
    
    Wait, George?
    
    You look again, but there's no one there. You're just seeing things.
    
    Grief overtakes you, and you head back home.
    
    *goto layer_2_gather_kiwis_rock_pile

*set diary ("Kiwiiii trifeeeeecta. Kiwi TRIIIIFECCTA. And shit I just took two kiwis instead of three.[n/][n/]" & diary)

The most important thing about harvesting kiwis, other than it being George's favorite task in the second layer, is the Kiwi trifecta (which you learned from George). You must always do things with kiwis in threes. Plant kiwis in threes, harvest them in threes, have only multiples of threes numbers of kiwis in your inventory, etc.

*if (george_gone)
    Oh, George… You wipe the thought of George away.

That's why when you're walking back with only two kiwis harvested, you feel immense disappointment. You couldn't have just gotten a third one? How long does it take to harvest a motherfucking kiwi?

[+2 Kiwis]
*set inventory_kiwi + 2

*comment TODO: Bonuses for multiples of higher powers of 3

*if ((inventory_kiwi modulo 3) = 0)
    But, as it turns out, you actually did yourself a favor by increasing the number of kiwis you have to exactly a multiple of 3. Good job! The Sun shines bright on you.

You keep in mind to check various places for those who appreciate the kiwi trifecta when you have high kiwi level (your kiwi level is the highest power of three that divides your number of kiwis). Instead of doing math, you can also just check the Misc. section of stats.

*label layer_2_gather_kiwis_rock_pile

As you walk home, you find a massive rock pile.

*if (researched_materials_1)
    [+6 Sharp Gray and White Rocks]
    *line_break
    [+1 Indistinct Smooth Pebble Rock]
    *set inventory_sharp_gray_and_white_rock + 1
    *set inventory_indistinct_smooth_pebble_rock + 1
    *set total_collected_sharp_gray_and_white_rock + 1
    *set total_collected_indistinct_smooth_pebble_rock + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_portal

*set layer_2_portal_visits + 1

*if (layer_2_portal_visits >= 2)
    You come across the ghost portal again. However, it seems disabled. Looks like it'll take some ectoplasm to fire it up again.
    
    *temp encounter 0
    *temp num_encounters 20
    *if (researched_exploration_1)
        *set num_encounters + 5
    *if (researched_exploration_2)
        *set num_encounters + 5
    *rand encounter 1 num_encounters
    
    *choice
        *selectable_if (inventory_ectoplasm >= 10) #Fire up the portal again and head inside!
            *set inventory_ectoplasm - 10
            *goto layer_2_portal_enter
        #Continue home
            *goto_scene {home_base_scene} {home_base_label}

*set diary ("Woah, I actually visited the first layer, but as a ghost![n/][n/]" & diary)

You are wandering around and look up a little too late.

Before you know it, you've wandered through the ghost portal that turns you into a ghost. Now, turning back into human form isn't so easy. You'll need to wander around layer 1 for the corresponding anti-ghost portal. You can't just walk backwards through the ghost portal, no no. Silly you. So time to explore in layer 1!

*label layer_2_portal_enter

*set layer_2_portal_enter_visits + 1

*set ghost_mode "ghost"

*gosub layer_1_general_exploration

Then you see the human portal! You fly through the arch and then head back home. What an adventure!

*set ghost_mode "normal"

*comment TODO: Put this at the top!

*if ((inventory_map_kit) and (not (mapped_layer_2_portal)))
    *set diary ("I mapped this place, now I can return here whenever I want.[n/][n/]" & diary)
    
    I should map this place, that was quite an experience, maybe using the ghost portal I can find some more things in the future.
    
    [Mapped Location]

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_max

*if (not (squirrel_alive_max))
    *comment TODO: Change this when there become other ways to kill Max
    
    You pass the marker of a squirrel grave on your path. It says "Here lies Max, once ruler of the Red-haired squirrels of layer 2. Killed battling Fred's army."
    
    *goto layer_2_max_rock

You pass a squirrel on your path.

*if (not (unlocked_squirrel_war))
    It squeaks at you ferociously, but you just kick it out of your way and continue back home.
    
    *goto_scene {home_base_scene} {home_base_label}

*comment TODO: Flesh this out

*set diary ("Oh another squirrel.[n/][n/]" & diary)

*label layer_2_max_max

"I am Max, the king of all Red-haired squirrels of layer 2!" Squeaks the squirrel that is apparently Max. "Despite my mighty stature, I would do anything for a nut. Who should I follow?"

*set temp_affiliate "squirrel_affiliation_max"
*set temp_affiliate_strength_var "squirrel_affiliation_max_strength_var"
*set logic_nuts_required 1
*set logic_squirrel_strength_offered 350

*gosub_scene logic squirrel_wrapper

*label layer_2_max_rock

Then, you continue on. The only other thing you find is a smooth rock.

*if (researched_materials_1)
    [+1 Indistinct Smooth Pebble Rock]
    *set inventory_indistinct_smooth_pebble_rock + 1
    *set total_collected_indistinct_smooth_pebble_rock + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_mine

*if (mapped_layer_2_mine)
    *set diary ("Oh, so this is where all those blue coins come from.[n/][n/]" & diary)
    *set mapped_layer_2_mine true

*if (not (knowledge_blue_coins))
    There's a sign next to a rocky path that explains this place as a mine for blue coins, and tells you what blue coins are.
    
    [Encyclopedia Entry: Blue Coins]
    *set knowledge_blue_coins true
    
*if (inventory_blue_coin_pickaxe_uses > 0)
    You arrive prepared at the blue coin mines with your trusty pick in hand.
    
    [-1 Blue Coin Pickaxe Uses]
    *line_break
    [+6 Blue Coins]
    *set inventory_blue_coin_pickaxe_uses - 1
    *set inventory_blue_coins + 6
    
    *goto_scene {home_base_scene} {home_base_label}

You come across the place where most people mine their blue coins. You'll have to get a special tool if you want to collect more than one of these things at a time, though!

[+1 Blue Coins]
*set inventory_blue_coins + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_potions

*set diary ("I came across a Lemen shop today![n/][n/]" & diary)

*comment TODO: More detailed

You come across a little lemen-shaped shop in the middle of the woods. There's a friendly witch with a big wart on her nose. She seems very obsessed with lemens, though, to the point that even her wart is lemen-shaped. You dare say it might have been put there on purpose.

She tells you about how she wants all your lemens, unlike that guy in town who just thinks he wants lemens but really just wants to show off how rich he is. No, she just wants lemens.

However, she also has a bit of a quirk. She only wants ripe lemens. Unripened lemens are no good to her, there are plenty of those as she lives in the tunnel. She doesn't have time to leave the tunnel, have them ripen, then return, though. First, she assesses your lemens to see how many months of ripening each one has left until it dies. Lemen ripening phases are often given names based on which rooms they'd be most appropriate to light. The lemens of the "fresh" variety actually are dimmer than hall lemens since they're just starting to shed their outer layers, which aren't the bright ones.

[b]Your Lemen Ripeness Report[/b]
*line_break
Freshly Picked Lemens (13 months left): ${inventory_lemens_13}
*line_break
Fresh Lemens (12 months left): ${inventory_lemens_12}
*line_break
Relatively Fresh Lemens (11 months left): ${inventory_lemens_11}
*line_break
Hall Lemens (10 months left): ${inventory_lemens_10}
*line_break
Kitchen Lemens (9 months left): ${inventory_lemens_9}
*line_break
Bedroom Lemens (8 months left): ${inventory_lemens_8}
*line_break
Tabletop Lemens (7 months left): ${inventory_lemens_7}
*line_break
Reading Lemens (6 months left): ${inventory_lemens_6}
*line_break
Dimlemens (5 months left): ${inventory_lemens_5}
*line_break
Spicy Baker's Lemens (4 months left): ${inventory_lemens_4}
*line_break
Bright Baker's Lemens (3 months left): ${inventory_lemens_3}
*line_break
Ripe Baker's Lemens (2 months left): ${inventory_lemens_2}
*line_break
Spoiled Lemens (1 month left): ${inventory_lemens_1}

*temp callie_lemens_available 0
*gosub layer_2_potions_update_lemens

This witch, her name is Callie, will only accept Baker's lemens. That is, ones with 2, 3, or 4 months left on them. She will take from your two-month-old lemens first, then the 3-month-old ones, then the 4-month-old ones. Therefore, right now you have ${callie_lemens_available} Lemens of interest to her.

*label layer_2_potions_shop

What would you like to buy? Potions have immediate effects (gotta use them they spoil!), and last only a certain amount of time. Their effects don't stack. Buying another just extends duration time.

*gosub layer_2_potions_update_lemens
Recall that you have ${callie_lemens_available} lemens of interest to Callie.

*choice
    *selectable_if (callie_lemens_available >= 4) #Potion of Restlessness [-4 Baker's Lemens, +2 Energy each Month for 13 Months]
        *set logic_lemens_to_remove 4
        *gosub layer_2_potions_pay_lemens
        
        *set effect_end_potion_restless_1 months_passed
        *set effect_end_potion_restless_1 + 13
        
        *goto layer_2_potions_shop
    *selectable_if (callie_lemens_available >= 5) #Potion of XP [-5 Baker's Lemens, +2 Experience each Month for 13 Months]
        *set logic_lemens_to_remove 5
        *gosub layer_2_potions_pay_lemens
        
        *set effect_end_potion_experience_1 months_passed
        *set effect_end_potion_experience_1 + 13
        
        *goto layer_2_potions_shop
    *selectable_if (callie_lemens_available >= 7) #Potion of Strength [-7 Baker's Lemens, +1 Base Fight Power for 13 Months]
        *set logic_lemens_to_remove 7
        *gosub layer_2_potions_pay_lemens
        
        *set effect_end_potion_strength_1 months_passed
        *set effect_end_potion_strength_1 + 13
        *goto layer_2_potions_shop
    #Nothing
        *goto_scene {home_base_scene} {home_base_label}

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_potions_update_lemens

*set callie_lemens_available + inventory_lemens_4
*set callie_lemens_available + inventory_lemens_3
*set callie_lemens_available + inventory_lemens_2

*return

*label layer_2_potions_pay_lemens

*if (inventory_lemens_2 > logic_lemens_to_remove)
    *set inventory_lemens_2 - logic_lemens_to_remove
    *set logic_lemens_to_remove 0
    *return
*set inventory_lemens_2 0
*set logic_lemens_to_remove - inventory_lemens_2

*if (inventory_lemens_3 > logic_lemens_to_remove)
    *set inventory_lemens_3 - logic_lemens_to_remove
    *set logic_lemens_to_remove 0
    *return
*set inventory_lemens_3 0
*set logic_lemens_to_remove - inventory_lemens_3

*if (inventory_lemens_4 > logic_lemens_to_remove)
    *set inventory_lemens_4 - logic_lemens_to_remove
    *set logic_lemens_to_remove 0
    *return
*set inventory_lemens_4 0
*set logic_lemens_to_remove - inventory_lemens_4

*return

*label layer_2_kiwi_trainer

*set diary ("Wow, a real-life kiwi trainer! Uh, whatever that is.[n/][n/]" & diary)

*if ((inventory_map_kit) and (not (mapped_layer_2_kiwi_trainer)))
    You jot down the place you visit on your map really quickly.
    
    *set mapped_layer_2_kiwi_trainer true

You come across a kiwi trainer.

*if (not (unlocked_trainings))
    However, as you have not learned about trainings yet, he pays no attention to you and you have to move on.
    
    *goto_scene {home_base_scene} {home_base_label}

He says he will give you trainings, once per kiwi level. That is, the first time you visit him, you must have kiwi level 1 for him to train you. Then the second time, it must be kiwi level 2. And so on.

*gosub_scene logic update_kiwi_level

Your kiwi level is: ${kiwi_level}
*line_break
You have trained: ${layer_2_kiwi_trainer_num_trainings}
*line_break
Your kiwi level must be greater than the number of times you've trained to receive a training.

*if (kiwi_level <= layer_2_kiwi_trainer_num_trainings)
    You don't have the kiwi level for the kiwi trainer to train you, so you wander back home.
*if (kiwi_level > layer_2_kiwi_trainer_num_trainings)
    You have high enough kiwi level for another training.
    
    [+1 Trainings]
    *set exploration_trainings + 1
    *set layer_2_kiwi_trainer_num_trainings + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_monument

*set diary ("I'm so inspired to build a huge monument on top of this mountain.[n/][n/]" & diary)

*comment TODO: Put acorn up here
*comment TODO: Something to do with controlling the weather here?

You trudge up a large mountain, and look out to an amazing view. You're inspired to build a monument here, but you'll need more stamina to haul the stones up, plus you'll need monumental_stones, which are special stones that actually can't be found in the Tunnel. Maybe if you get the monument set up, though, the gods will smile favorably upon you.

*if ((inventory_map_kit) and (layer_2_monument_mountain_name = ""))
    What would you like to call this place?
    
    *input_text layer_2_monument_mountain_name

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_starblossom

*if (not (knowledge_starblossom))
    *set diary ("I think I heard a voice from beyond the fourth wall yelling something? It sounded like 'damnit! Those were starblossoms! I dunno what they were talking about though.[n/][n/]" & diary)
    
    You see some flowers shaped like stars. You should probably know what these are by now, but you don't…
    
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("Found a starblossom patch.[n/][n/]" & diary)

You find a starblossom patch.

*if (inventory_shtg2)
    *set diary ("I was even able to harvest from it cuz I got the SHTG2.[n/][n/]" & diary)
    
    With your SHTG2, you're able to harvest a couple.
    
    [+2 Starblossoms]
    *set inventory_starblossom + 2
*if (not (inventory_shtg2))
    *set diary ("Unfortunately, I could not harvest from the patch, which made me very sad.[n/][n/]" & diary)
    
    You, unfortunately, don't have the SHTG2 to harvest the starblossom.
    
*goto_scene {home_base_scene} {home_base_label}

*label layer_2_spiders

*set diary ("I came across a spooky spider nest. I hope I don't get eaten... these things can be vicious.[n/][n/]" & diary)

You're wandering along and you come across a denser forest with a canopy that blocks out all light. Soon enough, you start to see webs, massive webs, strewn all over. You don't believe it at first since the light levels are so low, but after looking more carefully, you see that, yes, these are indeed spider webs.

This must be a Giant Spider nest… You thought those were only in layer 3 and beyond, but George did tell you that sometimes they migrate a little too close to home. You turn around to head back. You have to remain calm, since Giant Spiders mainly sense their prey via smelling their fear.

*if (george_gone)
    The sudden thought of George makes you sad, so you push the thought aside. You have to focus.

*if (((met_troy_general_store) and (not (saved_troy_orphanage))) and (months_passed < troy_orphaned_month))
    You turn around when you here screams behind you, a man and a woman. Do you go help them?
    
    *choice
        #Yes
            *goto layer_2_spiders_help
        #No
            *goto_scene {home_base_scene} {home_base_label}

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_spiders_help

You were eaten by spiders. Jk, but I haven't written this part yet.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_battleground

*if (not (unlocked_squirrel_war))
    You find a big open field. It sorta resembles a battleground for squirrels. Battleground for squirrels? What are you on about. You continue on and don't find much else.
    
    *goto_scene {home_base_scene} {home_base_label}

*if (layer_2_battleground_charles_battle_occurred)
    *goto layer_2_battleground_battle_2

*set diary ("I saw this huuuuge battle today.[n/][n/]" & diary)

*set diary ("Ohmygosh Fred is EVIL???[n/][n/]" & diary)

*set layer_2_battleground_charles_battle_occurred true

You come across a massive field. On one side, you see a massive line of squirrels, thousands strong. And on the other, you see, also a massive line of squirrels. You've never seen so many squirrels before. It kind of scares you.

*temp supported_fred false

*if (squirrel_affiliation_alice = "charles")
    You can spot Alice at the forefront of some squirrels in Charles' line. They wear the insignia of a bushy tail on their shields, and their spears are almost as long as your leg. How do they hold such long spears? They actually look lowkey ferocious, not gonna lie.
*if (squirrel_affiliation_alice = "fred")
    You can spot Alice at the forefront of some squirrels in Fred's line. They wear the insignia of a bushy tail on their shields, and their spears are almost as long as your leg. How do they hold such long spears? They actually look lowkey ferocious, not gonna lie.
    
    *set supported_fred true

*if (squirrel_affiliation_max = "charles")
    You see a wave of red in the back of Charles' line led by none other than Max.
*if (squirrel_affiliation_max = "fred")
    You see a wave of red in the back of Fred's line led by none other than Max.
    
    *set supported_fred true
    
*if (squirrel_affiliation_deadjamin = "charles")
    Hundreds of skeletal squirrels form the main force to Charles' army. All led by the ghost squirrel deadjamin himself.
*if (squirrel_affiliation_deadjamin = "fred")
    Hundreds of skeletal squirrels form the main force to Fred's army. All led by the ghost squirrel deadjamin himself.
    
    *set supported_fred true

*if (squirrel_affiliation_rory = "charles")
    To the side of Charles' army, you see a lone figure in chainsquirrelplate armor. Rory.

The squirrels march on each other.
    
*comment Fred loses ties
*if (squirrel_strength_fred > squirrel_strength_charles)
    *set squirrel_charles_defeated true
    
    The battle is close, but Fred comes out on top. When Charles sees that he has lost, he calls on his armies to surrender. Charles takes off his squirrelmet and shouts out, "Son, I hope you can forgive me. I no longer ask to defeat Edmington. I just ask that we can be together again."
    
    Fred nods and you see his armies part to allow Charles through. Charles smiles, his arms outstretched and walks to the circle of guards and royal attendants surrounding his son. "Son, let's be a family again," he says as he's just outside the circle. Fred nods again and the guards close the circle with their pikes.
    
    "Behead the traitor!" He decrees.
    
    In a slick, fluid motion, one of the two guards at the front of the circle beheads Fred's Father, then brings it to his post. Fred holds up the head of the man, or, err, squirrel, who once fathered him. "Now kill his followers!" Fred declares. "Nothing will stop us on the way to Edmington!"
    
    He leaves no survivors.
    
    *if (squirrel_affiliation_alice = "charles")
        Alice's forces put their weapons up again and release their warrior cry only to be slaughtered by Fred's superior and better-organized forces.
        
        Alice herself is dragged away from the main army and between to death with wooden twigs.
        *set squirrel_alive_alice false
        
    *if (squirrel_affiliation_max = "charles")
        Max's forces squander what little time they have left trying to form a new defensive line before being efficiently encircled and destroyed.
        
        You witness Max himself die in the center of the circle, not from the weapons of his enemy as he always dreamed of, but by being crushed by his fellow soldiers backing up in fear.
        *set squirrel_alive_Max false
        
    *if (squirrel_affiliation_rory = "charles")
        Well, he kills almost all of them. You see Rory battling his way out of the circle, towards Fred.
        
        *choice
            #"Rory, no!"
                Rory gives you a quick look, and in that very moment is skewered by the spear of one of Fred's guard. Rory swings his might battle axe back, though, and decapitates his attacker. A few hundred swings later, he's made his way to the opposite side from Fred and escapes as the only survivor.
                
                Fred looks around for the person who made that voice (which would be you), but doesn't seem to find you.
            #"Say nothing"
                You speak the words "say nothing" out loud. Rory gives you a quick look, and in that very moment is skewered by the spear of one of Fred's guard. Rory swings his might battle axe back, though, and decapitates his attacker. A few hundred swings later, he's made his way to the opposite side from Fred and escapes as the only survivor.
                
                Fred looks around for the person who made that voice (which would be you), but doesn't seem to find you.
    
    *if (supported_fred)
        After the battle, Fred asks for all his followers to come to celebrate with him. You come out from your hiding spot. "Hello, I was one who enlisted squirrels to your cause," you point out anxiously.
        
        "Ah, yes!" Says Fred excitedly. Then, he bows, "Human, I could not have done this without you. My forces are yours to command. You only need build a battle planning room in your base in layer 1, and you can use this favor I'm about to give you to enlist my army against your foes. Some foes may require larger forces or bigger planning rooms to fight."
        
        [+Encyclopedia Page]
        *line_break
        [+1 Favor]
        *set squirrel_favors_fred + 1
    *if (not (supported_fred))
        Since you didn't support Fred for the battle, you decide it's best to silently slip away.
*if (squirrel_strength_fred <= squirrel_strength_charles)
    *set squirrel_charles_defeated true
    
    The battle is close, but Charles comes out on top. "Fred," he tells his son after he's been captured, "I'm sorry for how I acted. The power has gone to my head. Can we be father and son again?"
    
    "Of course," Fred responds. They hug. You smile. A paternal bond rekindled. Then you notice a red liquid streaming from Charles' back. Fred goes on, "And by 'of course', I mean, 'of course I was scheming against you this whole time.' You were right not to trust me. Now, I, as your son, am the rightful ruler of the squirrels of the fifth layer." Fred then throws Charles' limp body onto the ground, chops of his head, and holds it high for the rest of his army to see.
    
    All of Fred's followers cheer. "Now," says Fred, "murder those who supported the false king!" In the ensuing confusion, Fred gains the upper hand and destroys the late Charles' forces.
    
    *if (squirrel_affiliation_alice = "charles")
        *set squirrel_alive_alice false
        You quickly notice that Alice has been silently dragged out of her usual post at the front of her army in the confusion, and without their usual organization, her army is destroyed.
        
        Alice herself is beaten with twigs off a ways from her army, far away from her family and friend.s
    *if (squirrel_affiliation_max = "charles")
        Max's forces squander what little time they have left trying to form a new defensive line before being efficiently encircled and destroyed.
        
        You witness Max himself die in the center of the circle, not from the weapons of his enemy as he always dreamed of, but by being crushed by his fellow soldiers backing up in fear.
        *set squirrel_alive_Max false
    
    *if (squirrel_affiliation_rory = "charles")
        Well, he kills almost all of them. You see Rory battling his way out of the circle, towards Fred.
        
        *choice
            #"Rory, no!"
                Rory gives you a quick look, and in that very moment is skewered by the spear of one of Fred's guard. Rory swings his might battle axe back, though, and decapitates his attacker. A few hundred swings later, he's made his way to the opposite side from Fred and escapes as the only survivor.
                
                Fred looks around for the person who made that voice (which would be you), but doesn't seem to find you.
            #"Say nothing"
                You speak the words "say nothing" out loud. Rory gives you a quick look, and in that very moment is skewered by the spear of one of Fred's guard. Rory swings his might battle axe back, though, and decapitates his attacker. A few hundred swings later, he's made his way to the opposite side from Fred and escapes as the only survivor.
                
                Fred looks around for the person who made that voice (which would be you), but doesn't seem to find you.
    
    *if (supported_fred)
        After the battle, Fred asks for all his followers to come to celebrate with him. You come out from your hiding spot. "Hello, I was one who enlisted squirrels to your cause," you point out anxiously.
        
        "Ah, yes!" Says Fred excitedly. Then, he bows, "Human, I could not have done this without you. My forces are yours to command. You only need build a battle planning room in your base in layer 1, and you can use this favor I'm about to give you to enlist my army against your foes. Some foes may require larger forces or bigger planning rooms to fight."
        
        [+Encyclopedia Page]
        *line_break
        [+1 Favor]
        *set squirrel_favors_fred + 1
    *if (not (supported_fred))
        Since you didn't support Fred for the battle, you decide it's best to silently slip away.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_battleground_battle_2

Nothing much happens on your trip.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_tso

*if (layer_2_tso_found_note)
    Nothing much happens on your trip.
    
    *goto_scene {home_base_scene} {home_base_label}

*if (not (layer_1_tso_found_note))
    You pass by a note, but something tells you from the deep belly of the plot that you're meant to find another note in layer 1 first.
    
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("I found another note from George's father.[n/][n/]" & diary)

*set layer_2_tso_found_note true

"I hope someone finds this. I need them to deliver this to my son George, let him know I'm okay. There was an… accident with me and someone important a few days ago, someone I owe many debts. I need to get away, and the only way to escape is through this goddamned tunnel. I have to get out. Let him know that I love him and am still alive."

"I'm on my way to the further layers. But I'm badly hurt. Someone needs to send help. Watch out for the spiders.

— Tso

P.S. Tell George I'm proud of him."

You know if you show this note to George you won't be able to stop him from searching for his father. And, while George is a decent explorer and can easily survive in layers 2 and 3, who knows what'll happen to him if he needs to go further?

*choice
    #Show George
        *goto layer_2_tso_note_show_confirm
    #Don't Tell George
        *goto layer_2_tso_note_hide

*label layer_2_tso_note_show_confirm

Are you sure? This may not turn out well for George.

*choice
    #Yes, I'm sure
        *goto layer_2_tso_note_show
    #No
        *goto layer_2_tso_note_hide

*label layer_2_tso_note_show

*set george_departure_date months_passed

*set diary ("I miss George already.[n/][n/]" & diary)

"I knew it! He's still alive!" Shouts George when you show him the note back at his room in the orphanage. Then he immediately starts packing.

"Wait, you're leaving [i]now[/i]?" You ask.

"Of course. I can't just let my father die." He says, laughing and crying at the same time. His voice is hard to describe. You're afraid you might have offended him by even asking that question, but you have to make sure he's safe.

"Can I come with you?" You say.

*comment TODO: Allow character to depart if they have fifth layer pass somehow
"Psh, as if they'll allow you into the fifth layer and beyond."

"Uh, George, you're a pioneer pass holder."

"So?"

"That only gives you access to the fourth layer. They won't let you beyond that."

"Haha, I have my ways."

"And what if I have mine," you retort.

George looks at you, with the most serious face he can muster. "Don't follow me." He says, with a low, cold voice. "I love you Lauren." Then he pecks you on your nose. It hurts a little, just a bit, since he happens to kiss you just where the pimple you got a few days ago broke out. You barely notice the pain, honestly.

"Wait!" You cry out. But by that time, you've sat too long in shock. He's gone. You reach up and touch the zit, still wet.

No, you'll see him again one day. You know it.

*set george_gone true

*if ((not (unlocked_trainings)) and (unlocked_level_up_2))
    *page_break
    
    You didn't fucking talk to George about the yellow pass? Ugh, well, since you're so bad at this I guess you have to talk to me.
    
    There's this thing called trainings. You need them for the yellow pass for layer 3. Here's one. SIKE. That's what George would have said; he would have given you a training. Nope, not me. But you get to know what they are at least.
    
    [Encyclopedia Entry: Trainings]
    *set unlocked_trainings true

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_tso_note_hide

It's probably for the best.

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_artifact_dungeon

*set diary ("Wtf is an artifact dungeon.[n/][n/]" & diary)

You come across the ARTIFACT DUNGEON!!!

*temp experience_required 0
If you want a special artifact this time, you'll have to endure the following:
*if (layer_2_artifact_dungeon_level >= 1)
    *line_break
    Losing all your lemens. (Not including immortal lemens).
    *set experience_required 125
*if (layer_2_artifact_dungeon_level >= 2)
    *line_break
    Also, losing all your equipments.
    *set experience_required 175
*if (layer_2_artifact_dungeon_level >= 3)
    *line_break
    Also, losing all your doubloons.
    *set experience_required 275
*if (layer_2_artifact_dungeon_level >= 4)
    *line_break
    Also, your maximum energy is being decreased by 4 (requires at least 11 max energy).
    *set experience_required 325
*if (layer_2_artifact_dungeon_level >= 5)
    *line_break
    Also, losing all your experience.
    *set experience_required 475
*if (layer_2_artifact_dungeon_level >= 6)
    *line_break
    Also, losing all your residential properties.
    *set experience_required 550
*if (layer_2_artifact_dungeon_level >= 7)
    *line_break
    Also, kill off Ms. Bee.
    *set experience_required 665
*line_break
You'll also need to have at least ${experience_required} experience.

*if (experience < experience_required)
    You don't have enough experience, unfortunately. Return when you have more.
    
    *goto_scene {home_base_scene} {home_base_label}

Only continue if you are sure about losing everything.

*choice
    #No
        *goto_scene {home_base_scene} {home_base_label}
    #Continue into the dungeon
        *goto layer_2_artifact_dungeon_1

*label layer_2_artifact_dungeon_1

First, they took the lemens, and I was left without light.

*gosub_scene logic update_total_lemens
*temp lemens_in_inventory total_lemens
*set lemens_in_inventory - inventory_lemens_immortal

*choice
    #Stop
        *goto_scene {home_base_scene} {home_base_label}
    #Lose all ${lemens_in_inventory} mortal lemens in your inventory
        *goto layer_2_artifact_dungeon_2

*label layer_2_artifact_dungeon_2

[-${lemens_in_inventory} Lemens]
*set logic_lemens_to_remove lemens_in_inventory
*gosub_scene logic remove_lemens

*if (layer_2_artifact_dungeon_level = 1)
    *set layer_2_artifact_dungeon_level 2
    
    You receive the blessing of the Lemen of the Ten Fold Way. The sales price per lemen at the general store is now increased by one per ten lemens you have. (Normal price is about 4 Doubloons per Lemen).
    
    Up to nine times in a month, selling Lemens to the general store will increase this bonus by one. (That is, if you sell lemens nine times, and have 15 lemens left, the sales price would now be increased by 15 per lemen, or 150 per 10 lemens).
    
    [Acquired Artifact: Lemen Ten Fold Way]
    *line_break
    [Encyclopedia Entry: Lemen Ten Fold Way]
    *set artifact_has_lemen_ten_fold_way true
    
    *goto_scene {home_base_scene} {home_base_label}

Then, they took my equipments. I was stranded.

*choice
    #Why am I doing this?
        *goto_scene {home_base_scene} {home_base_label}
    #Lose all ${inventory_equipments} equipments in my possession
        *goto layer_2_artifact_dungeon_3

*label layer_2_artifact_dungeon_3

*if (layer_2_artifact_dungeon_level = 2)
    *set layer_2_artifact_dungeon_level 3
    
    You receive the equipments stocking device. You now get 10 equipments at the start of each year.
    
    [Acquired Artifact: Equipments Stocking Device]
    *set artifact_has_equipments_stocking_device true
    
    *goto_scene {home_base_scene} {home_base_label}

After that, it was my money, naturally.

*choice
    #Okay, I've had enough
        *goto_scene {home_base_scene} {home_base_label}
    #Lose all ${doubloons} doubloons in my possession
        *goto layer_2_artifact_dungeon_4

*label layer_2_artifact_dungeon_4

*if (layer_2_artifact_dungeon_level = 3)
    *set layer_2_artifact_dungeon_level 4
    
    You receive the Doubloon Doubler, which can be used once to double the number of doubloons you have. I recommend waiting a bit before using it.
    
    [Acquired Artifact: Doubloon Doubler]
    *set artifact_has_doubloon_doubler true
    
    Access the doubloon doubler from home base options.

Alright, the dungeon hasn't been built this far, unfortunately, so I'll just give you another use on your doubloon doubler.

[+1 Doubloon Doubler Charge]
*set artifact_uses_doubloon_doubler + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_temple_of_knowledge

*comment TODO

You come across the temple of knowledge, which confers various learning bonuses to you. Each time you come here, you get to choose one of the following bonuses. These will mostly only make sense if you've unlocked the town library.

*choice
    #Increase Research Speed by 20
        *set research_speed + 20
        *goto_scene {home_base_scene} {home_base_label}
    *if (layer_2_temple_of_knowledge_passes_unlocked < 3)
        #Get a new Library Pass to the Temple of Knowledge Library, which houses exclusive new books/researches. (Access it from the main layer 2 exploration menu).
            *set layer_2_temple_of_knowledge_passes_unlocked + 1
            *goto_scene {home_base_scene} {home_base_label}
    #Apply 500 Research Points to ALL books you're researching.
        *set logic_research_points_to_add 500
        *gosub_scene logic update_library_research
        *goto_scene {home_base_scene} {home_base_label}

*label layer_2_temple_of_knowledge_library

You arrive at the Temple of Knowledge. It's surrounded by jungle and leaves and vines, but the feeling of knowledge emanates so strongly from its cobblestone walls that it wasn't hard to find.

*if ((book_checked_out_knowledge_library_1_research_points_left = 0) and (not (book_checked_out_knowledge_library_1 = "nothing")))
    You return the book you checked out on your library pass 1.
    
    *set book_checked_out_knowledge_library_1 "nothing"
*if ((book_checked_out_knowledge_library_2_research_points_left = 0) and (not (book_checked_out_knowledge_library_2 = "nothing")))
    You return the book you checked out on your library pass 2.
    
    *set book_checked_out_knowledge_library_2 "nothing"
*if ((book_checked_out_knowledge_library_3_research_points_left = 0) and (not (book_checked_out_knowledge_library_3 = "nothing")))
    You return the book you checked out on your library pass 3.
    
    *set book_checked_out_knowledge_library_3 "nothing"

*choice
    #Read through the library catalog.
        The Library of Knowledge's catalog, unfortunately, needs special training to read.
        *goto layer_2_temple_of_knowledge_library
    #Check out a book on an unused library card
        *goto layer_2_temple_of_knowledge_library_check_out
    #Return an unfinished book (and keep none of the progress in it)
        *goto layer_2_temple_of_knowledge_library_unfinished_return
    *selectable_if (energy >= 21) #Use the Library of Knowledge for an intensive reading sesh [+1 Month, -5 Energy, requires 21 Energy, +300 Research Points to ALL books you're researching
        *set logic_research_points_to_add 300
        *gosub_scene logic update_library_research
        
        *gosub_scene logic month_passed
        
        [-5 Energy]
        *set energy - 5
        
        *goto_scene {home_base_scene} {home_base_label}
    #Back home
        *goto_scene {home_base_scene} {home_base_label}

*label layer_2_temple_of_knowledge_library_check_out

TODO

*goto layer_2_temple_of_knowledge_library

*label layer_2_temple_of_knowledge_library_unfinished_return

TODO

*goto layer_2_temple_of_knowledge_library

*label layer_2_coin_pile

You find a huge pile of 2000 Doubloons.

[+2000 Doubloons]
*set doubloons + 2000

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_mother_of_specters

*if (layer_2_mother_of_specters_fight_won)
    *goto layer_2_mother_of_specters_flower_field

You come across a flower guardian in a patch of flowers.

*choice
    #Fight
        *goto layer_2_mother_of_specters_fight
    #Run away
        *goto_scene {home_base_scene} {home_base_label}

*label layer_2_mother_of_specters_fight

*set logic_fight_encounter "flower_guardian"
*gosub_scene logic fight

*if (logic_fight_result = "enemy_won")
    Sadly, you were defeated and receive no rewards on this exploration journey.
    
    *goto_scene {home_base_scene} {home_base_label}

*set layer_2_mother_of_specters_fight_won true

You win!

[+20 Experience]
*line_break
[+12 Rosalia]
*set experience + 20
*set inventory_rosalia + 12

*goto layer_2_mother_of_specters_flower_field

*label layer_2_mother_of_specters_flower_field

You walk through a field of yellow flowers with short, yellow petals and a buttery scent to them.

*if (not (knowledge_mother_of_specters))
    You have no idea what these are, so you just go back home.
    
    *comment Wtf is this formatting. Fix.
    <Need Botany II Research>
    
    *goto_scene {home_base_scene} {home_base_label}

These are Mother of Specters. One of the deadliest plants in the Tunnel. Time for harvesting.

[+3 Mother of Specters]
*line_break
[+1 Mother of Specters Fetch Skill]
*set inventory_mother_of_specters + 3
*set skill_fetch_mother_of_specters + 1

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_scrolls

*set diary ("Woah, some farmhand experience![n/][n/]" & diary)

You find a scroll with secret knowledge on being an incredible farmhand.

[+15 Farmhand Experience]
*set job_experience_farm_hand + 15

*goto_scene {home_base_scene} {home_base_label}

*comment ////////////////////////////////////////////////////////////
*comment // General exploration encounters are ABOVE this
*comment ////////////////////////////////////////////////////////////

*comment TODO: This isn't linked... fix!

*label layer_2_fight

*if (layer_2_fight_won)
    Nothing really happens.
    
    *goto_scene {home_base_scene} {home_base_label}

*if (not (can_fight))
    Woah, woah, buddy, this is a fighting encounter and you haven't unlocked that yet! Buzz off!
    
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("Oh no a dingrit![n/][n/]" & diary)

You see a dingrit, a small monster, snarling at a child, no older than 150 months. What is a child doing here?

*choice
    #Fight (opponent is 1 dingrit)
        *goto general_exploration_layer_2_fight_fight
    #Leave
        *goto_scene {home_base_scene} {home_base_label}

*label general_exploration_layer_2_fight_fight

You start a fight against a single dingrit.

*set logic_fight_encounter "single_dingrit"
*gosub_scene logic fight

*if (logic_fight_result = "dingrit_won")
    *set diary ("I'll get you one day, dingrit. *holds up balled fist*[n/][n/]" & diary)
    
    You run far away from the dingrit. Next time you'll get 'em.
    *goto_scene {home_base_scene} {home_base_label}

*set diary ("I wonder what that child is on about.[n/][n/]" & diary)

*set layer_2_fight_won true

*if (not (logic_fight_result = "dingrit_annihilated"))
    As the dingrit flees, you turn to find the child crying. "What's wrong?" You ask.
*if (logic_fight_result = "dingrit_annihilated")
    You stare at the child over the bloodied corpse of the dingrit you just slew in front of him and ask, "What's wrong?"

"The dragon killed the city!" Repeats the child. "The dragon killed the city!"

You wonder what this could mean. There've been no dragon sightings before the third layer in your lifetime, and even the one that did occur in the third layer while you were still a baby was really, [i]really[/i] big news. Having someone dislocated in the main dome because of a dragon… well… that would be concerning to say the least.

"Say, kiddo, which part of the Dome do you come from."

*comment Kid mispronounces on purpose (should be City of Emeralds)
The child goes silent and frowns at you. Then pouts, "The Dome is evil, I'm from the City of Esmerald." You frown, you've never heard of that location before.

"Here, come back with me to the dome an…" You start before the child runs away.

"NO DOME. NO DOME." He screeches.

Well, there goes that child's chances of survival.

*comment TODO: Further options to save the child

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_special_dirt

*set diary ("Today I fetched some dirt. Okay, I know that sounds weird, but listen up, you see, in this game there's a shop that buys dirt from you... Okay nvm.[n/][n/]" & diary)

You go out and collect some of layer 2's famed Special Dirt. It seems like it's everywhere.

*if (not (inventory_shovel))
    The problem is scooping it into your bag. If only you had a special tool for this.

[+11 Special Dirt]
*set inventory_special_dirt + 11

*if (inventory_shovel)
    Good thing you have your Double hovel with you on this mission, because you just got double the Special Dirt.
    
    [+11 Special Dirt]
    *set inventory_special_dirt + 11

*goto_scene {home_base_scene} {home_base_label}

*label layer_2_the_boss

You come across a giant subway sandwhich with meatballs and pepperoni. It's a "The Boss". You sense great rewards if you beat it, but you need to have good weaponry or have high energy to defeat it. Let's hope you do!

*set diary ("Fighting The Boss... Brings a new meaning to 'Let's get this bread.'[n/][n/]" & diary)

*gosub_scene logic fight_the_boss

*if (logic_fight_result = "the_boss_won")
    You flee from battle.
    
    *goto_scene {home_base_scene} {home_base_label}

*set layer_2_the_boss_defeated true

*set diary ("Mhmmm, this sandwhich is tasty.'[n/][n/]" & diary)

You've barely finished eating the last bit of marinara when you notice a huge pile of Doubloons and Experience behind The Boss's remains (well, where the remains would be if you hadn't eaten them).

[+1200 Doubloons]
*line_break
[+100 Experience]
*set doubloons + 1200
*set experience + 100

*goto_scene {home_base_scene} {home_base_label}

*comment ////////////////////////////////////////////////////////////
*comment // Layer 3 TAG_LAYER_3
*comment ////////////////////////////////////////////////////////////